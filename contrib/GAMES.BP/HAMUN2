*MODIFIED MANU GAME
     PRINT @(-1);PRINT "      WELCOME TO MESOPOTAMIA"
     PRINT
     PRINT 'TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA.'
     PRINT 'THE OBJECT OF THIS GAME IS TO TRY TO STAY IN OFFICE FOR SOME MONTHS.'
     PRINT 'HOW MANY MONTHS DO YOU WISH TO TRY':;INPUT TERM
     IF TERM = 'I' THEN TERM = -1
     IF TERM = '' THEN TERM = 20
     PRINT
11   PRINT 'DO YOU WANT THE COMPLEXITY OF MAINTAINING AN ARMY':; INPUT Q
     ARMY = 0; SOLD=0
     IF Q EQ 'NO' THEN GOTO 110
     ARMY = 1
     IF Q NE 'YES' THEN PRINT '"YES" ASSUMED...'
110  PRINT
     PRINT @(-1);PRINT @(1,1):'PEOPLE REQUIRE 20 BUSHELS PER MONTH TO LIVE.'
     PRINT ' ONE PERSON CAN HARVEST 10 ACRES; YOU NEED 1/2 BUSHEL PER ACRE TO PLANT.'
     PRINT ' YOU MAY BUY OR SELL LAND EACH MONTH.'
     IF ARMY = 0 THEN GOTO 12
     PRINT ' TO MAINTAIN AN ARMY EACH MONTH, REQUIRES 5 BUSHELS PER SOLDIER.'
     PRINT ' YOUR SOLDIERS ARE ALMOST TWICE AS EFFICIENT AS THE INVADERS.'
     PRINT ' YOU WILL BE WARNED WHEN THE INVADERS ARE WITHIN 30 KM. OF THE CITY.'
     PRINT ' BUT THERE MAY BE A SNEAK ATTACK... BEWARE.':
     INPUT X; *PAUSE HERE
* INITIALIZE
12   D1 = 0; P1 = 0; D = 0; INVADERS = RND(10)+25; INVASION = RND(10)+25
     MS1 = "R#5";MS2 = "R#6"
     MONTH = 0; PEOPLE = 90+RND(16); H = 2100+RND(600); E = RND(200)+250; BUSHELS = H-E
     A = 1000; Y = INT(H/A); I = 5; Q = 10; SOL = 0; B = BUSHELS; DLTACR = 0
     GOTO 220
***************************************************
211  INPUT X
FOR J = 15 TO 22; PRINT @(0,J):STR(' ',75);NEXT J
     IF MONTH = 1 THEN
FOR J = 1 TO 10; PRINT @(0,J):STR(' ',75);NEXT J
       PRINT @(1,1):'-----------------------------------------------'
       PRINT @(1,2):'HAMURABI: I BEG TO REPORT TO YOU THAT IN MONTH ':MONTH
       PRINT @(5,4):'PEOPLE':@(15,4):'PLAGUE':@(25,4):'NEW':@(35,4):'SOLDIERS'
       PRINT @(5,5):'STARVED':@(15,5):'DEATHS':@(25,5):'ARRIVALS':@(35,5):'DIED'
     END;PRINT @(5,6):PS MS2:@(15,6):PD MS2:@(25,6):PA MS2:@(35,6):SD MS2
     IF MONTH = 1 THEN
       PRINT @(5,8):'BUSHELS':@(15,8):'RATS':@(25,8):'ACRES':@(35,8):'BUSHELS/':@(45,8):'BUSHELS'
       PRINT @(5,9):'FORWARD':@(15,9):'ATE':@(25,9):'PLANTED':@(35,9):'PER ACRE':@(45,9):'HARVESTED';END
     PRINT @(5,10):OBUSHELS MS2:@(15,10):E MS2:@(25,10):AP MS2:@(35,10):Y MS2:@(45,10):H MS2
     PEOPLE = PEOPLE+PA
     PRINT @(48,2):MONTH
220  LAND.PRICE = RND(10)+17; ** SELECT PRICE OF LAND
     NET.ACRES = A+(INT(BUSHELS/LAND.PRICE))
     IF MONTH = 0 THEN IACR = NET.ACRES ELSE DLTACR = NET.ACRES-IACR/IACR/MONTH
     IF MONTH = 0 THEN
       PRINT @(5,12):'POPULATION':@(15,12):'  ACRES':@(25,12):'BUSHELS':@(35,12):'LAND PRICE':@(47,12):'NET ACRES':@(57,12):'ACRE CHANGE';END
     PRINT @(5,13):PEOPLE MS2:@(16,13):A MS2:@(25,13):BUSHELS MS2:@(35,13):LAND.PRICE MS2:@(47,13):NET.ACRES MS2:@(57,13):DLTACR MS2
     MONTH=MONTH +1
     IF MONTH = TERM THEN GOTO 860; * END OF TERM
     IF ARMY # 0 THEN
       IF INVASION <= 25+RND(10) THEN
         PRINT @(5,15):'THERE ARE NOW ':INVADERS:' INVADERS ':INVASION:' KILOMETERS FROM HERE'
     END; END
     PRINT @(1,17):'HOW MANY ACRES DO YOU WANT TO BUY ':
320  PRINT @(36,17):STR(' ',10):@(36,17):;INPUT Q; IF TERM = -1 AND Q = 'X' THEN GOTO 860
     IF NOT(NUM(Q)) OR Q<0 THEN GOTO 320
     IF LAND.PRICE*Q>BUSHELS THEN
       PRINT @(1,22):STR(' ',75):@(1,22):'HAMURABI: THINK AGAIN, YOU OWN ONLY ':BUSHELS:' BUSHELS'
       GOTO 320;END
     IF Q#0 THEN
       A = A+Q; BUSHELS=BUSHELS-LAND.PRICE*Q; C = 0; GOTO 400; END
     PRINT @(31,17):'SELL ':
340  PRINT @(36,17):STR(' ',10):@(36,17):;INPUT Q; IF Q<0 THEN GOTO 340
     IF Q>=A THEN
       PRINT @(1,22):STR(' ',75):@(1,22):'TRY AGAIN; YOU ONLY HAVE ':A MS1:' ACRES'
       GOTO 340;END
     A = A-Q; BUSHELS = BUSHELS+LAND.PRICE*Q; C=0
400  PRINT @(16,13):A MS2:@(25,13):BUSHELS MS2
     PRINT @(1,17):'HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE ':
410  PRINT @(50,17):;INPUT Q; IF Q<0 THEN GOTO 410
     IF Q > BUSHELS THEN
       PRINT @(1,22):STR(' ',75):@(1,22):'THINK AGAIN; YOU HAVE ONLY ':BUSHELS MS1:' BUSHELS OF GRAIN!'
       GOTO 410;END
     BUSHELS = BUSHELS-Q; C=1
     IF Q/20 < PEOPLE THEN PRINT @(59,17):'** KILLER!!'
     PRINT @(25,13):BUSHELS MS2
     PRINT @(1,17):STR(' ',60):@(1,17):'HOW MANY ACRES DO YOU WANT TO PLANT ':
440  PRINT @(37,17):STR(' ',10):@(37,17):;INPUT D; IF D=0 THEN GOTO 5100
     IF D<0 THEN GOTO 440
     IF D<=A THEN GOTO 450
     PRINT @(1,22):STR(' ',75):@(1,22):'TRY AGAIN; YOU ONLY HAVE ':A MS1:' ACRES'
     GOTO 440
450  IF D/2 <= BUSHELS THEN GOTO 455
     PRINT @(1,22):STR(' ',75):@(1,22):'BUT, HAMURABI, YOU HAVE ONLY ':BUSHELS:' BUSHELS OF GRAIN!'
     GOTO 440
455  IF D-1<10*PEOPLE THEN GOTO 510
460  PRINT @(1,22):STR(' ',75):@(1,22):'BUT YOU ONLY HAVE ':PEOPLE:' PEOPLE TO TEND THE FIELDS!'
     GOTO 440
510  BUSHELS = BUSHELS-INT(D/2)
     AP=D; * AP => ACRES PLANTED
     PRINT @(25,13):BUSHELS MS2
5100 IF ARMY = 0 THEN GOTO 511
     PRINT @(1,17):'HOW MANY SOLDIERS ARE TO BE IN YOUR ARMY ':STR(' ',10)
5101 PRINT @(42,17):STR(' ',10):@(42,17):;INPUT SOL; IF SOL < 0 THEN GOTO 5101
     IF SOL > PEOPLE-D/10 THEN PRINT @(1,22):STR(' ',75):@(1,22):'BUT WITH ':D/10:' PEOPLE TENDING FIELDS, YOU HAVE ONLY ':PEOPLE-D/10:' PEOPLE LEFT!'; GOTO 5101
     IF SOL*5 > BUSHELS THEN PRINT @(1,22):STR(' ',75):@(1,22):'ONLY ':BUSHELS:' BUSHELS LEFT, HAMU....'; GOTO 5101
     BUSHELS = BUSHELS-SOL*5
511  PRINT @(1,17):STR(' ',75):@(1,22):STR(' ',75):@(1,16)
****************** HARVEST ***
     C = RND(20)
     Y = 1 + RND(6); ** HARVEST YIELD
     IF C  >= 18 THEN PRINT '** A FRIENDLY WITCH HAS WAVED HER WAND OVER YOUR FIELDS!'; Y = 7
     H = D*Y; E = 0
     IF Y > 3 THEN PRINT '** A BOUNTIFUL HARVEST!.'
     IF Y < 2 THEN PRINT '** SPARSE RAINFALL HAS KILLED THE HARVEST.'
****************** RATS ****
     C = 1+RND(6)-INT(BUSHELS/3000)*1-INT(BUSHELS/6000)*1
     IF C <= 2 THEN
       C = RND(4)+3-INT(BUSHELS/6000)*1; * RATS EATING
       E=INT(BUSHELS/C); * RATS ATE SO MANY BUSHELS
       PRINT '** RATS ON THE MOVE!!'; END
     OBUSHELS=BUSHELS;BUSHELS = BUSHELS-E+H; * NEW AMOUNT OF GRAIN
***************** INCOMING PEOPLE
     C = 1+RND(5)
     XX = RND(7)-4; IF XX < 0 THEN XX = 0
     PA = INT(C*(20*A+BUSHELS)/PEOPLE/100 +1 +XX*30)
     IF XX # 0 THEN PRINT '** AN INFLUX OF REFUGEES!!'
     D=PEOPLE-INT(Q/20); * DEAD THIS MONTH
     IF D<0 THEN D=0
     P1=INT(P1+D*100/PEOPLE/MONTH)
     PEOPLE=PEOPLE-D; D1=D1+D; PS=D; * PS -> PEOPLE STARVED
     IF P1 > RND(10)+5 THEN GOTO 560
     SOLD = 0;SD=0
     IF ARMY = 0 THEN GOTO 720
**************** TEST INVADERS VS. ARMY
     INVASION = INVASION - (RND(25)+10); IF INVASION < 0 THEN INVASION = 0
     IF INVASION > RND(5)+7 THEN GOTO 720
     PRINT '** THE CITY HAS BEEN ATTACKED.'
     X1 = RND(2)+1; X2 = X1 + (RND(2)+1)
     IF SOL*X2 < INVADERS THEN PRINT '** THE CITY HAS FALLEN, SINCE YOU ONLY HAD ':SOL:' SOLDIERS DEFENDING IT.'; GOTO 999
     IF SOL*X1 < INVADERS THEN GOTO 710
     PRINT '** BUT THE ATTACK HAS BEEN SUCCSSFULLY REPULSED BY YOUR VALIANT ARMY.'
     INVASION = 50+RND(20);  ** CLEAR INVASION
     GOTO 712
710  INVADERS = (INVADERS - SOL*X1)
     PRINT '** A PITCHED BATTLE IS TAKING PLACE'
712  SOLD = INT(INVADERS/X2)
     SOL = SOL - SOLD; PEOPLE = PEOPLE - SOLD; SD = SOLD; * SD -> SOLDIERS DIED.
***************** TEST IF PLAGUE HAS OCCURRED **
720  Q = RND(12);PD=0
     IF Q <= 3 THEN
       Q = RND(5)*10+10; * SELECT PERCENTAGE DEATHS
       PRINT '** A HORRIBLE PLAGUE HAS STRUCK; ':Q:'% OF THE POPULATION HAS DIED.'
       PD = INT(PEOPLE*Q/100); * PD -> PLAGUE DEATHS.
       PEOPLE = PEOPLE - PD; END
     GOTO 211
*******************************************
570  FOR J = 1 TO 3; PRINT @(1,J):STR(' ',75);NEXT J; J=1;RETURN
*******************************************
999  GOSUB 570;GOTO 998
560  GOSUB 570;PRINT @(1,J):'YOU STARVED ':D:' PEOPLE THIS MONTH!';J=J+1
998  PRINT @(1,J):'DUE TO EXTREME MIS-MANAGEMENT, YOU HAVE BEEN REMOVED FROM OFFICE!'
860  FOR J = 15 TO 22; PRINT @(0,J):STR(' ',75);NEXT J; PRINT @(1,14)
     PRINT 'YOU MANAGED TO STAYED IN OFFICE FOR ':MONTH:' MONTHS!!!'
     PRINT 'DURING YOUR TERM, ':P1:' PERCENT OF THE POPULATION'
     PRINT 'DIED OF STARVATION PER MONTH; A TOTAL OF ':D1:' PEOPLE DIED.'
     IF PEOPLE<1 THEN PRINT 'YOU KILLED EVERYONE, YOU GET THE ZPG AWARD.' ELSE
       L = INT((A+1+((BUSHELS-B)/LAND.PRICE))/PEOPLE)
       PRINT 'YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH ':L:
       PRINT ' ACRES PER PERSON,'
       PRINT 'AFTER ADJUSTING THE CURRENT SUPPLY OF WHEAT AGAINST THE ORIGINAL.'
       IF P1 > 3 THEN PRINT 'YOU ARE AN EXTREMELY MALEVOLENT RULER!' ELSE
         IF P1<1 THEN PRINT 'YOUR BENEVOLENCE IS APPRECIATED!' ELSE
           PRINT 'YOUR ATTITUDE TOWARD THE POPULACE COULD BE IMPROVED.'
     END;END;END
1000 PRINT @(1,22):'TRY AGAIN (Y/N) ':
     INPUT Q; IF Q NE 'N' THEN PRINT @(-1); GOTO 110 ELSE STOP
END
