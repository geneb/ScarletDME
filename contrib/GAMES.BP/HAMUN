*ANOTHER HAMU
     PRINT @(-1);PRINT "      WELCOME TO MESOPOTAMIA"
     PRINT
     PRINT 'TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA.'
     PRINT 'THE OBJECT OF THIS GAME IS TO TRY TO STAY IN OFFICE FOR SOME MONTHS.'
     PRINT 'HOW MANY MONTHS DO YOU WISH TO TRY':;INPUT TERM
     IF TERM = 'I' THEN TERM = -1
     PRINT
11   PRINT 'DO YOU WANT THE COMPLEXITY OF MAINTAINING AN ARMY':; INPUT Q
     ARMY = 0; SOLD=0
     IF Q EQ 'NO' THEN GOTO 110
     ARMY = 1
     IF Q NE 'YES' THEN PRINT '"YES" ASSUMED...'
110  PRINT
     PRINT;PRINT 'PEOPLE REQUIRE 20 BUSHELS PER MONTH TO LIVE.'
     PRINT 'ONE PERSON CAN HARVEST 10 ACRES; YOU NEED 1/2 BUSHEL PER ACRE TO PLANT.'
     PRINT 'YOU MAY BUY OR SELL LAND EACH MONTH.'
     IF ARMY = 0 THEN GOTO 12
     PRINT 'TO MAINTAIN AN ARMY EACH MONTH, REQUIRES 5 BUSHELS PER SOLDIER.'
     PRINT 'YOUR SOLDIERS ARE ALMOST TWICE AS EFFICIENT AS THE INVADERS.'
     PRINT 'YOU WILL BE WARNED WHEN THE INVADERS ARE WITHIN 30 KM. OF THE CITY.'
     PRINT 'BUT THERE MAY BE A SNEAK ATTACK... BEWARE.'
     INPUT X; *PAUSE HERE
* INITIALIZE
12   D1 = 0; P1 = 0; D = 0; INVADERS = RND(10)+25; INVASION = RND(10)+25
     MS1 = "R#5"
     MONTH = 0; PEOPLE = 90+RND(16); H = 2100+RND(600); E = RND(200)+250; BUSHELS = H-E
     A = 1000; Y = INT(H/A); I = 5; Q = 10; SOL = 0; B = BUSHELS
     PRINT @(-1);PRINT;PRINT;GOTO 220
***************************************************
211  INPUT X; PRINT @(-1); PRINT;PRINT
     PRINT '-----------------------------------------------'
     PRINT 'HAMURABI: I BEG TO REPORT TO YOU,'
     PRINT 'IN MONTH ':MONTH:':  ':D:' PEOPLE STARVED, ':I:' CAME TO THE CITY':
     IF ARMY # 0 THEN IF SOLD > 0 THEN PRINT ',  ':SOLD:' SOLDIERS DIED':
     PRINT;PRINT
     PEOPLE = PEOPLE+I
220  LAND.PRICE = RND(10)+17; ** SELECT PRICE OF LAND
     PRINT 'POPULATION IS NOW ':PEOPLE MS1
     PRINT 'THE CITY OWNS     ':A MS1:'    ACRES'
     IF MONTH # 0 THEN
       PRINT 'YOU HARVESTED     ':Y MS1:'    BUSHELS PER ACRE':
       IF E # 0 THEN PRINT ' BUT RATS ATE ':E:' BUSHELS':
     PRINT;END;MONTH=MONTH+1
     PRINT 'YOU NOW HAVE      ':BUSHELS MS1:'    BUSHELS IN STORE'
     IF MONTH = TERM THEN GOTO 860; * END OF TERM
     PRINT 'LAND PRICE IS     ':LAND.PRICE MS1:'    BUSHELS PER ACRE'
     IF ARMY = 0 THEN GOTO 2112
     IF INVASION > 25+RND(10) THEN GOTO 2112
     PRINT'THERE ARE NOW     ':INVADERS MS1:' INVADERS, ':INVASION:'  KILOMETRES FROM HERE'
2112 PRINT
320  PRINT 'HOW MANY ACRES DO YOU WANT TO BUY ':
     INPUT Q; IF Q<0 THEN GOTO 320
     IF LAND.PRICE*Q>BUSHELS THEN
       PRINT 'HAMURABI: THINK AGAIN, YOU OWN ONLY ':BUSHELS:' BUSHELS'
       GOTO 320;END
     IF Q#0 THEN
       A = A+Q; BUSHELS=BUSHELS-LAND.PRICE*Q; C = 0
       PRINT '** ':BUSHELS:' BUSHELS AND ':A MS1:' ACRES NOW'
       GOTO 400;END
340  PRINT 'HOW MANY ACRES DO YOU WANT TO SELL ':
     INPUT Q; IF Q<0 THEN GOTO 340
     IF Q>=A THEN
       PRINT 'TRY AGAIN; YOU ONLY HAVE ':A MS1:' ACRES'
       GOTO 340;END
     A = A-Q; BUSHELS = BUSHELS+LAND.PRICE*Q; C=0
     PRINT '** ':BUSHELS MS1:' BUSHELS AND ':A MS1:' ACRES NOW'
400  REM
410  PRINT 'HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE ':
     INPUT Q; IF Q<0 THEN GOTO 410
420  IF Q > BUSHELS THEN
       PRINT 'THINK AGAIN; YOU HAVE ONLY ':BUSHELS MS1:' BUSHELS OF GRAIN!'
       GOTO 410;END
     BUSHELS = BUSHELS-Q; C=1
     IF Q/20 < PEOPLE THEN PRINT '** KILLER!!'
     PRINT '** ':BUSHELS MS1:' BUSHELS NOW'
440  PRINT 'HOW MANY ACRES DO YOU WANT TO PLANT ':
     INPUT D; IF D=0 THEN GOTO 5100
     IF D<0 THEN GOTO 440
     IF D<=A THEN GOTO 450
     PRINT 'TRY AGAIN; YOU ONLY HAVE ':A MS1:' ACRES'
     GOTO 440
450  IF D/2 <= BUSHELS THEN GOTO 455
     PRINT 'BUT, HAMURABI, YOU HAVE ONLY ':BUSHELS:' BUSHELS OF GRAIN!'
     GOTO 440
455  IF D-1<10*PEOPLE THEN GOTO 510
460  PRINT 'BUT YOU ONLY HAVE ':PEOPLE:' PEOPLE TO TEND THE FIELDS!'
     GOTO 440
510  BUSHELS = BUSHELS-INT(D/2)
     PRINT '** ':BUSHELS:' BUSHELS NOW'
5100 IF ARMY = 0 THEN GOTO 511
5101 PRINT 'HOW MANY SOLDIERS ARE TO BE IN YOUR ARMY ':
     INPUT SOL
     IF SOL < 0 THEN GOTO 5101
     IF SOL > PEOPLE-D/10 THEN PRINT 'BUT WITH ':D/10:' PEOPLE TENDING FIELDS, YOU HAVE ONLY ':PEOPLE-D/10:' PEOPLE LEFT!'; GOTO 5101
     IF SOL*5 > BUSHELS THEN PRINT 'ONLY ':BUSHELS:' BUSHELS LEFT, HAMU....'; GOTO 5101
     BUSHELS = BUSHELS-SOL*5
511  PRINT
****************** HARVEST ***
     C = RND(20)
     Y = 1 + RND(6); ** HARVEST YIELD
     IF C  >= 18 THEN PRINT '** A FRIENDLY WITCH HAS WAVED HER WAND OVER YOUR FIELDS!'; Y = 7
     H = D*Y; E = 0
     IF Y > 3 THEN PRINT '** A BOUNTIFUL HARVEST!.'
     IF Y < 2 THEN PRINT '** SPARSE RAINFALL HAS KILLED THE HARVEST.'
****************** RATS ****
     C = 1+RND(6)-INT(BUSHELS/3000)*1-INT(BUSHELS/6000)*1; IF C < 1 THEN C=1
     IF C > 2 THEN GOTO 530; * RATS UNDER CONTROL
     C = RND(4)+3-INT(BUSHELS/6000)*1
     E=INT(BUSHELS/C); * RATS ATE SO MANY BUSHELS
     PRINT '** RATS ON THE MOVE!!'
530  BUSHELS = BUSHELS-E+H; * NEW AMOUNT OF GRAIN
***************** INCOMING PEOPLE
     C = 1+RND(5)
     BIRTHS = RND(5)
     XX = RND(7)-4; IF XX < 0 THEN XX = 0
     I = INT(C*(20*A+BUSHELS)/PEOPLE/100 +1 +XX*30)
     IF XX # 0 THEN PRINT '** AN INFLUX OF REFUGEES!!'
     D=PEOPLE-INT(Q/20); * DEAD THIS MONTH
     IF D<0 THEN D=0
     P1=INT(P1+D*100/PEOPLE/MONTH)
     PEOPLE=PEOPLE-D; D1=D1+D
     IF P1 > RND(10)+5 THEN GOTO 560
     SOLD = 0
     IF ARMY = 0 THEN GOTO 720
**************** TEST INVADERS VS. ARMY
     INVASION = INVASION - (RND(25)+10); IF INVASION < 0 THEN INVASION = 0
     IF INVASION > RND(5)+7 THEN GOTO 720
     PRINT '** THE CITY HAS BEEN ATTACKED.'
     X1 = RND(2)+1; X2 = X1 + (RND(2)+1)
     IF SOL*X2 < INVADERS THEN PRINT '** THE CITY HAS FALLEN, SINCE YOU ONLY HAD ':SOL:' SOLDIERS DEFENDING IT.'; GOTO 999
     IF SOL*X1 < INVADERS THEN GOTO 710
     PRINT '** BUT THE ATTACK HAS BEEN SUCCSSFULLY REPULSED BY YOUR VALIANT ARMY.'
     INVASION = 50+RND(20);  ** CLEAR INVASION
     GOTO 712
710  INVADERS = (INVADERS - SOL*X1)
     PRINT '** A PITCHED BATTLE IS TAKING PLACE'
712  SOLD = INT(INVADERS/X2)
     SOL = SOL - SOLD; PEOPLE = PEOPLE - SOLD
***************** TEST IF PLAGUE HAS OCCURRED **
720  Q = RND(12)
     IF Q > 3 THEN GOTO 211; * NO PLAGUE
     Q = RND(5)*10+10; * SELECT PERCENTAGE DEATHS
     PRINT '** A HORRIBLE PLAGUE HAS STRUCK; ':Q:'% OF THE POPULATION HAS DIED.'
     PEOPLE = PEOPLE - INT(PEOPLE*Q/100); ** DECREASE POPULATION
     GOTO 211
*******************************************
560  PRINT;PRINT;PRINT; PRINT 'YOU STARVED ':D:' PEOPLE THIS MONTH!'
999  PRINT 'DUE TO EXTREME MIS-MANAGEMENT, YOU HAVE BEEN REMOVED FROM OFFICE!'
     GOTO 870
1000 PRINT;PRINT;PRINT;PRINT 'TRY AGAIN (Y/N) ':
     INPUT Q; IF Q NE 'N' THEN GOTO 110 ELSE STOP
860  PRINT;PRINT;PRINT;PRINT
870  PRINT 'YOU MANAGED TO STAYED IN OFFICE FOR ':MONTH:' MONTHS!!!'
     PRINT 'DURING YOUR TERM, ':P1:' PERCENT OF THE POPULATION'
     PRINT 'DIED OF STARVATION PER MONTH; A TOTAL OF ':D1:' PEOPLE DIED.'
     IF PEOPLE<1 THEN PRINT 'YOU KILLED EVERYONE, YOU GET THE ZPG AWARD.'; GOTO 1000
     L = INT((A+1+((BUSHELS-B)/LAND.PRICE))/PEOPLE)
     PRINT 'YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH ':L:
     PRINT ' ACRES PER PERSON,'
     PRINT 'AFTER ADJUSTING THE CURRENT SUPPLY OF WHEAT AGAINST THE ORIGINAL.'
     PRINT
     IF P1 > 3 THEN PRINT 'YOU ARE AN EXTREMELY MALEVOLENT RULER!' ELSE
       IF P1<1 THEN PRINT 'YOUR BENEVOLENCE IS APPRECIATED!' ELSE
         PRINT 'YOUR ATTITUDE TOWARD THE POPULACE COULD BE IMPROVED.'
       END
     END
     GOTO 1000
END
