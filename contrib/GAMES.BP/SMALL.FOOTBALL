*    THIS IS A SMALL VERSION OF A FOOTBALL GAME.  EASY TO RUN AND SELF
*    EXPLANITORY.
     DIM M(6)
     DIM N(8)
     DIM NY(8)
     NUL=CHAR(0)
     FF=CHAR(12)
     BELL=CHAR(7)
     RESET=@(0,9) CAT STR(' ',56) CAT @(4,9)
     RESET2=@(30,9) CAT STR(' ',26) CAT @(30,9)
     CLEAN=@(0,11) CAT STR(' ',56)
     CLR=NUL CAT NUL CAT FF CAT NUL CAT NUL
     N(1)="ILLEGAL PROCEDURE CALLED AGAINST "
     N(2)="OFF-SIDES CALLED AGAINST "
     N(3)="PERSONAL FOUL CALLED AGAINST "
     N(4)="HOLDING CALLED AGAINST "
     N(5)="OFF-SIDES CALLED AGAINST "
     N(6)="TOO MANY MEN ON THE FIELD CALLED AGAINST "
     N(7)="PERSONAL FOUL CALLED AGAINST "
     N(8)="HOLDING CALLED AGAINST "
     NY(1)=-5
     NY(2)=-5
     NY(3)=-15
     NY(4)=-15
     NY(5)=5
     NY(6)=5
     NY(7)=15
     NY(8)=15
100  *
     PRINT CLR:@(0,3):"THERE ARE 3 WAYS OF PLAYING    F O O T B A L L  ":@(3,6):"1   PLAYER  VS  PLAYER":@(3,8):"2   PLAYER  VS  COMPUTER":
     PRINT @(3,10):"3   COMPUTER  VS  COMPUTER":@(0,13):"ENTER BY NUMBER THE TYPE OF GAME TO BE PLAYED  ":
     INPUT TYPE
     IF TYPE#1 THEN IF TYPE#2 THEN IF TYPE#3 THEN GOTO 100
     PRINT CLR:@(0,3):"HOW MANY MINUTES PER QUARTER WOULD YOU LIKE TO PLAY?":
     INPUT QTR
     IF TYPE=3 THEN A=RND(13)+1; GOSUB 300; TEAM2=A; GOTO 130
120  *
     PRINT CLR:@(0,3):"SELECT YOUR TEAM":
     PRINT @(3,5):"1  RAMS":@(3,6):"2  COWBOYS":@(3,7):"3  RAIDERS":@(3,8):"4  PACKERS":@(3,9):"5  EAGLES":
     PRINT @(3,10):"6  OILERS":@(3,11):"7  DOLPHINS":@(3,12):"8  STEELERS":@(3,13):"9  BRONCOS":@(2,14):"10  CHARGERS":@(2,15):"11  BUCANNEERS":@(2,16):"12  CARDINALS":
     PRINT @(2,17):"13  BENGALS":@(2,18):"14  VIKINGS":
     B=1
     PRINT @(0,20):
125  *
     IF TYPE=1 THEN PRINT "ENTER TEAM - ":B:"  ": ELSE PRINT "ENTER BY NUMBER":
     INPUT A
     IF A=0 THEN GOTO 120
     IF A>14 THEN GOTO 120
     GOSUB 300
     IF TYPE=2 THEN TEAM2=A; GOTO 130
     IF B=2 THEN TEAM2=A; GOTO 135
     TEAM1=A
     B=2
     GOTO 125
130  *
     A=RND(13)+1
     GOSUB 300
     TEAM1=A
135  *
     IF TEAM1 EQ TEAM2 THEN GOTO 130
     PRINT CLR:@(0,2):"TODAY'S GAME IS THE ":TEAM1:" AGAINST THE ":TEAM2:" .":
     PRINT @(0,4):"A FLIP OF THE COIN!  DO THE ":TEAM2:" WANT  1 - HEADS  OR  2 - TAILS?":
     INPUT A
     B=RND(2)+1
     IF A=B THEN NAME=TEAM2; H.TMDR=1; TMDR=2 ELSE NAME=TEAM1; H.TMDR=2; TMDR=1
     IF B EQ '1' THEN B=" HEADS"
     IF B EQ '2' THEN B=" TAILS"
     PRINT @(0,6):"THE COIN LANDS ":B:" UP":
     PRINT @(0,8):NAME:" WIN THE TOSS AND WILL RECEIVE!":
     IF TYPE=3 THEN GOTO 200
200  *
     DN=0; YDS=0; PYDS=0
     YTG=0
     PERIOD=0
     CLOCK=0
     SCORE1=0; SCORE2=0
     RUSH1=0;  RUSH2=0
     PASS1=0;  PASS2=0
     TOFF1=0;  TOFF2=0
     TPLAY1=0; TPLAY2=0
     TDNS1=0;  TDNS2=0
     COMPL1=0; COMPL2=0
     AYP1=0;   AYP2=0
     PENTL1=0; PENTL2=0
     NPASS1=0; NPASS2=0
     CPASS1=0; CPASS2=0
     NPUNT1=0; NPUNT2=0
     YPUNT1=0; YPUNT2=0
     TOS1=0;   TOS2=0
     GOSUB 2200
     HQTR=QTR*100
     PERIOD=1
     SW=1
210  *
     GOSUB 400
     DN=1
250  *
     GOSUB 950
     PRINT CLEAN:
     GOSUB 500
     GOSUB 2700
260  *
     IF TYPE=2 THEN IF TMDR=1 THEN GOTO 2100
     IF TYPE=3 THEN GOTO 2100
     PRINT @(70,22):"     ":@(70,22):
     INPUT PL
     TME2=TIME()
     IF PL EQ 'RESET' THEN GOSUB 2200; GOSUB 950; GOTO 260
     GOTO 2110
265  *
     IF TMDR=1 THEN TPLAY1=TPLAY1+1 ELSE TPLAY2=TPLAY2+1
     A=RND(24)+1
     IF A=3 THEN GOTO 270
     GOTO 280
270  *
     A=RND(8)+1
     IF A>4 THEN GOTO 275
271  *
     PRINT RESET:N(A):OFF:
     IF YDLN<(NY(A)*(-2)) THEN YDS=(INT(YDLN/2))*(-1) ELSE YDS=NY(A)
     IF TMDR=1 THEN PENTL1=PENTL1-YDS ELSE PENTL2=PENTL2-YDS
     GOTO 277
275  *
     PRINT RESET:N(A):DEF:
     IF YDLN>(100-(NY(A)*2)) THEN YDS=(INT((100-YDLN)/2)) ELSE YDS=NY(A)
     IF TMDR=1 THEN PENTL2=PENTL2+YDS ELSE PENTL1=PENTL1+YDS
277  *
     YDLN=YDLN+YDS
     PRINT RESET:ABS(YDS):" YARD PENALTY":
     GOSUB 500
     PYDS=0
     DN=DN-1
     GOSUB 2000
     GOTO 250
280  *
     CHANGE=0
     PYDS=0; YDS=0
     IF PL=1 THEN GOSUB 1000; GOTO 250
     IF PL=2 THEN GOSUB 1000; GOTO 250
     IF PL=3 THEN GOSUB 1100; GOTO 250
     IF PL=4 THEN GOSUB 1100; GOTO 250
     IF PL=5 THEN GOSUB 1200; GOTO 250
     IF PL=6 THEN GOSUB 1200; GOTO 250
     IF PL=7 THEN GOSUB 1300; GOTO 250
     IF PL=8 THEN GOSUB 1400; GOTO 250
     IF PL=9 THEN GOSUB 1500; GOTO 250
     A=1; GOTO 271
300  *
     IF A=1 THEN A='RAMS'; GOTO 399
     IF A=2 THEN A='COWBOYS'; GOTO 399
     IF A=3 THEN A='RAIDERS'; GOTO 399
     IF A=4 THEN A='PACKERS'; GOTO 399
     IF A=5 THEN A='EAGLES'; GOTO 399
     IF A=6 THEN A='OILERS'; GOTO 399
     IF A=7 THEN A='DOLPHINS'; GOTO 399
     IF A=8 THEN A='STEELERS'; GOTO 399
     IF A=9 THEN A='BRONCOS'; GOTO 399
     IF A=10 THEN A='CHARGERS'; GOTO 399
     IF A=11 THEN A="BUCANNEERS"; GOTO 399
     IF A=12 THEN A="CARDINALS"; GOTO 399
     IF A=13 THEN A="BENGALS"; GOTO 399
     IF A=14 THEN A="VIKINGS"; GOTO 399
     A=''
399  *
     RETURN
400  *
     IF PERIOD=1 THEN T.DR=TMDR; NAMEA=TEAM1; NAMEB=TEAM2; GOTO 405
     IF PERIOD=3 THEN T.DR=TMDR; NAMEA=TEAM1; NAMEB=TEAM2; GOTO 405
     IF TMDR=1 THEN T.DR=2 ELSE T.DR=1
     NAMEB=TEAM1; NAMEA=TEAM2
405  *
     PRINT @(3,7):NAMEA:SPACE(5):@(44,7):SPACE(10-LEN(NAMEB)):NAMEB:
     PRINT @(0,11):STR(' ',56):
     IF T.DR=1 THEN GOTO 410
     PRINT @(20,11):'=>':
     GOTO 420
410  *
     PRINT @(36,11):'<=':
420  *
     FOR I=11 TO 23
       PRINT @(56,I):SPACE(23):
     NEXT I
     IF TYPE=3 THEN PRINT RESET:"KICK-OFF":; GOTO 425
     IF TYPE=2 THEN IF TMDR=2 THEN PRINT RESET:"KICK-OFF":; GOTO 425
     PRINT @(58,18):"HIT 'NEW LINE' TO":@(59,19):"   KICK OFF   ":
     INPUT A
     FOR I=17 TO 20
       PRINT @(56,I):SPACE(23):
     NEXT I
425  *
     TME2=TIME()
     PRINT @(58,12):"OFFENSIVE PLAYS":@(60,13):"1=DRAW PLAY":@(60,14):"2=END SWEEP":@(60,15):"3=REVERSE":
     PRINT @(60,16):"4=QTRBACK OPTION":@(60,17):"5=SHORT PASS":@(60,18):"6=SCREEN PASS":@(60,19):"7=LONG PASS":
     PRINT @(60,20):"8=FIELD GOAL":@(60,21):"9=PUNT":@(58,22):"ENTER A PLAY":
     YDS=21+RND(50); YDLN=40+YDS
     IF TMDR=1 THEN TMDR=2 ELSE TMDR=1
     GOSUB 2600
     IF TMDR=1 THEN TMDR=2 ELSE TMDR=1
     YDLN=100-YDLN
     IF TMDR=1 THEN
       OFF=TEAM1; DEF=TEAM2
     END ELSE
       OFF=TEAM2; DEF=TEAM1
     END
     IF YDLN<0 THEN PRINT RESET2:"TOUCHBACK!":; YDLN=20; DN=1; GOTO 499
     GOSUB 500
     A=RND(10)+1
     IF A=5 THEN PRINT RESET2:"FUMBLE!":; GOSUB 700; GOTO 499
     A=RND(6)+1
     IF A=3 THEN GOSUB 500; PRINT RESET:"TACKLED AT THE ":YDLN:; DN=1; GOTO 499
     IF A=1 THEN GOTO 430
     IF A=2 THEN YDS=RND(100)+1 ELSE YDS=RND(30)+1
     GOSUB 600
     DN=1
     GOTO 499
430  *
     YDS=RND(13)+1
     TEST=YDLN-YDS
     IF TEST<0 THEN GOTO 440
     PRINT RESET2:"TACKLED AT THE ":(YDLN-YDS):
     YDLN=YDLN-YDS
     GOSUB 500
     DN=1
     GOTO 499
440  *
     PRINT RESET2:"S A F E T Y !":
     IF TMDR=1 THEN TMDR=2; SCORE2=SCORE2+2 ELSE TMDR=1; SCORE1=SCORE1+2
     DN=1; YTG=10
     GOSUB 950
     GOSUB 400
     DN=1
     CHANGE=1
499  *
     PYDS=0; YDS=0
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     RETURN
500  *
     ACC=INT(YDLN/2)
     IF ACC<1 THEN ACC=1
     IF PERIOD=1 THEN T.DR=TMDR; GOTO 505
     IF PERIOD=3 THEN T.DR=TMDR; GOTO 505
     IF TMDR=1 THEN T.DR=2 ELSE T.DR=1
505  *
     IF T.DR=1 THEN ACC=ACC+3 ELSE ACC=53-ACC
     IF T.DR=1 THEN GOTO 510
     PRINT @(ACC-1,11):' <=':
     GOTO 599
510  *
     HACC=ACC-1
     PRINT @(HACC,11):'=> ':
599  *
     RETURN
600  *
     TEST1=PYDS+YDS
     TEST=YDS
     MESS1=" YARDS"
     IF TEST1<0 THEN MESS="  LOSS OF "
     IF TEST1>0 THEN MESS="  GAIN OF "
     IF TEST1=0 THEN MESS="  NO GAIN "; MESS1=""
     IF TEST1=1 THEN MESS1=" YARD"
     IF TEST1=-1 THEN MESS1=" YARD"
610  *
     IF TEST<=2 THEN PYD=YDLN+TEST ELSE PYD=YDLN
     IF PYD<0 THEN GOTO 440
     IF PYD>50 THEN PYD=100-PYD
     IF TEST<=2 THEN
       IF (TEST+YDLN)<100 THEN
         PRINT RESET2:MESS:ABS(YDS+PYDS):MESS1:
         YDLN=YDLN+TEST
         GOSUB 500
         GOTO 699
       END
     END
     IF TEST<=2 THEN IF (TEST+YDLN)>100 THEN GOTO 620
     IF TEST>2 THEN IF (YDLN+2)>100 THEN GOTO 620
     YDLN=YDLN+2
     GOSUB 500
     TEST=TEST-2
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF PYD<0 THEN GOTO 620
     GOTO 610
620  *
     PRINT BELL:RESET2:BELL:"T O U C H D O W N ! !":
     IF TMDR=1 THEN SCORE1=SCORE1+7; TMDR=2 ELSE SCORE2=SCORE2+7; TMDR=1
     DN=1
     YTG=10
     PYDS=0; YDS=0
     GOSUB 950
     GOSUB 400
     CHANGE=1
699  *
     RETURN
700  *
     A=RND(10)+1
     IF A<6 THEN PRINT RESET2:OFF:" RECOVERS":; GOTO 799
     YDLN=100-YDLN
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     GOSUB 1950
     GOSUB 1800
     CHANGE=1
     PRINT RESET2:0FF:" RECOVER":
     A=RND(5)+1
     IF A>2 THEN PRINT RESET2:"BALL AT THE ":PYD:; GOTO 798
     IF A=1 THEN YDS=RND(80)+1 ELSE YDS=RND(8)+1
     IF (YDLN+YDS)<100 THEN GOSUB 1930
     GOSUB 600
798  *
     DN=1
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     YDS=0;PYDS=0
799  *
     RETURN
950  *
     TME1=TIME()
     TEST=(TME1-TME2)*19
     TME2=TME1
     HQTR=HQTR-TEST
     IF HQTR<1 THEN
       IF PERIOD=1 THEN
         NAMEA=TEAM2; NAMEB=TEAM1
         PRINT RESET:"END OF THE FIRST QUARTER":
         PRINT @(3,7):NAMEA:SPACE(5):@(44,7):SPACE(10-LEN(NAMEB)):NAMEB:
       END
     END
     IF HQTR<1 THEN
       IF PERIOD=2 THEN
         PRINT RESET:"END OF THE FIRST HALF"
         TMDR=H.TMDR; PERIOD=3; HQTR=QTR*100; DN=1; YTG=10; GOSUB 400; CHANGE=1; GOSUB 1930
       END
     END
     IF HQTR<1 THEN
       IF PERIOD=3 THEN
         NAMEA=TEAM2; NAMEB=TEAM1
         PRINT RESET:"END OF THE THIRD QUARTER":
         PRINT @(3,7):NAMEA:SPACE(5):@(44,7):SPACE(10-LEN(NAMEB)):NAMEB:
       END
     END
     IF HQTR<1 THEN PERIOD=PERIOD+1; HQTR=QTR*100
     IF LEN(HQTR)=4 THEN MIN=HQTR[1,2]; SEC=HQTR[3,2]; GOTO 960
     IF LEN(HQTR)=3 THEN MIN=HQTR[1,1]; SEC=HQTR[2,2]; GOTO 960
     IF LEN(HQTR)<3 THEN MIN=0; SEC=HQTR
960  *
     SEC=INT((SEC*6)/10)
     IF LEN(MIN)=2 THEN IF LEN(SEC)=2 THEN CLOCK=MIN CAT ':' CAT SEC; GOTO 970
     IF LEN(MIN)=2 THEN CLOCK=MIN CAT ':0' CAT SEC; GOTO 970
     IF LEN(SEC)=2 THEN CLOCK=' ' CAT MIN CAT ':' CAT SEC; GOTO 970
     CLOCK=' ' CAT MIN CAT ':0' CAT SEC
970  *
     PRINT @(4,4):DN:@(11,4):SPACE(3-LEN(YTG)):YTG:@(20,4):PERIOD:@(23,4):CLOCK:
     PRINT @(33,5):SPACE(3-LEN(SCORE1)):SCORE1:SPACE(11-LEN(SCORE2)):SCORE2:
     PRINT @(70,2):SPACE(4-LEN(RUSH1)):RUSH1:SPACE(6-LEN(RUSH2)):RUSH2
     PRINT @(70,3):SPACE(4-LEN(PASS1)):PASS1:SPACE(6-LEN(PASS2)):PASS2
     PRINT @(70,4):SPACE(4-LEN(TOFF1)):TOFF1:SPACE(6-LEN(TOFF2)):TOFF2
     PRINT @(70,5):SPACE(4-LEN(TPLAY1)):TPLAY1:SPACE(6-LEN(TPLAY2)):TPLAY2
     PRINT @(70,6):SPACE(4-LEN(TDNS1)):TDNS1:SPACE(6-LEN(TDNS2)):TDNS2
     PRINT @(70,7):SPACE(4-LEN(COMPL1)):COMPL1:SPACE(6-LEN(COMPL2)):COMPL2
     PRINT @(70,8):SPACE(4-LEN(AYP1)):AYP1:SPACE(6-LEN(AYP2)):AYP2
     PRINT @(70,9):SPACE(4-LEN(PENTL1)):PENTL1:SPACE(6-LEN(PENTL2)):PENTL2
     PRINT @(70,10):SPACE(4-LEN(TOS1)):TOS1:SPACE(6-LEN(TOS2)):TOS2
     IF PERIOD=5 THEN GOTO 9998
     RETURN
1000 *
     R=RND(10)+1
     IF R<3 THEN GOTO 1010
     IF R>4 THEN GOTO 1020
1005 *
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF PYDS=0 THEN PRINT RESET2:"NO GAIN":
     IF PYDS<0 THEN PRINT RESET2:"LOSS OF ":ABS(PYDS):
     IF PYDS>0 THEN PRINT RESET2:" GAIN OF ":PYDS:
     YDS=0
     GOSUB 1900
     GOTO 1090
1010 *
     YDS=-(RND(5)+1)
     GOSUB 1900
     GOSUB 600
     GOTO 1090
1020 *
     IF R>9 THEN GOTO 1030
1025 *
     YDS=RND(6)+1
     GOSUB 1900
     GOSUB 600
     GOTO 1090
1030 *
     YDS=RND(50)+1
     GOSUB 1900
     GOSUB 600
1090 *
     A=RND(18)+1
     IF A=9 THEN PRINT RESET2:"FUMBLE!":; GOSUB 700
1095 *
     IF CHANGE=0 THEN GOSUB 2000
     RETURN
1100 *
     R=RND(10)+1
1110 *
     IF R<3 THEN GOTO 1150
     IF R>7 THEN GOSUB 1030; GOTO 1199
     IF R>4 THEN GOSUB 1025; GOTO 1199
     GOSUB 1005
     GOTO 1199
1150 *
     YDS= -(RND(10)+1)
     GOSUB 1900
     GOSUB 600
     GOSUB 1090
1199 *
     RETURN
1200 *
     LPASS=3
     R=RND(10)+1
     IF R>2 THEN X=10; GOTO 1210
     IF R>1 THEN GOTO 1205
1201 *
     YDS=-(RND(5)+1)
     YDLN=YDLN+YDS
     IF YDLN<0 THEN PL=1; GOSUB 440; GOTO 1299
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET2:"LOSS OF ":ABS(YDS):
     PL=1; GOSUB 1900
     GOSUB 1090
     GOTO 1299
1205 *
     YDS=0
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET2:'NO GAIN':
     GOSUB 1090
     GOTO 1299
1210 *
     IF R>4 THEN GOTO 1250
     IF R=1 THEN GOTO 1220
1215 *
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET2:'INCOMPLETE PASS':
     IF TMDR=1 THEN NPASS1=NPASS1+1 ELSE NPASS2=NPASS2+1
     YDS=0; GOSUB 1900
     GOSUB 2000
     GOTO 1299
1220 *
     GOSUB 1290
     YDS=0; GOSUB 1900
     IF PYD<0 THEN PRINT RESET2:"INTERCEPTED IN END ZONE":; GOTO 1222
     PRINT RESET2:'INTERCEPTED AT THE ':PYD:
1222 *
     CHANGE=1
     PYDS=0
     GOSUB 1950
     GOSUB 1800
     YDLN=100-YDLN
     A=RND(5)+1
     IF PYD<0 THEN YDLN=20; PYD=20; GOTO 1223
     IF A<3 THEN GOTO 1225
1223 *
     PRINT RESET:OFF:' TAKE OVER AT THE ':PYD:; GOSUB 1090; DN=1; YTG=10; GOTO 1299
1225 *
     YDS=RND(20)+1
     DN=1
     R=RND(2)+1
     PL=0
     IF R=1 THEN GOSUB 1000 ELSE GOSUB 1100
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     GOTO 1299
1250 *
     GOSUB 1290
     IF YDLN<0 THEN GOSUB 440; GOTO 1299
     IF PYD<-10 THEN
       PRINT RESET2:"THROWN OUT OF THE END ZONE"
       YDLN=YDLN-YDS; PYDS=0; YDS=0; GOSUB 1900; GOSUB 2000; GOTO 1299
     END
     IF TMDR=1 THEN CPASS1=CPASS1+1; NPASS1=NPASS1+1 ELSE CPASS2=CPASS2+1; NPASS2=NPASS2+1
     GOSUB 2600
     GOSUB 1900; YDS=0
     IF PYD<0 THEN GOSUB 620; GOTO 1299
     PRINT RESET2:'COMPLETE AT THE ':PYD:
     IF PL=6 THEN R=RND(4)+6
     IF PL=6 THEN IF R=6 THEN GOSUB 1005; YDS=0; GOTO 1299
     IF PL=6 THEN IF R<9 THEN YDS=0
     IF PL=6 THEN GOSUB 1110; YDS=0; GOTO 1299
     GOSUB 1000
     GOTO 1299
1290 *
     A=RND(5)+1
     IF A>4 THEN YDS=-(RND(5)+1) ELSE YDS=RND(X)+1
     YDS=YDS+LPASS
     YDLN=YDLN+YDS
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF PYD=0 THEN PYD="GOAL LINE"
     PYDS=YDS
     RETURN
1299 *
     RETURN
1300 *
     LPASS=0
     R=RND(10)+1
     IF R>8 THEN GOSUB 1201; GOTO 1399
     IF R>5 THEN GOSUB 1215; GOTO 1399
     X=30; LPASS=25
     IF R<2 THEN GOSUB 1220 ELSE GOSUB 1250
1399 *
     RETURN
1400 *
     A=RND(20)+1
     IF YDLN<90 THEN GOTO 1410
     IF A<19 THEN GOTO 1450 ELSE GOTO 1470
1410 *
     IF YDLN<80 THEN GOTO 1420
     IF A<17 THEN GOTO 1450 ELSE GOTO 1470
1420 *
     IF YDLN<70 THEN GOTO 1430
     IF A<13 THEN GOTO 1450 ELSE GOTO 1470
1430 *
     IF YDLN<60 THEN GOTO 1440
     IF A<9 THEN GOTO 1450 ELSE GOTO 1470
1440 *
     IF YDLN<50 THEN GOTO 1470
     IF A>5 THEN GOTO 1470
1450 *
     YDS=100-YDLN; YDLN=100; GOSUB 2600
     PRINT RESET2:'THE KICK IS GOOD!!':
     IF TMDR=1 THEN TMDR=2; SCORE1=SCORE1+3 ELSE SCORE2=SCORE2+3; TMDR=1
     DN=1; YTG=10
     GOSUB 950
     GOSUB 400
     GOSUB 950
     GOTO 1499
1470 *
     R=RND(10)+1
     IF R=5 THEN GOSUB 1700; GOTO 1499
     IF YDLN<70 THEN IF YDLN>55 THEN GOTO 1480
1475 *
     YDS=100-YDLN; YDLN=100; GOSUB 2600
     PRINT RESET2:'KICK IS NO GOOD':
     YDLN=YDS; GOSUB 1800
     IF PYD<20 THEN YDLN=20; PYD=20
     PRINT RESET:'BALL COMES OUT TO THE ':PYD:
     DN=1; YTG=10
     GOTO 1499
1480 *
     R=RND(5)
     IF R#3 THEN GOTO 1475
     YDS=99-YDLN-RND(6); YDLN=YDLN+YDS; GOSUB 2600
     PRINT RESET2:'CAUGHT AT THE ':YDLN:
     YDLN=100-YDLN; GOSUB 1800; DN=1
     PL=0
     GOSUB 1000
1499 *
     RETURN
1500 *
     R=RND(10)+1
     IF R=1 THEN GOSUB 1700; GOTO 1599
     CHANGE=1
     YDS=RND(20)+1
     DN=1; PYDS=0
     IF R<6 THEN GOTO 1520
     IF R<9 THEN GOTO 1540
     YDS=YDS+15
     GOTO 1550
1520 *
     YDS=YDS+40
     GOTO 1550
1540 *
     YDS=YDS+25
1550 *
     YDLN=YDLN+YDS; GOSUB 2600; YDLN=100-YDLN
     GOSUB 1900
     GOSUB 1800
     IF YDLN<0 THEN PRINT RESET2:"TOUCHBACK!":; YDLN=20; YTG=10; DN=1; GOTO 1599
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET2:'CAUGHT AT THE ':PYD:
     R=RND(3)+1
     YDS=0
     IF R=1 THEN GOSUB 1090; YTG=10; DN=1; GOTO 1599
     PL=0
     IF R=2 THEN GOSUB 1100 ELSE GOSUB 1000
     YTG=10; DN=1
1599 *
     YDS=0
     RETURN
1700 *
     R=RND(3)+1
     A=RND(2)
     IF A=1 THEN YDS=RND(5) ELSE YDS= -RND(5)
     YDLN=YDLN+YDS
     GOSUB 2600
     PRINT RESET2:'THE KICK WAS BLOCKED':
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF R>1 THEN GOTO 1710
     PRINT RESET2:OFF:" RECOVER":
     GOSUB 1900
     PYDS=YDS
     GOSUB 1000
     GOTO 1799
1710 *
     PRINT RESET2:DEF:" RECOVER":
     GOSUB 1900
     GOSUB 1800
     YDLN=100-YDLN
     DN=1; YDS=0; PYDS=0
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     R=RND(2)
     PL=0
     IF R=1 THEN GOSUB 1000 ELSE GOSUB 1100
     DN=1; YDS=0; PYDS=0
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
1799 *
     RETURN
1800 *
     IF TMDR=1 THEN TMDR=2 ELSE TMDR=1
     HOLD=DEF
     DEF=OFF
     OFF=HOLD
     RETURN
1900 *
     IF PL=1 THEN GOTO 1910
     IF PL=2 THEN GOTO 1910
     IF PL=3 THEN GOTO 1910
     IF PL=4 THEN GOTO 1910
     IF PL=5 THEN GOTO 1920
     IF PL=6 THEN GOTO 1920
     IF PL=7 THEN GOTO 1920
     IF PL=9 THEN GOTO 1940
     GOTO 1999
1910 *
     IF (YDLN+YDS)>100 THEN TEMP=100-YDLN ELSE TEMP=YDS
     IF (YDLN+YDS)<0 THEN TEMP=YDLN*(-1)
     IF TMDR=1 THEN RUSH1=RUSH1+TEMP ELSE RUSH2=RUSH2+TEMP
     GOTO 1925
1920 *
     IF YDLN>100 THEN TEMP=(100-(YDLN-YDS)) ELSE TEMP=YDS
     IF YDLN<0 THEN TEMP=(YDLN-YDS)*(-1)
     IF TMDR=1 THEN PASS1=PASS1+TEMP ELSE PASS2=PASS2+TEMP
     IF NPASS1#0 THEN COMPL1=INT((CPASS1*100)/NPASS1)
     IF NPASS2#0 THEN COMPL2=INT((CPASS2*100)/NPASS2)
1925 *
     TOFF1=RUSH1+PASS1
     TOFF2=RUSH2+PASS2
     GOTO 1999
1930 *
     IF TMDR=1 THEN TDNS1=TDNS1+1 ELSE TDNS2=TDNS2+1
     GOTO 1999
1940 *
     IF (YDLN+YDS)>100 THEN TEMP=100-YDLN ELSE TEMP=YDS
     IF TMDR=1 THEN NPUNT1=NPUNT1+1; YPUNT1=YPUNT1+TEMP ELSE NPUNT2=NPUNT2+1; YPUNT2=YPUNT2+TEMP
     IF NPUNT1#0 THEN AYP1=INT(YPUNT1/NPUNT1)
     IF NPUNT2#0 THEN AYP2=INT(YPUNT2/NPUNT2)
     GOTO 1999
1950 *
     IF TMDR=1 THEN TOS1=TOS1+1 ELSE TOS2=TOS2+1
1999 *
     RETURN
2000 *
     DN=DN+1
     YDS=YDS+PYDS
     IF DN<5 THEN GOTO 2001
     IF YDS>=YTG THEN GOTO 2001
     GOSUB 1800
     YDLN=100-YDLN
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET:OFF:' TAKE OVER AT THE ':PYD:
     DN=1
     GOSUB 1930
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     GOTO 2002
2001 *
     IF YDS>=YTG THEN IF YDLN<=90 THEN YTG=10; DN=1; GOSUB 1930; GOTO 2002
     IF YDS>=YTG THEN IF YDLN>90 THEN YTG=100-YDLN; DN=1; GOSUB 1930; GOTO 2002
     YTG=YTG-YDS
     IF YTG<1 THEN IF YDLN<90 THEN YTG=10; DN=1; GOSUB 1930 ELSE YTG=100-YDLN
2002 *
     RETURN
2100 *
     IF DN=4 THEN IF YDLN>60 THEN PL=8; GOTO 2110
     IF DN=4 THEN PL=9; GOTO 2110
     IF DN=2 THEN IF YTG<4 THEN IF YDLN>30 THEN IF YDLN<70 THEN PL=7; GOTO 2110
     IF DN=3 THEN IF YTG<4 THEN PL=1; GOTO 2110
     IF YDLN<35 THEN PL=RND(6)+1; GOTO 2110
     IF YDLN>34 THEN IF YDLN<70 THEN PL=RND(7)+1; GOTO 2110
     IF YDLN>69 THEN IF YDLN<92 THEN PL=RND(5)+1; GOTO 2110
     PL=1
2110 *
     IF PL=1 THEN MESS=' DRAW PLAY'
     IF PL=2 THEN MESS=" END SWEEP"
     IF PL=3 THEN MESS=" REVERSE"
     IF PL=4 THEN MESS=" QUARTERBACK OPTION"
     IF PL=5 THEN MESS=" SHORT PASS"
     IF PL=6 THEN MESS=" SCREEN PASS"
     IF PL=7 THEN MESS=" LONG PASS"
     IF PL=8 THEN MESS=" FIELD GOAL"
     IF PL=9 THEN MESS=" PUNT"
     PRINT RESET:MESS:
     GOTO 265
2200 *
     PRINT CLR:@(24,0):"*** FOOTBALL ***":@(70,0):"HOME  AWAY":@(70,1):"----  ----":
     PRINT @(2,2):"DOWN  YDS TO GO  QTR  TIME  HOME-TEAM  AWAY-TEAM       YDS RUSHING":
     PRINT @(2,3):"----  ---------  ---  ----  ":TEAM1:SPACE(11-LEN(TEAM1)):TEAM2:@(57,3):"YDS PASSING":
     PRINT @(4,4):"0       00      0":SPACE(4-LEN(QTR)):QTR:":00":@(34,5):"00":@(45,5):"00":
     PRINT @(30,4):"---------  ---------       TOTAL OFFENSE":@(57,5):"TOTAL # PLAYS":
     PRINT @(57,6):"FIRST DOWNS":@(57,7):"% PASS COMPL":
     PRINT @(3,8):"G....1....2....3....4....5....4....3....2....1....G   AVE YDS/PUNT":
     PRINT @(57,9):"PENALTIES":@(57,10):"TURNOVERS":
     PRINT @(3,13):"G....1....2....3....4....5....4....3....2....1....G":
     PRINT @(58,12):"OFFENSIVE PLAYS":@(60,13):"1=DRAW PLAY":@(60,14):"2=END SWEEP":@(60,15):"3=REVERSE":
     PRINT @(60,16):"4=QTRBACK OPTION":@(60,17):"5=SHORT PASS":@(60,18):"6=SCREEN PASS":@(60,19):"7=LONG PASS":
     PRINT @(60,20):"8=FIELD GOAL":@(60,21):"9=PUNT":@(58,22):"ENTER A PLAY":
     RETURN
2600 *
     KPYDLN=YDLN-YDS
     IF YDS<0 THEN GOTO 2610
2605 GOSUB 2650
     KPYDLN=KPYDLN+2
     IF KPYDLN<=YDLN THEN GOTO 2605
     RETURN
2610 GOSUB 2650
     KPYDLN=KPYDLN-2
     IF KPYDLN>=YDLN THEN GOTO 2610
     RETURN
2650 *
     ACC=INT(KPYDLN/2)
     IF ACC<1 THEN ACC=1
     IF PERIOD=1 THEN T.DR=TMDR; GOTO 2655
     IF PERIOD=3 THEN T.DR=TMDR; GOTO 2655
     IF TMDR=1 THEN T.DR=2 ELSE T.DR=1
2655 *
     IF T.DR=1 THEN ACC=ACC+3 ELSE ACC=53-ACC
     IF T.DR=1 THEN GOTO 2660
     PRINT @(ACC-1,11):' -':
     GOTO 2699
2660 *
     PRINT @(ACC,11):'- ':
2699 *
     RETURN
2700 *
     IF DN#1 THEN GOTO 2750
     IF YTG=10 THEN GOTO 2710
     IF YTG#(100-YDLN) THEN GOTO 2750
2710 *
     PRINT @(0,10):STR(' ',56):
     DOWN1=INT(YDLN/2)
     DOWN2=INT((YDLN+YTG)/2)
     IF PERIOD=1 THEN T.DR=TMDR; GOTO 2720
     IF PERIOD=3 THEN T.DR=TMDR; GOTO 2720
     IF TMDR=1 THEN T.DR=2 ELSE T.DR=1
2720 *
     IF T.DR=1 THEN DOWN1=DOWN1+3; DOWN2=DOWN2+3 ELSE DOWN1=53-DOWN1; DOWN2=53-DOWN2
     PRINT @(DOWN1,10):'!':@(DOWN2,10):'!':
2750 *
     PRINT @(0,12):STR(' ',56):
     ACC=INT(YDLN/2)
     IF PERIOD=1 THEN T.DR=TMDR; GOTO 2760
     IF PERIOD=3 THEN T.DR=TMDR; GOTO 2760
     IF TMDR=1 THEN T.DR=2 ELSE T.DR=1
2760 *
     IF T.DR=1 THEN ACC=ACC+3 ELSE ACC=53-ACC
     PRINT @(ACC,12):DN:
     RETURN
9998 *
     PRINT RESET:'TIME RAN OUT! - DO YOU WANT TO PLAY AGAIN(Y/N)?':
     INPUT A
     IF A EQ 'Y' THEN GOTO 100
     IF A NE 'N' THEN GOTO 9998
     STOP
END
