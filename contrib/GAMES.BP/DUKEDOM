*DUKEDOM
*REVISED BY CRAIG SHUCKER
*MARCH, 1976
     DIM P(9),L(4),G(11),PX(9),LX(4),GX(11),SX(6),SOIL(6),LANDUSE(6)
     PRINTER OFF
     ERASE=@(-1)
     BELL=CHAR(7)
     XBELOW=CHAR(27):CHAR(74)
     XLINE=CHAR(27):CHAR(75)
     PROMPT ' '
*
*  DOES PLAYER WANT INSTRUCTIONS?
*
1000 PRINT ERASE:
     PRINT @(24,10):"DO YOU WANT INSTRUCTIONS? ":
     INPUT ANS
     IF ANS="NO" OR ANS="N" THEN GOTO 1050
     IF ANS<>"YES" AND ANS<>"Y" THEN GOTO 1000
*
*  INSTRUCTIONS
*
     PRINT ERASE:
     PRINT @(10,7):
     PRINT "YOU ARE AN ASPIRING YOUNG DUKE WITH INTENTIONS OF BECOMING":
     PRINT @(10,9):
     PRINT "KING.  YOUR NORMAL LENGTH OF REIGN IS 45 YEARS.  YOU MUST":
     PRINT @(10,11):
     PRINT "ATTEMPT TO ACCOMPLISH YOUR GOAL WITHIN THIS AMOUNT OF TIME.":
     GOSUB 3300
     PRINT ERASE:
     PRINT @(10,4):
     PRINT "TO BE A COMPETENT RULER YOU MUST KNOW ABOUT THE AFFAIRS OF":
     PRINT @(10,6):
     PRINT "YOUR DUCHY AND OTHERS.  THE MEDIUM OF EXCHANGE IS GRAIN":
     PRINT @(10,8):
     PRINT "WHICH IS MEASURED IN HECTOLITRES (HL.)  YOU HAVE A POPULATION":
     PRINT @(10,10):
     PRINT "OF PEASANTS WHOSE MAIN PURPOSE IS TO PLANT AND HARVEST YOUR":
     PRINT @(10,12):
     PRINT "GRAIN AND TO FIGHT YOUR WARS.  LAND IS MEASURED IN HECTARES":
     PRINT @(10,14):
     PRINT "(HA.)  LAND IS USED FOR PLANTING GRAIN.  EXCESS LAND CAN BE":
     PRINT @(10,16):
     PRINT "SOLD.  IF THERE IS A NEED FOR MORE LAND IT CAN BE PURCHASED.":
     GOSUB 3300
     PRINT ERASE:
     PRINT @(10,5):
     PRINT "DURING YOUR REIGN YOU WILL FACE MANY PROBLEMS.  EVERY SEVEN":
     PRINT @(10,7):
     PRINT "YEARS LOCUSTS WILL ATTACK YOUR CROPS.  RATS WILL PERIODICALLY":
     PRINT @(10,9):
     PRINT "INFEST THE GRANARY.  POX EPIDEMICS AND THE BLACK PLAGUE WILL":
     PRINT @(10,11):
     PRINT "REDUCE THE POPULATION.  OTHER DUKES MAY DECIDE TO ATTACK YOUR":
     PRINT @(10,13):
     PRINT "DUCHY.  AND, OF COURSE, YOU HAVE TO DEAL WITH THE HIGH KING.":
     GOSUB 3300
     PRINT ERASE:
     PRINT @(10,1):
     PRINT "IF YOU ARE A WISE LEADER YOU WILL HAVE LEARNED FROM THE":
     PRINT @(10,3):
     PRINT "EXPERIENCES OF OTHER NOBLES.  THE PEASANTS WILL REMAIN HAPPY":
     PRINT @(10,5):
     PRINT "AND WILL REPRODUCE AT A REASONABLE RATE IF THEY ARE EACH FED":
     PRINT @(10,7):
     PRINT "13 HL. OF GRAIN PER YEAR.  (ANY NUMBER FROM 0 TO 20 THAT IS":
     PRINT @(10,9):
     PRINT "TYPED IN RESPONSE TO THE FOOD QUESTION WILL AUTOMATICALLY BE":
     PRINT @(10,11):
     PRINT "MULTIPLIED BY THE NUMBER OF PEASANTS.)  FREQUENT SHORTAGES":
     PRINT @(10,13):
     PRINT "OF FOOD WILL CAUSE THE PEASANTS TO RISE UP AGAINST YOU.":
     PRINT @(10,15):
     PRINT "EACH PEASANT IS CAPABLE OF PLANTING A MAXIMUM OF 4 HA. PER":
     PRINT @(10,17):
     PRINT "YEAR.  A GOOD COMBINATION OF BUYING AND SELLING OF LAND":
     PRINT @(10,19):
     PRINT "TENDS TO COUNTERACT LAND PRODUCTIVITY DECREASE TRENDS.":
     GOSUB 3300
     PRINT ERASE:
     PRINT @(10,3):
     PRINT "SINCE OTHER DUKES ALSO HAVE SIMILAR HOPES OF BECOMING KING,":
     PRINT @(10,5):
     PRINT "YOU MAY FIND YOUR DUCHY THREATENED BY SOME OF THESE RETCHED":
     PRINT @(10,7):
     PRINT "SOULS.  YOU MAY ATTACK FIRST WITH YOUR PEASANTS OR YOU MAY":
     PRINT @(10,9):
     PRINT "DECIDE TO HIRE MERCENARIES.  MERCENARIES ARE MORE EFFICIENT":
     PRINT @(10,11):
     PRINT "FIGHTERS, BUT TEND TO BE MORE EXPENSIVE.  THEY WILL ALSO":
     PRINT @(10,13):
     PRINT "LOOT YOUR VILLAGES AND KILL YOUR PEASANTS IF THEY ARE NOT":
     PRINT @(10,15):
     PRINT "PAID IN FULL.  THE AMOUNT OF LAND AND PEASANTS LOST OR GAINED":
     PRINT @(10,17):
     PRINT "DEPENDS ON THE MARGIN OF DEFEAT OR VICTORY, RESPECTIVELY.":
     GOSUB 3300
     PRINT ERASE:
     PRINT @(10,4):
     PRINT "YOUR DEALINGS WITH THE KING WILL PRIMARILY BE IN THE FORM OF":
     PRINT @(10,6):
     PRINT "TAXES AND LEVIES.  HOWEVER, IF THE KING FEELS THAT YOU ARE":
     PRINT @(10,8):
     PRINT "OF SUCH STRENGTH AS TO JEOPARDIZE HIS SAFETY, HE WILL ATTACK":
     PRINT @(10,10):
     PRINT "YOU.  AT THIS TIME YOU HAD BETTER BE PREPARED TO FIGHT, FOR":
     PRINT @(10,12):
     PRINT "THE HIGH KING WILL NOT GIVE YOU A SECOND CHANCE.  YOU MUST":
     PRINT @(10,14):
     PRINT "WIN, OR ELSE....":
     PRINT @(35,17):
     PRINT "GOOD LUCK!":
     GOSUB 3300
     PRINT ERASE:
*
*  PEASANT REPORT MESSAGES
*
1050 PX(1)="PEASANTS AT START"
     PX(2)="  STARVATIONS"
     PX(3)="  KING'S LEVY"
     PX(4)="  WAR CASUALTIES"
     PX(5)="  LOOTING VICTIMS"
     PX(6)="  DISEASE VICTIMS"
     PX(7)="  NATURAL DEATHS"
     PX(8)="  BIRTHS"
     PX(9)="PEASANTS AT END"
*
*  PEASANT SUBSCRIPTS
*
     PSTART=1
     STARV=2
     KINGLEVY=3
     WARVIC=4
     LOOTVIC=5
     DISVIC=6
     NATDEAD=7
     BIRTH=8
     PEND=9
*
*  LAND REP0RT MESSAGES
*
     LX(1)="LAND AT START"
     LX(2)="  BOUGHT/SOLD"
     LX(3)="  FRUITS OF WAR"
     LX(4)="LAND AT END"
*
*  LAND SUBSCRIPTS
*
     LSTART=1
     TRANS=2
     LWAR=3
     LEND=4
*
*  LAND PRODUCTIVITY REPORT MESSAGES
*
     SX(1)="100%"
     SX(2)=" 80%"
     SX(3)=" 60%"
     SX(4)=" 40%"
     SX(5)=" 20%"
     SX(6)="DEPL"
*
*  GRAIN REPORT MESSAGES
*
     GX(1)="GRAIN AT START"
     GX(2)="  USED FOR FOOD"
     GX(3)="  LAND DEALS"
     GX(4)="  SEEDINGS"
     GX(5)="  RAT LOSSES"
     GX(6)="  MERCENARY HIRE"
     GX(7)="  FRUITS OF WAR"
     GX(8)="  CROP YIELD"
     GX(9)="  CASTLE EXPENSE"
     GX(10)="  ROYAL TAX"
     GX(11)="GRAIN AT END"
*
*  GRAIN SUBSCRIPTS
*
     GSTART=1
     FEED=2
     LANDDEAL=3
     SEED=4
     RATLOSS=5
     HIRE=6
     GWAR=7
     CROPS=8
     CASTLE=9
     TAX=10
     GEND=11
*
*  PEASANT INITIALIZATION
*
1100 PEAS=100
     P(PSTART)=96
     P(STARV)=0
     P(KINGLEVY)=0
     P(WARVIC)=0
     P(LOOTVIC)=0
     P(DISVIC)=0
     P(NATDEAD)=-4
     P(BIRTH)=8
     P(PEND)=100
*
*  LAND INITIALIZATION
*
     LAND=600
     L(LSTART)=600
     L(TRANS)=0
     L(LWAR)=0
     L(LEND)=600
*
*  LAND PRODUCTIVITY INITIALIZATION
*
     SOIL(1)=216
     SOIL(2)=200
     SOIL(3)=184
     SOIL(4)=0
     SOIL(5)=0
     SOIL(6)=0
*
*  GRAIN INITIALIZATION
*
     GRAIN=4177
     G(GSTART)=5193
     G(FEED)=-1344
     G(LANDDEAL)=0
     G(SEED)=-768
     G(RATLOSS)=0
     G(HIRE)=0
     G(GWAR)=0
     G(CROPS)=1516
     G(CASTLE)=-120
     G(TAX)=-300
     G(GEND)=4177
*
*  INITIALIZATION OF OTHER VARIABLES
*
     YEAR=0
     YIELD=3.95
     KING=0
     DISEASE=0
     UNRESTST=0
     UNRESTLT=0
*
*  PRINT INITIAL REPORT
*
1200 PRINT ERASE:
     GOSUB 3400
*
*  PRINT PEASANT REPORT
*
     FOR I=1 TO 9
       PRINT @(2,I+4):PX(I):
       PRINT @(20,I+4):P(I) "R####":
     NEXT I
*
*  PRINT LAND REPORT
*
     FOR I=1 TO 4
       PRINT @(28,I+4):LX(I):
       PRINT @(44,I+4):L(I) "R######":
     NEXT I
*
*  PRINT LAND PRODUCTIVITY REPORT
*
     PRINT @(30,10):"LAND PRODUCTIVITY":
     FOR I=1 TO 6
       PRINT @(34,I+10):SX(I):
       PRINT " ":SOIL(I) "R####":
     NEXT I
*
*  PRINT GRAIN REPORT
*
     FOR I=1 TO 11
       PRINT @(54,I+4):GX(I):
       PRINT @(71,I+4):G(I) "R######":
     NEXT I
     IF YEAR=0 THEN
       PRINT @(16,18):"(SEVERE CROP DAMAGE DUE TO SEVEN YEAR LOCUSTS)":
     END
*
*  CLEAR REPORTS FROM SCREEN
*
     PRINT @(27,20):"PRESS <RETURN> TO CONTINUE":
     INPUT Z
     PRINT @(0,3):XBELOW:
*
*  INCREMENT YEAR
*
     YEAR=YEAR+1
*
*  INITIALIZE REPORT DATA FOR NEW YEAR
*
     MAT P = 0 ; MAT L = 0 ; MAT G = 0
     P(1)=PEAS
     L(1)=LAND
     G(1)=GRAIN
*
*  CHECK FOR LOSING CONDITIONS
*
     IF PEAS<33 THEN
       PRINT @(0,5):
       PRINT "YOU HAVE SO FEW PEASANTS THAT THE HIGH KING ":
       PRINT "HAS ABOLISHED YOUR DUCAL RIGHT."
       GOTO 3100
     END
     IF LAND<199 THEN
       PRINT @(0,5):
       PRINT "YOU HAVE SO LITTLE LAND THAT THE HIGH KING HAS ":
       PRINT "ABOLISHED YOUR DUCAL RIGHT."
       GOTO 3100
     END
     IF UNRESTST>88 OR UNRESTLT>99 OR GRAIN<429 THEN
       PRINT @(0,5):"THE PEASANTS TIRE OF WAR AND STARVATION."
       PRINT "YOU ARE DEPOSED."
       GOTO 3100
     END
     IF YEAR>45 AND KING=0 THEN
       PRINT ERASE:@(0,4):
       PRINT "YOU HAVE REACHED THE AGE OF MANDATORY RETIREMENT."
       GOTO 3100
     END
*
*  PROVOCATION TO WAR
*
     UNRESTST=0
     IF KING<=0 THEN GOTO 1400
     PRINT "THE HIGH KING DEMANDS TWICE THE ROYAL TAX IN HOPE ":
     PRINT "0F PROVOKING WAR.":
1300 PRINT "WILL YOU PAY?":
     INPUT ANS
     IF ANS="YES" OR ANS="Y" THEN KING=2 ELSE
       IF ANS="NO" OR ANS="N" THEN KING=-1 ELSE
         GOTO 1300
       END
     END
*
*  FEED PEASANTS
*
1400 PRINT ERASE
     PRINT @(0,3):XLINE:@(0,3):
     PRINT "GRAIN FOR FOOD = ":
     INPUT FOOD
     PRINT @(0,4):XBELOW:
     IF FOOD<>INT(FOOD) THEN GOTO 1400
     IF NOT(NUM(FOOD)) THEN GOTO 1400
     IF FOOD<0 THEN GOTO 1400
     IF FOOD<21 THEN FOOD=FOOD*PEAS
     IF FOOD>GRAIN THEN
       PRINT @(0,5):"BUT YOU DON'T HAVE ENOUGH GRAIN."
       GOTO 1400
     END
     IF FOOD/PEAS<11 AND FOOD<GRAIN THEN
       PRINT @(0,5):"THE PEASANTS DEMONSTRATE BEFORE THE CASTLE WITH ":
       PRINT "SHARPENED SCYTHES."
       UNRESTST=UNRESTST+3
     END
     G(FEED)=-FOOD
     GRAIN=GRAIN-FOOD
     GOSUB 3400
     RATION=FOOD/PEAS
*
*  STARVATIONS
*
     IF RATION<13 THEN
       PRINT @(0,5):"SOME PEASANTS HAVE STARVED DURING THE WINTER."
       P(STARV)=-INT(PEAS-FOOD/13)
       PEAS=PEAS+P(STARV)
       GOSUB 3400
     END
     IF RATION>18 THEN X=RATION-14 ELSE X=4
     UNRESTST=UNRESTST-3*P(STARV)-2*X
     IF UNRESTST>88 THEN
       PRINT @(0,7):
       PRINT "THE PEASANTS TIRE OF WAR AND STARVATION."
       PRINT "YOU ARE DEPOSED."
       GOTO 3100
     END
     IF PEAS<33 THEN
       PRINT @(0,5):
       PRINT "YOU HAVE SO FEW PEASANTS THAT THE HIGH KING HAS ":
       PRINT "ABOLISHED YOUR DUCAL RIGHT."
       GOTO 3100
     END
*
*  CALCULATE LAND PRICES
*
     GOSUB 3200
     PRICE=INT(XRAND+2*YIELD-5)
     IF PRICE<4 THEN PRICE=4
*
*  BUY LAND
*
1500 PRINT @(0,8):XLINE:@(0,8):
     PRINT "LAND TO BUY AT ":PRICE:" HL./HA. = ":
     INPUT BOUGHT
     PRINT @(0,9):XBELOW:
     IF BOUGHT<>INT(BOUGHT) THEN GOTO 1500
     IF NOT(NUM(BOUGHT)) THEN GOTO 1500
     IF BOUGHT<0 THEN GOTO 1500
     G(LANDDEAL)=-BOUGHT*PRICE
     IF -G(LANDDEAL)>GRAIN THEN
       PRINT @(0,10):"BUT YOU DON'T HAVE ENOUGH GRAIN."
       GOTO 1500
     END
     L(TRANS)=BOUGHT
     SOIL(3)=SOIL(3)+BOUGHT
     IF BOUGHT>0 THEN GOTO 1950
*
*  SELL LAND
*
     GOODLAND=SOIL(1)+SOIL(2)+SOIL(3)
     FOR I=1 TO 3
       PRICE=PRICE-1
1600   PRINT @(0,9):XLINE:@(0,9):
       PRINT "LAND TO SELL AT ":PRICE:" HL./HA. = ":
       INPUT SOLD
       PRINT @(0,10):XBELOW:
       IF SOLD<>INT(SOLD) THEN GOTO 1600
       IF NOT(NUM(SOLD)) THEN GOTO 1600
       IF SOLD<0 THEN GOTO 1600
       IF SOLD>GOODLAND THEN
         PRINT @(0,11):"BUT YOU DON'T HAVE ENOUGH GOOD LAND."
         GOTO 1700
       END
       G(LANDDEAL)=SOLD*PRICE
       IF G(LANDDEAL)>4000 THEN
         PRINT @(0,11):"NO BUYERS HAVE THAT MUCH GRAIN - SELL LESS."
       END ELSE
         GOTO 1800
       END
1700 NEXT I
     PRINT @(0,11):"BUYERS HAVE LOST INTEREST.":
     G(LANDDEAL)=0
     SOLD=0
1800 L(TRANS)=-SOLD
     FOR I=3 TO 1 STEP -1
       IF SOLD<=SOIL(I) THEN GOTO 1900
       SOLD=SOLD-SOIL(I)
       SOIL(I)=0
     NEXT I
     STOP
1900 SOIL(I)=SOIL(I)-SOLD
1950 LAND=LAND+L(TRANS)
     GOSUB 3400
     IF LAND<10 THEN
       PRINT @(0,11):
       PRINT "YOU HAVE SO LITTLE LAND THAT THE HIGH KING HAS ":
       PRINT "ABOLISHED YOUR DUCAL RIGHT."
       GOTO 3100
     END
     IF L(TRANS)<0 AND PRICE<4 THEN
       G(LANDDEAL)=INT(G(LANDDEAL)/2)
       PRINT @(0,11):"THE HIGH KING APPROPRIATES HALF OF YOUR EARNINGS"
       PRINT "AS PUNISHMENT FOR SELLING AT SUCH A LOW PRICE."
     END
     GRAIN=GRAIN+G(LANDDEAL)
     GOSUB 3400
     IF KING=-2 THEN GOTO 3000
*
*  PLANT LAND
*
2000 PRINT @(0,15):XLINE:@(0,15):
     PRINT "LAND TO PLANT = ":
     INPUT PLANT
     PRINT @(0,16):XBELOW:
     IF PLANT<>INT(PLANT) THEN GOTO 2000
     IF NOT(NUM(PLANT)) THEN GOTO 2000
     IF PLANT<0 THEN GOTO 2000
     IF PLANT>LAND THEN
       PRINT @(0,17):"BUT YOU DON'T HAVE ENOUGH LAND."
       GOTO 2000
     END
     IF PLANT>4*PEAS THEN
       PRINT @(0,17):"BUT YOU DON'T HAVE ENOUGH PEASANTS."
       GOTO 2000
     END
     G(SEED)=-2*PLANT
     IF -G(SEED)>GRAIN THEN
       PRINT @(0,17):"BUT YOU DON'T HAVE ENOUGH GRAIN."
       GOTO 2000
     END
     G(CROPS)=PLANT
     GRAIN=GRAIN+G(SEED)
     GOSUB 3400
*
*  CALCULATE NEW LAND PRODUCTIVITY CONDITIONS
*
     SOW=PLANT
     FOR I=1 TO 6
       LANDUSE(I)=0
     NEXT I
     FOR I=1 TO 6
       IF SOW<=SOIL(I) THEN GOTO 2100
       SOW=SOW-SOIL(I)
       LANDUSE(I)=SOIL(I)
       SOIL(I)=0
     NEXT I
     STOP
2100 LANDUSE(I)=SOW
     SOIL(I)=SOIL(I)-SOW
     SOIL(1)=SOIL(1)+SOIL(2)
     SOIL(2)=0
     FOR I=3 TO 6
       SOIL(I-2)=SOIL(I-2)+SOIL(I)
       SOIL(I)=0
     NEXT I
     FOR I=1 TO 5
       SOIL(I+1)=SOIL(I+1)+LANDUSE(I)
     NEXT I
     SOIL(6)=SOIL(6)+LANDUSE(6)
*
*  DETERMINE GROWING CONDITIONS
*
     GOSUB 3200
     COND=XRAND+3
*
*  SEVEN YEAR LOCUSTS
*
     PRINT @(0,3):XBELOW:
     IF YEAR=7*INT(YEAR/7) THEN
       PRINT @(0,3):"SEVEN YEAR LOCUSTS"
       PRINT
       COND=COND/2
     END
*
*  DETERMINE CROP YIELD
*
     IF PLANT=0 THEN
       YIELD=0
     END ELSE
       PROD=0
       FOR I=1 TO 5
         PROD=PROD+LANDUSE(I)*(1.2-.2*I)
       NEXT I
       YIELD=INT(PROD/PLANT*COND*100)/100
     END
     PRINT @(0,4):"YIELD = ":YIELD:" HL./HA."
     PRINT
*
*  RATS
*
     RAND=1+RND(10)
     IF RAND<9 THEN GOTO 2300
     G(RATLOSS)=-INT(RAND*GRAIN/83)
     GRAIN=GRAIN+G(RATLOSS)
     PRINT @(0,5):"RATS INFEST THE GRANARY"
     GOSUB 3400
     PRINT
     IF PEAS<67 THEN GOTO 2300
     GOSUB 3200
*
*  PEASANT LEVY
*
     LEVY=INT(XRAND)
     IF LEVY>PEAS/30 THEN GOTO 2300
     PRINT @(0,7):"THE HIGH KING REQUIRES ":LEVY:" PEASANTS FOR HIS ":
     PRINT "ESTATES AND MINES."
2200 PRINT @(0,8):XLINE:@(0,8):
     PRINT "WILL YOU SUPPLY THEM (YES) OR ":
     PRINT "PAY 100 HL. OF GRAIN FOR EACH INSTEAD (N0)? ":
     INPUT ANS
     IF ANS="YES" OR ANS="Y" THEN
       P(KINGLEVY)=-LEVY
       PEAS=PEAS-LEVY
     END ELSE
       IF ANS="NO" OR ANS="N" THEN
         G(TAX)=-100*LEVY
         GRAIN=GRAIN+G(TAX)
       END ELSE
         GOTO 2200
       END
     END
     GOSUB 3400
*
*  KING PREPARES FOR WAR
*
2300 IF KING=-1 THEN
       KING=-2
       PRINT @(0,10):"THE KING CALLS FOR PEASANT LEVIES AND ":
       PRINT "HIRES MANY FOREIGN MERCENARIES."
       GOTO 2800
     END
*
*  DETERMINE KING'S CONCERN
*
     COND=INT(11-1.5*YIELD)
     IF COND<2 THEN COND=2
     ENEMYPOW=0
     IF KING=0 AND PEAS>109 THEN
       IF GRAIN+17*(LAND-400)>10600 THEN
         PRINT @(0,10):
         PRINT "THE HIGH KING GROWS UNEASY AND MAY BE SUBSIDIZING ":
         PRINT "WARS AGAINST YOU."
         PRINT
         COND=COND+2
         ENEMYPOW=YEAR+5
       END ELSE
         ENEMYPOW=0
       END
     END
*
*  DUKE THREATENS WAR
*
     WARCOND=1+RND(10)
     IF COND<WARCOND THEN GOTO 2800
     PRINT @(0,11):"A NEARBY DUKE THREATENS WAR."
     GOSUB 3200
     ENEMYPOW=INT(18*XRAND+ENEMYPOW+85)
     EFF=1.2-UNRESTST/16
     PLAYPOW=INT(PEAS*EFF)+13
*
*  PLAYER ATTACKS FIRST
*
2400 PRINT @(0,12):XLINE:@(0,12):
     PRINT "WILL YOU ATTACK FIRST? ":
     INPUT ANS
     IF ANS="NO" OR ANS="N" THEN GOTO 2500
     IF ANS<>"YES" AND ANS<>"Y" THEN GOTO 2400
     IF PLAYPOW>ENEMYPOW THEN
       PRINT @(0,13):"PEACE NEGOTIATIONS WERE SUCCESSFUL."
       P(WARVIC)=-COND-1
       ENEMYPOW=0
     END ELSE
       PRINT @(0,13):"FIRST STRIKE FAILED - YOU NEED PROFESSIONALS."
       P(WARVIC)=-COND-WARCOND-1
       ENEMYPOW=ENEMYPOW+3*P(WARVIC)
     END
     IF ENEMYPOW<1 THEN GOTO 2750
*
*  HIRE MERCENARIES
*
2500 PRINT @(0,14):XLINE:@(0,14):
     PRINT "HOW MANY MERCENARIES WILL YOU HIRE AT 40 HL. EACH = ":
     INPUT MERC
     PRINT @(0,15):XBELOW:
     IF MERC<>INT(MERC) THEN GOTO 2500
     IF NOT(NUM(MERC)) THEN GOTO 2500
     IF MERC<0 THEN GOTO 2500
     IF MERC>75 THEN
       PRINT @(0,15):"THERE AREN'T THAT MANY MERCENARIES AROUND."
       GOTO 2500
     END
*
*  CALCULATE WAR RESULTS
*
     ENEMYPOW=INT(1.95*ENEMYPOW)
     PLAYPOW=7*MERC+INT(EFF*PEAS)+13
     PEASLOSS=ENEMYPOW-4*MERC-INT(.25*PLAYPOW)
     DIFFPOW=PLAYPOW-ENEMYPOW
     L(LWAR)=INT(.8*DIFFPOW)
*
*  PLAYER IS OVERRUN
*
     IF -L(LWAR)=INT(.8*DIFFPOW) THEN
       PRINT @(0,15):
       PRINT "YOU HAVE BEEN OVERRUN AND HAVE LOST THE ENTIRE DUKEDOM."
       GOTO 3100
     END
*
*  CALCULATE PRODUCTIVITY OF LAND LOST OR GAINED
*
     WARLAND=L(LWAR)
     FOR I=1 TO 3
       PARTLAND=INT(WARLAND/(4-I))
       IF -PARTLAND>SOIL(I) THEN
         ADDLAND=-SOIL(I)
       END ELSE
         ADDLAND=PARTLAND
       END
       SOIL(I)=SOIL(I)+ADDLAND
       WARLAND=WARLAND-ADDLAND
     NEXT I
*
*  ENEMY DUKE IS OVERRUN
*
     IF L(LWAR)<399 THEN GOTO 2600
     PRINT @(0,15):
     PRINT "YOU HAVE OVERRUN THE ENEMY AND HAVE ANNEXED HIS ":
     PRINT "ENTIRE DUKEDOM."
     G(GWAR)=3513
     GRAIN=GRAIN+G(GWAR)
     PEASLOSS=-47
     XYIELD=.55
*
*  KING IS WORRIED
*
     IF KING>0 THEN GOTO 2700
     KING=1
     PRINT @(0,16):"THE KING FEARS FOR HIS THRONE AND MAY BE PLANNING ":
     PRINT "DIRECT ACTION."
     GOTO 2700
*
*  PLAYER HAS WON OR LOST THE WAR
*
2600 IF DIFFPOW<0 THEN
       PRINT @(0,15):
       PRINT "YOU HAVE LOST THE WAR."
       XYIELD=G(CROPS)/LAND
     END ELSE
       PRINT @(0,15):
       PRINT "YOU HAVE WON THE WAR."
       XYIELD=6.7
       G(GWAR)=INT(1.7*L(LWAR))
       GRAIN=GRAIN+G(GWAR)
     END
*
*  CALCULATE NEW PEASANT AND GRAIN TOTALS
*
     IF PEASLOSS<0 THEN
       PEASLOSS=0
     END ELSE
       PEASLOSS=INT(PEASLOSS/10)
     END
2700 IF PEASLOSS>PEAS THEN PEASLOSS=PEAS
     P(WARVIC)=P(WARVIC)-PEASLOSS
     PEAS=PEAS-PEASLOSS
     G(CROPS)=G(CROPS)+INT(XYIELD+L(LWAR))
*
*  CALCULATE COST OF MERCENARIES
*
     MERCHIRE=40*MERC
     IF MERCHIRE>GRAIN THEN
       G(HIRE)=-GRAIN
       P(LOOTVIC)=-INT((MERCHIRE-GRAIN)/7)-1
       PRINT
       PRINT @(0,17):"THERE ISN'T ENOUGH GRAIN TO PAY THE MERCENARIES."
     END ELSE
       G(HIRE)=-MERCHIRE
     END
     GRAIN=GRAIN+G(HIRE)
     IF -P(LOOTVIC)>PEAS THEN P(LOOTVIC)=-PEAS
     PEAS=PEAS+P(LOOTVIC)
     LAND=LAND+L(LWAR)
2750 UNRESTST=UNRESTST-2*P(WARVIC)-3*(LOOTVIC)
*
*  DISEASES
*
2800 RAND=1+RND(10)
     IF RAND>3 THEN GOTO 2850
     IF RAND=2 OR RAND=3 THEN
       PRINT @(0,19):"A POX EPIDEMIC HAS BROKEN OUT."
       KILLDIV=5*RAND
     END ELSE
       IF DISEASE<=0 THEN
         PRINT @(0,20):"THE BLACK PLAGUE HAS STRUCK THE AREA."
         DISEASE=13
         KILLDIV=3
       END
     END
     P(DISVIC)=-INT(PEAS/KILLDIV)
     PEAS=PEAS+P(DISVIC)
*
*  CALCULATE BIRTHS AND NATURAL DEATHS
*
2850 IF P(LOOTVIC)=0 THEN RAND=5+RND(10) ELSE RAND=4.5
     P(BIRTH)=INT(PEAS/RAND)
     P(NATDEAD)=INT(.3-PEAS/22)
     PEAS=PEAS+P(BIRTH)+P(NATDEAD)
     DISEASE=DISEASE-1
*
*  HARVEST
*
     G(CROPS)=INT(G(CROPS)*YIELD)
     GRAIN=GRAIN+G(CROPS)
*
*  CASTLE EXPENSE
*
     SURPLUS=G(CROPS)-4000
     IF SURPLUS>0 THEN G(CASTLE)=-INT(.1*SURPLUS)
     G(CASTLE)=G(CASTLE)-120
     GRAIN=GRAIN+G(CASTLE)
*
*  ROYAL TAX
*
     IF KING>=0 THEN
       ROYALTAX=-INT(LAND/2)
     END ELSE
       GOTO 2900
     END
     IF KING=2 THEN ROYALTAX=2*ROYALTAX
     IF -ROYALTAX>GRAIN THEN
       PRINT @(0,22):
       PRINT "YOU HAVE INSUFFICIENT GRAIN TO PAY THE ROYAL TAX."
       PRINT "THE HIGH KING HAS ABOLISHED YOUR DUCAL RIGHT."
       GOTO 3100
     END
     G(TAX)=G(TAX)+ROYALTAX
     GRAIN=GRAIN+G(TAX)
*
*  CALCULATE LONG TERM PEASANT DISCONTENT
*
2900 UNRESTLT=INT(.85*UNRESTLT)+UNRESTST
*
*  ENDING TOTALS
*
     G(GEND)=GRAIN
     L(LEND)=LAND
     P(PEND)=PEAS
     PRINT @(27,23):"PRESS <RETURN> TO CONTINUE":
     INPUT Z
     GOTO 1200
*
*  PLAYER FIGHTS THE KING
*
3000 PRINT @(0,3):XBELOW:@(0,4):
     MERC=INT(GRAIN/100)
     PRINT "THE KING'S ARMY IS ABOUT TO ATTACK YOUR DUCHY!"
     PRINT
     PRINT "AT 100 HL. EACH (PAYMENT IN ADVANCE)"
     PRINT "YOU HAVE HIRED ":MERC:" FOREIGN MERCENARIES."
     PRINT
     PRINT
     IF 8*MERC+PEAS>2399 THEN
       PRINT "WIPE THE BLOOD FROM THE CROWN - YOU ARE NOW HIGH KING."
       PRINT
       PRINT "A NEARBY MONARCH THREATENS WAR"
       PRINT "HOW MANY ......"
       PRINT
       PRINT
       PRINT
     END ELSE
       PRINT "THE PLACEMENT OF YOUR HEAD ATOP THE CASTLE GATE ":
       PRINT "SIGNIFIES THAT"
       PRINT "THE HIGH KING HAS ABOLISHED YOUR DUCAL RIGHT."
       PRINT
       PRINT
       PRINT
     END
*
*  DOES PLAYER WANT TO TRY AGAIN?
*
3100 PRINT
     PRINT "DO YOU WANT TO TRY AGAIN? ":
     INPUT ANS
     IF ANS="YES" OR ANS="Y" THEN GOTO 1100
     IF ANS="NO" OR ANS="N" THEN
       PRINT ERASE:@(0,0):
       CHAIN 'GAMES'
       STOP
     END
     GOTO 3100
*
*  SPECIAL RANDOM NUMBER GENERATOR
*
3200 IF RND(2)=1 THEN
       XRAND=1+RND(10)
     END ELSE
       XRAND=(2+RND(10)+RND(10))/2
     END
     RETURN
*
*  CONTINUATION
*
3300 PRINT @(27,22):"PRESS <RETURN> TO CONTINUE":
     INPUT Z
     RETURN
*
*  PRINT CONDENSED REPORT
*
3400 PRINT @(12,1):
     PRINT "YEAR: ":YEAR "L##":
     PRINT SPACE(4):
     PRINT "PEASANTS: ":PEAS "L###":
     PRINT SPACE(4):
     PRINT "LAND: ":LAND "L#####":
     PRINT SPACE(4):
     PRINT "GRAIN: ":GRAIN "L#####":
     RETURN
END
