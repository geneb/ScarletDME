*  A FEW DAYS AT THE RACES
$*"OS TEST GROUP(C) ADDS 1988 - HSR"
*
*  CREATED BY HERB RUBIN SEPT. 1988
*
*  FROM THE IDEA BY CRAIG SHUCKER, CSU, LONG BEACH, CA
*
*  NOTES:
*  MAXIMUM OF 6 PLAYERS, AND 7 BETS EACH.
*  IT PLAYS AS MANY DAYS AS YOU WANT,
*  WITH 10 RACES PER DAY
*  AFTER EACH DAY, NEW HORSE RATINGS ARE CALCULATED.
*  HORSE ODDS ARE CALCULATED BY COMPARING RATINGS
*
* VARIABLES:
*  NP - NUMBER OF PLAYERS. IT DECREMENTS AS PLAYERS LOSE ALL THEIR MONEY.
*  NUMPLAYERS - SELF EXPLANATORY
*  HNAME(55) - EACH HORSES NAME
*  HRATING(55) - ODDS AND LUCK OF A HORSE PER DAY OF RACES
*  PLAYER(6) - NAME OF EACH PLAYER
*  MYHORSES(6,7) - EACH OF THE SIX PLAYERS HORSES CHOSEN. MAX OF 7 BETS EACH
*  MYBETS(6,7) - EACH OF THE SIX PLAYERS BET IN DOLLARS. MAX OF 7 BETS EACH
*  BETTYPE(6,7) - EACH OF THE SIX PLAYERS TICKET TYPE. WIN,PLACE,OR SHOW
*  HPOS(10) - 10 HORSES WILL RACE AND THIS IS THEIR SCREEN POSITION
*  LINEUP(10) - 1 TO 55. THIS IS THE ABSOLUTE HORSE(1-10 IS RELATIVE TO A RACE)
*  PLACE(3) - OUTCOME OF A RACE. WIN, PLACE OR SHOW
*  MONEY(6) - AMOUNT OF CASH OF EACH PLAYER
*
********************************************************

   CLEAR
   PROMPT ">"
   DIM MONEY(6);  * EACH PLAYERS WINNINGS 
   DIM MYHORSES(6,7),MYBETS(6,7),BETTYPE(6,7);  * EACH 6 PLAYERS, MAX 7 BETS EACH
   DIM HNAME(55),HPOS(10),LINEUP(10),PLACE(3);  * 55 DIFFERENT HORSES TO CHOOSE FROM
   DIM PLAYER(6), HRATING(55);  * EACH HORSE HAS A RATING THAT DETERMINES ODDS AND SLIGHT EDGE DURING RACE
   XLINE=SPACE(79)
   HORIZLINE ="-------------------------------------------------------------------------------"
   
********************************************************
*
*  PRINT INTRODUCTION
* 
********************************************************

   PRINT @(-1)
   PRINT @(19,4):"WELCOME TO:  A FEW DAYS AT THE RACES"
   PRINT @(24,6):"A BETTER SIMULATION"
   
1000
********************************************************
*
*  DETERMINE NUMBER OF PLAYERS
*  MAXIMUM OF SIX. WHY? NO PARTICULAR REASON!
*
********************************************************

   PRINT @(30,10):"HOW MANY PLAYERS? (MAX. 6) ":
   INPUT NUMPLAYERS
   IF NOT(NUM(NUMPLAYERS)) THEN
     PRINT @(32,12):"ILLEGAL INPUT"
     SLEEP 1
     CRT @(30,10):@(-3)
     GOTO 1000
    END
   IF NUMPLAYERS < 1 OR NUMPLAYERS > 6 THEN
     PRINT @(32,12):"MUST BE 1 TO 6"
     SLEEP 1
     CRT @(30,10):@(-3)
     GOTO 1000
    END
   NP = NUMPLAYERS
   DAY = 1
   CURRENT.RACE = 0
   FOR I = 1 TO 6; FOR J = 1 TO 6; MYHORSES(I,J)=-1;NEXT J;NEXT I
   GOSUB SELECT.RATINGS;  * FIRST DAYS RATINGS

*******************************************************
*
*  PROMPT FOR NAMES AND GIVE EACH A CASH AMOUNT $100-$199
*  RANDOM EDGE PER PLAYER. BESIDES, WHO EVER HEARD OF ALL
*  GAMBLERS STARTING WITH THE SAME AMOUNT OF MONEY?
*
*******************************************************

   FOR I=1 TO NP
     PRINT @(30,11+I):"ENTER PLAYER ":I:"'S NAME ":
     INPUT PLAYER(I)
     MONEY(I)= 100 + RND(5000)
   NEXT I
   
********************************************************
*
*  SET UP HORSE NAMES 
*  THIS TEDIOUS MESS BELOW IS DONE TO KEEP THE GAME ONE ITEM 
*  RATHER THAN HAVE IT READ EACH NAME FROM AN OPEN FILE
*  
********************************************************

   HNAME(1)="WHITE LADY"
   HNAME(2)="TAX JAM"
   HNAME(3)="CRAZY GLUE"
   HNAME(4)="TRUE BLUE"
   HNAME(5)="EMERGENCY EXIT"
   HNAME(6)="SEABREEZE"
   HNAME(7)="BLACK JACK"
   HNAME(8)="ACES HIGH"
   HNAME(9)="DADDY'S GIRL"
   HNAME(10)="LOST HORIZONS"
   HNAME(11)="GREY FOX"
   HNAME(12)="SOUTHERN COMFORT"
   HNAME(13)="TOP DOG"
   HNAME(14)="DEAD BATTERY"
   HNAME(15)="SILVER LINING"
   HNAME(16)="SLIM JIM"
   HNAME(17)="OPEN 24 HOURS"
   HNAME(18)="CAPITAL GAIN"
   HNAME(19)="BLANK CHECK"
   HNAME(20)="SMOKING GUN"
   HNAME(21)="FRENCH MAID"
   HNAME(22)="BUD MAN"
   HNAME(23)="JIGSAW PUZZLE"
   HNAME(24)="GRAND SLAM"
   HNAME(25)="LANDSLIDE"
   HNAME(26)="SHOOTING STAR"
   HNAME(27)="MY LITTLE PONY"
   HNAME(28)="REFERENCE FRAME"
   HNAME(29)="SLOW MOTION"
   HNAME(30)="INSTANT REPLAY"
   HNAME(31)="BLUE BELLE"
   HNAME(32)="CALCULATED RISK"
   HNAME(33)="PURPLE HAZE"
   HNAME(34)="LUCKY LADY"
   HNAME(35)="HARVEST MOON"
   HNAME(36)="HOT CHOCOLATE"
   HNAME(37)="STRAWBERRY FIELDS"
   HNAME(38)="HAM ON RYE"
   HNAME(39)="HIGH SPIRITS"
   HNAME(40)="LONG GONE"
   HNAME(41)="SWEET CHILD"
   HNAME(42)="PENNY ANTE"
   HNAME(43)="LIFE SAVINGS"
   HNAME(44)="4 WHEEL DRIVE"
   HNAME(45)="CURIOSITY"
   HNAME(46)="POST NO BILLS"
   HNAME(47)="SPEEDING TICKET"
   HNAME(48)="PEPPERMINT TWITS"
   HNAME(49)="FAULTY WIRING"
   HNAME(50)="TRUE ZOO REVUE"
   HNAME(51)="MRS. ED"
   HNAME(52)="CRACK OF DAWN"
   HNAME(53)="TAYLOR MADE"
   HNAME(54)="CHROME BUMPER"
   HNAME(55)="FUEL INJECTED"
   GOTO MAIN

SELECT.RATINGS:
***********************************************
*
*  SELECT RATINGS ONCE PER DAY
*  RANGE:0 TO 10
*  MEANING 1:ODDS CALCULATION - 11 MINUS RATING TO ONE ODDS
*  MEANING 2:RACE ADVANTAGE - RATING IS BONUS PERCENT MODIFIER
*    ON BASE CHANCE TO GAIN A STEP DURING RUN OF RACE
*
***********************************************

   FOR I = 1 TO 55
    HRATING(I) = RND(11); * ZERO TO 10 RANGE
   NEXT I
   RETURN

SELECT.HORSES:
***********************************************
*
* SELECT 10 HORSES FOR NEXT RACE
* LINEUP HOLDS THE ACTUAL INDEX (OUT OF 55)
* OF THE 10 PICKED HORSES
*
***********************************************

   DUPLICATE = "N"
   COUNT = 1
   LOOP WHILE COUNT <= 10 DO 
     R=RND(55) +1
     FOR I = 1 TO COUNT -1
       IF R = LINEUP(I) THEN DUPLICATE = "Y"
     NEXT I
     IF DUPLICATE = "Y" ELSE LINEUP(COUNT) = R; COUNT += 1
     DUPLICATE = "N"; * IF DUPLICATE = Y THEN DO NOTHING!
   REPEAT
   RETURN

PRINT.HORSES:
************************************************
*
*  EACH HORSE AND ITS ODDS BASED ON THE RATINGS
*  FOR PLACING BETS
*
************************************************

   FOR I = 1 TO 10
     HORSE = HNAME(LINEUP(I))
     PRINT I-1:" ":HORSE:STR(' ',20-LEN(HORSE)):11 - HRATING(LINEUP(I)):":1"
   NEXT I
   PRINT "                                               (maximum of 7 bets per player)"
   PRINT HORIZLINE
   GOSUB SHOW.SHIFTED.SCORES
   RETURN

SHOW.SHIFTED.SCORES:
***********************************************
* 
* SCORES IN $$ ARE PRINTED IN UPPER RIGHT CORNER
*
***********************************************

   PRINT @(38,0):"  PLAYER            CASH TOTAL"
   PRINT @(38,1):" ---------         ------------"
   FOR Z =1 TO NP
     PRINT @(59,Z+1):"*           "
     PRINT @(38,Z+1):"  ":PLAYER(Z):STR(' ',19-LEN(PLAYER(Z))):"$ ":MONEY(Z)
   NEXT Z
   RETURN

PLACE.BETS:
*************************************************
*
* PRINT RACING FORM AND ACCEPT MAX 7 BETS PER PLAYER PER RACE
* NEXT.BET - COUNTS THE NUMBER OF BETS A PLAYER HAS MADE ON CURRENT RACE
*
*************************************************

PROMPT " "
PRINT @(1,14):"DAY ":DAY
PRINT @(71,14):"RACE ":CURRENT.RACE
PRINT @(27,14):"GREEN ACRES RACING FORM"
FOR I = 1 TO NP
  NEXT.BET = 1
  PRINT @(9,16):"                    "
  PRINT @(1,16):"PLAYER: ":PLAYER(I)
  PRINT @(9,17):STR("-",LEN(PLAYER(I)))
  LOOP WHILE NEXT.BET < 8 DO
   BET = 'X'
    LOOP WHILE NOT(NUM(BET)) DO
      PRINT @(35,15+NEXT.BET):"BET: $":; INPUT BET
      IF BET = 'Q' THEN STOP
      IF BET = '' THEN GOTO DROP.OUT 
      IF BET > MONEY(I) THEN BET = 'X'
      IF BET < 0 THEN BET = 'X'
    REPEAT
    MYBETS(I,NEXT.BET) = BET
    MONEY(I) = MONEY(I) - BET; GOSUB SHOW.SHIFTED.SCORES
    HORSENUM = -1;  * FORCE ENTRY IN LOOP
    LOOP WHILE HORSENUM < 0 OR HORSENUM > 9 DO
      PRINT @(47,15+NEXT.BET):"HORSE #:":;INPUT HORSENUM
    REPEAT
    MYHORSES(I,NEXT.BET)= HORSENUM
    BET.TYPE = 'X'
    LOOP WHILE BET.TYPE <> 'W' AND BET.TYPE <> 'P' AND BET.TYPE <> 'S' DO
      PRINT @(58,15+NEXT.BET):"BET TICKET TYPE:":; INPUT BET.TYPE
    REPEAT
    BETTYPE(I,NEXT.BET) = BET.TYPE
    NEXT.BET = NEXT.BET + 1
  REPEAT
    PRINT @(9,16):STR(' ',LEN(PLAYER(I)))
    PRINT @(9,17):STR(' ',LEN(PLAYER(I)))
DROP.OUT:
  IF NEXT.BET = 8 THEN NEXT.BET = 7
  FOR K= 1 TO NEXT.BET 
    PRINT @(25,15+K):STR(' ',54)
  NEXT K
NEXT I
GOSUB WAIT.TIL.READY
RETURN

RUN.RACE:
*************************************************
*
* SET UP TRACK AND RUN RACE
*
*************************************************

PRINT @(-1)
FOR I = 1 TO 10 
   HPOS(I) = 19
   PRINT @(1,10+I):HNAME(LINEUP(I)):STR(' ',18-LEN(HNAME(LINEUP(I)))):" ~*":I-1:"*^":@(79,10+I):"|"
NEXT I
FOR I = 1 TO 3
   PLACE(I) = -1;   * CLEAR PLACES
NEXT I
PRINT @(29,5):"GREEN ACRES RACEWAY"
SLEEP 1
PRINT @(20,7):"BANG! ...AND THEY'RE OFF!"
EVENT = "NOT OVER"
CHANCE = 50;  * GET ALL HORSES TO GET A JUMP OUT OF THE GATE.
FOR I = 1 TO 10
  GOSUB ONE.STEP
NEXT I
CHANCE = 30;  * GIVE 'EM A RUN FOR THEIR MONEY
FOR I = 1 TO 10
  GOSUB ONE.STEP
NEXT I
LOOP WHILE EVENT = "NOT OVER" DO
  GOSUB ONE.STEP
  IF PLACE(1) <> -1 THEN EVENT = "OVER"
REPEAT
PRINT @(9,0):" STANDINGS"
PRINT @(6,1):"---------------"
PRINT @(5,2):11-HRATING(LINEUP(PLACE(1))):":1":@(10,2):PLACE(1)-1:' ':HNAME(LINEUP(PLACE(1))):" WINS"
PRINT @(5,3):11-HRATING(LINEUP(PLACE(2))):":1":@(10,3):PLACE(2)-1:' ':HNAME(LINEUP(PLACE(2))):" 2ND"
PRINT @(5,4):11-HRATING(LINEUP(PLACE(3))):":1":@(10,4):PLACE(3)-1:' ':HNAME(LINEUP(PLACE(3))):" 3RD"
RETURN

ONE.STEP:
*************************************************
*
* SEE IF EACH HORSE GETS TO GO ONE STEP, 
* ONCE A HORSE STEPS ACROSS FINISH LINE HIS POSITION IS CHANGED TO 
* THE FLAG OF -1.
* TRUE, THAT THE FIRST HORSE OVER THE LINE IS 1ST PLACE AND MOST
* LIKELY IN A TIE SITUATION THE LOWER # HORSE WOULD UNFAIRLY WIN,
* BUT IT IS VERY RARE, SO DON'T WORRY ABOUT IT.
*
*************************************************

FOR HO = 1 TO 10
R=RND(100) + 1
  IF HPOS(HO) = -1 THEN R = 100
  IF R <= CHANCE + HRATING(LINEUP(HO)) THEN HPOS(HO) +=1;PRINT @(HPOS(HO),10+HO):" ~*":HO-1:"*^":
  IF HPOS(HO) > 73 THEN HPOS(HO) = -1; PLACE(1)= PLACE(2); PLACE(2)=PLACE(3); PLACE(3)= HO
NEXT HO
RETURN

TAB.RESULTS:
*************************************************
*
* UPDATE WINNERS CASH TOTAL
* AND IN THE PROCESS CLEAR OUT ALL THE BETS BY INITIALIZING
* ALL CHOSEN HORSE #S TO -1
* WIN = FULL / PLACE = HALF / SHOW = QUARTER
*
*************************************************

FOR I = 1 TO NP
  FOR J = 1 TO 7
    BEGIN CASE
      CASE MYHORSES(I,J) = PLACE(1) -1
             IF BETTYPE(I,J) = "W" THEN MONEY(I) =MONEY(I) + MYBETS(I,J) + MYBETS(I,J) * (11-HRATING(LINEUP(PLACE(1))))
             IF BETTYPE(I,J) = "P" THEN MONEY(I) =MONEY(I) + MYBETS(I,J) + INT(MYBETS(I,J) * (11-HRATING(LINEUP(PLACE(1)))))/2
             IF BETTYPE(I,J) = "S" THEN MONEY(I) =MONEY(I) + MYBETS(I,J) + INT(MYBETS(I,J) * (11-HRATING(LINEUP(PLACE(1)))))/4
      CASE MYHORSES(I,J) = PLACE(2) -1
             IF BETTYPE(I,J) = "P" THEN MONEY(I) =MONEY(I) + MYBETS(I,J) + INT(MYBETS(I,J) * (11-HRATING(LINEUP(PLACE(2)))))/2
             IF BETTYPE(I,J) = "S" THEN MONEY(I) =MONEY(I) + MYBETS(I,J) + INT(MYBETS(I,J) * (11-HRATING(LINEUP(PLACE(1)))))/4
      CASE MYHORSES(I,J) = PLACE(3) -1
             IF BETTYPE(I,J) = "S" THEN MONEY(I) =MONEY(I) + MYBETS(I,J) + INT(MYBETS(I,J) * (11-HRATING(LINEUP(PLACE(3)))))/4
    END CASE
    MYHORSES(I,J)=-1
  NEXT J
NEXT I
RETURN

GONE.BROKE:
************************************************
*
* SEE IF ANY PLAYER SHOULD BE TERMINATED
*
************************************************

PRINT @(5,9):
TEST=NP
I = 1
LOOP WHILE I <> NP DO
  IF MONEY(I) = 0 THEN
  PRINT PLAYER(I):", ":
    FOR J = I TO NP -1
      MONEY(J) = MONEY(J+1)
      PLAYER(J)=PLAYER(J+1)
    NEXT J
    NP = NP -1
   END ELSE I += 1
REPEAT
IF NP = 1 AND MONEY(1) = 0 THEN 
   IF TEST <> NP THEN PRINT PLAYER(1):", HAS LOST HIS (OR HER) SHIRT)!"
   PRINT @(1,22);GOSUB WAIT.TIL.READY; 
   PRINT @(-1):@(34,10):"GAME OVER"; STOP
  END ELSE NULL
IF MONEY(NP) = 0 THEN NP = NP -1; PRINT PLAYER(NP):", ":
IF TEST <> NP THEN PRINT "HAS LOST HIS (OR HER) SHIRT!"
RETURN

SHOW.RESULTS:
************************************************
*
*  PRINT RESULTS IN UPPER CORNER
*  WHILE STILL SHOWING THE RACE TRACK
*
************************************************

GOSUB SHOW.SHIFTED.SCORES
RETURN

MAIN:
**********************************************
*
* THIS RUNS THE WHOLE GAME IN A LOOP
* AFTER EACH DAY, NEW RATINGS ARE SELECTED FOR ALL 55 HORSES
*
**********************************************

PRINT @(-1)
PRINT
PRINT 
CURRENT.RACE +=1 
PRINT "            DAY: ":DAY:"     RACE: ":CURRENT.RACE
IF CURRENT.RACE > 10 THEN DAY += 1; CURRENT.RACE =1; GOSUB SELECT.RATINGS
GOSUB SELECT.HORSES
PRINT @(-1):"   HORSE              ODDS"
PRINT "  -------            -----"
GOSUB PRINT.HORSES
GOSUB PLACE.BETS
GOSUB RUN.RACE
GOSUB TAB.RESULTS
PRINT @(0,5):XLINE
PRINT @(0,7):XLINE
GOSUB SHOW.RESULTS
GOSUB GONE.BROKE
PRINT @(1,22):"TYPE 'Q' TO QUIT"
GOSUB WAIT.TIL.READY
GOTO MAIN

WAIT.TIL.READY:
***********************************************
*
*  WAIT FOR USER TO DIGEST SCREEN SUBROUTINE
*  AT ANY TIME HIT Q TO QUIT
*
***********************************************

PRINT "                        Hit RETURN to continue.":
INPUT RESPONSE
IF RESPONSE = "Q" THEN STOP
RETURN
