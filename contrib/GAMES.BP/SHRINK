*SIMULATES PSYCHOANALYSIS. USES A FILE THAT WE DONT HAVE
*
*    WRITTEN:    12 SEP 1977   LJB
     PRINT 'DR YOUNG WILL SEE YOU IN A MOMENT'
     OPEN '','GAMES' TO FILE ELSE PRINT "GAMES FILE MISSING"
     READ REP.LIST FROM FILE,'REPLIES' ELSE PRINT "REPLIES RECORD MISSING"
     READ CJ.LIST FROM FILE,'CONJUGATIONS' ELSE PRINT "CONJUGATIONS MISSING"
     READ KW.LIST FROM FILE,'KEY-WORDS' ELSE PRINT "KEY-WORDS MISSING"
*
     PRINT 'TUT,TUT, WHAT SEEMS TO BE THE PROBLEM':
     PREV = ''
100  INPUT RESP
     IX = 1
     KI = 0
     KW = 'DUMMY'
     IF INDEX(RESP,'SHUT',1) THEN STOP
     FOR I = 1 TO LEN(RESP) UNTIL IX = 0
       IX = INDEX(RESP,"'",1)
       IF IX THEN RESP = RESP[1,IX-1]:RESP[IX+1,99]
     NEXT I
     IF RESP = PREV THEN
       PRINT "PLEASE DON'T REPEAT YOURSELF"
       GOTO 100
     END
     FOR K = 1 TO 100 UNTIL KW = '' OR KI > 0
       KEY.WORD = 'DUMMY'
       KI = 0
       FOR KK = 1 TO 10 UNTIL KEY.WORD = '' OR KI > 0
         KW = KEY.WORD
         KEY.WORD = EXTRACT(KW.LIST,K,KK,0)
         IF KEY.WORD = 'NOKEYFOUND' THEN KI = 1 ELSE
           LK = LEN(KEY.WORD)
           IF LK THEN KI = INDEX(RESP,KEY.WORD,1)
         END
         IF KI = 0 THEN KI = INDEX(KEY.WORD,RESP,1)
       NEXT KK
     NEXT K
     K = K-1
     IF KI THEN KI = KI + LK ELSE KI = 99
110  REPLY = ' ':RESP[KI,99]
     CONJUG1 = 'DUMMY'
     FOR C = 1 TO 100 UNTIL CONJUG1 = ''
       CONJUG1 = EXTRACT(CJ.LIST,C,1,0)
       CONJUG2 = EXTRACT(CJ.LIST,C,2,0)
       LCJ1 = LEN(CONJUG1)
       LCJ2 = LEN(CONJUG2)
       IF LCJ1 = 0 THEN GOTO 150
120    CI = INDEX(REPLY,CONJUG1,1)
       IF CI THEN
         REPLY = REPLY[1,CI-1]:'**':REPLY[CI+LCJ1,99]
         GOTO 120
       END
130    CI = INDEX(REPLY,CONJUG2,1)
       IF CI THEN
         REPLY = REPLY[1,CI-1]:CONJUG1:REPLY[CI+LCJ2,99]
         GOTO 130
       END
140    CI = INDEX(REPLY,'**',1)
       IF CI THEN
         REPLY = REPLY[1,CI-1]:CONJUG2:REPLY[CI+2,99]
         GOTO 140
       END
150  NEXT C
     REPLY = TRIM(REPLY)
     LIZ = EXTRACT(REP.LIST,K,1,0)
     LL = LEN(LIZ)
     REP.LIST = DELETE(REP.LIST,K,1,0)
     REP.LIST = INSERT(REP.LIST,K,-1,0,LIZ)
     IF LIZ[LL,1] = '*' THEN LIZ = LIZ[1,LL-1]:' ':REPLY
     PRINT LIZ
     PREV = RESP
     GOTO 100
END
