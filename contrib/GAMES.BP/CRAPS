*CRAPS - NO MONEY DICE
     TIMES=0
     BET.MAX=19
     DIM BETTER(9),MONEY(9),BETS(9,19)
     DIM BET.TYPE(19),BORROWED(9)
     MAT MONEY=100
     MAT BORROWED=0
     MAT BETS=0
     BET.TYPE(1)="PASS"
     BET.TYPE(2)="DON'T PASS"
     BET.TYPE(3)="ANY CRAPS"
     BET.TYPE(4)="2"
     BET.TYPE(5)="3"
     BET.TYPE(6)="12"
     BET.TYPE(7)="7"
     BET.TYPE(8)="11"
     BET.TYPE(9)="FIELD"
     BET.TYPE(10)="HARD 4"
     BET.TYPE(11)="HARD 6"
     BET.TYPE(12)="HARD 8"
     BET.TYPE(13)="HARD 10"
     BET.TYPE(14)="PLACE 4"
     BET.TYPE(15)="PLACE 5"
     BET.TYPE(16)="PLACE 6"
     BET.TYPE(17)="PLACE 8"
     BET.TYPE(18)="PLACE 9"
     BET.TYPE(19)="PLACE 10"
     BLANK=' '
     TOP=@(-1)
1    PRINT TOP:'HOW MANY PLAYERS WILL THERE BE (1-9)':
     INPUT NO.PLAYERS
     IF NO.PLAYERS MATCHES '0N' THEN GOTO 2
     PRINT 'ILLEGAL RESPONSE'
     GOTO 1
2    PRINT "PLEASE ENTER THE PLAYER'S NAMES ONE AT A TIME"
     FOR I=1 TO NO.PLAYERS
       INPUT BETTER(I)
     NEXT I
     PRINT 'YOU ALL HAVE $100 TO START WITH'
*FIRST.ROLL = 0 IMPLIES IT IS THE COME OUT ROLL
90   FIRST.ROLL=0
     TIMES=TIMES+1
     POINT=0
100  PRINT
     PRINT 'PLACE YOUR BETS'
     FOR I = 1 TO NO.PLAYERS
101    PRINT BETTER(I):' HAS $':MONEY(I):
       IF BORROWED(I) GT 0 THEN PRINT ' BUT OWES $':BORROWED(I):
       PRINT
       FOR J=1 TO 19
         IF BETS(I,J)#0 THEN PRINT SPACE(5-LEN(BETS(I,J))):BETS(I,J):BLANK:BET.TYPE(J)
       NEXT J
110    INPUT LINE
       IF LINE MATCHES '' THEN GOTO 1000
       BET=FIELD(LINE,BLANK,1)
       IF BET MATCHES '0N' THEN GOTO 120
       IF BET MATCHES '"-"0N' THEN GOTO 120
       IF BET='P' THEN
         IF POINT#0 THEN PRINT 'THE CURRENT POINT IS ':POINT
         GOTO 110
       END
       IF BET='B' THEN
         BET=FIELD(LINE,BLANK,2)
         IF BET MATCHES '0N' THEN
           IF BET+BORROWED(I) GT 10*TIMES THEN
             PRINT 'SORRY THE CREDIT LIMIT IS ONLY $':10*TIMES
             GOTO 110
           END
113        BORROWED(I)=BORROWED(I)+BET
           MONEY(I)=MONEY(I)+BET
           GOTO 101
         END
         IF BET MATCHES '"-"0N' THEN
           IF MONEY(I) LT ABS(BET) THEN GOTO 121
           IF BORROWED(I) GE ABS(BET) THEN GOTO 113
           IF BORROWED(I) = 0 THEN PRINT "YOU DON'T OWE ANYTHNG";GOTO 110
           PRINT 'YOU ONLY OWE $':BORROWED(I)
           GOTO 110
         END
       END
       PRINT 'BET MUST BE NUMERIC'
       GOTO 110
120    IF BET > MONEY(I) THEN
121      PRINT 'YOU ONLY HAVE $':MONEY(I)
         GOTO 110
       END
       ABREV=FIELD(LINE,BLANK,2)
       TYPE.BET=FIELD(LINE,BLANK,3)
       TYPE.BET.2=FIELD(LINE,BLANK,4)
       IF TYPE.BET='THE' THEN TYPE.BET=TYPE.BET.2;TYPE.BET.2=FIELD(LINE,BLANK,5)
       IF ABREV = 'P' THEN GOTO 130
       IF ABREV = 'D' THEN GOTO 131
       IF ABREV = 'A' THEN GOTO 132
       IF ABREV = '2' THEN GOTO 133
       IF ABREV = '3' THEN GOTO 134
       IF ABREV = '12' THEN GOTO 135
       IF ABREV = '7' THEN GOTO 136
       IF ABREV = '11' THEN GOTO 137
       IF ABREV = 'F' THEN GOTO 138
       IF ABREV = 'H4' THEN GOTO 139
       IF ABREV = 'H6' THEN GOTO 140
       IF ABREV = 'H8' THEN GOTO 141
       IF ABREV = 'H10' THEN GOTO 142
       IF ABREV = 'P4' THEN GOTO 143
       IF ABREV = 'P5' THEN GOTO 144
       IF ABREV = 'P6' THEN GOTO 145
       IF ABREV = 'P8' THEN GOTO 146
       IF ABREV = 'P9' THEN GOTO 147
       IF ABREV = 'P10' THEN GOTO 148
       IF TYPE.BET # 'PASS' THEN GOTO 200
130    IF FIRST.ROLL#0 THEN
         PRINT 'PASS LINE BETS ARE ONLY LEGAL ON COME OUT ROLL'
         GOTO 110
       END
       K=1
       GOTO 900
150    PRINT 'ILLEGAL TYPE OF BET'
       GOTO 110
160    PRINT 'PLACE BETS MUST BE IN MULTIPLES OF 5'
       GOTO 110
170    PRINT 'PLACE BETS MUST BE IN MULTIPLES OF 6'
       GOTO 110
180    PRINT 'ONLY THE AMOUNT OF THE BET CAN BE WITHDRAWN'
       GOTO 110
200    IF TYPE.BET#"DON'T" THEN GOTO 250
       IF TYPE.BET.2#'PASS' THEN GOTO 150
131    IF FIRST.ROLL#0 THEN
         PRINT "DON'T PASS BETS ARE ONLY LEGAL ON COME OUT ROLL"
         GOTO 110
       END
       K=2
       GOTO 900
250    IF TYPE.BET # 'ANY' THEN GOTO 300
       IF TYPE.BET.2 # 'CRAPS' THEN GOTO 150
132    K=3
       GOTO 900
300    IF TYPE.BET # 2 THEN GOTO 350
133    K=4
       GOTO 900
350    IF TYPE.BET # 3 THEN GOTO 400
134    K=5
       GOTO 900
400    IF TYPE.BET # 12 THEN GOTO 450
135    K=6
       GOTO 900
450    IF TYPE.BET # 7 THEN GOTO 500
136    K=7
       GOTO 900
500    IF TYPE.BET # 11 THEN GOTO 550
137    K=8
       GOTO 900
550    IF TYPE.BET # 'FIELD' THEN GOTO 600
138    K=9
       GOTO 900
600    IF TYPE.BET # 'HARD' THEN GOTO 700
       IF TYPE.BET.2 # 4 THEN GOTO 625
139    K=10
       GOTO 900
625    IF TYPE.BET.2 # 6 THEN GOTO 650
140    K=11
       GOTO 900
650    IF TYPE.BET.2 # 8 THEN GOTO 675
141    K=12
       GOTO 900
675    IF TYPE.BET.2 # 10 THEN GOTO 150
142    K=13
       GOTO 900
700    IF TYPE.BET # 'PLACE' THEN GOTO 150
       IF TYPE.BET.2 # 4 THEN GOTO 710
143    IF BET # (BET/5)*5 THEN GOTO 160
       K=14
       GOTO 900
710    IF TYPE.BET.2 # 5 THEN GOTO 720
144    IF BET # (BET/5)*5 THEN GOTO 160
       K=15
       GOTO 900
720    IF TYPE.BET.2 # 6 THEN GOTO 730
145    IF BET # (BET/6)*6 THEN GOTO 170
       K=16
       GOTO 900
730    IF TYPE.BET.2 # 8 THEN GOTO 740
146    IF BET # (BET/6)*6 THEN GOTO 170
       K=17
       GOTO 900
740    IF TYPE.BET.2 # 9 THEN GOTO 750
147    IF BET # (BET/5)*5 THEN GOTO 160
       K=18
       GOTO 900
750    IF TYPE.BET.2 # 10 THEN GOTO 150
148    IF BET # (BET/5)*5 THEN GOTO 160
       K=19
900    BET.1=BETS(I,K)+BET
       IF BET.1 < 0 THEN GOTO 180
       BETS(I,K)=BET.1
       MONEY(I)=MONEY(I)-BET
       GOTO 110
1000 NEXT I
     D=RND(600)+100
     DICE1=D[1,1]
     D=RND(300)+RND(300)+100
     DICE2=D[1,1]
     PRINT 'THE ROLL IS ':DICE1:BLANK:DICE2
     SUM=DICE1+DICE2
*TO TAKE CARE OF ONE ROLL BETS
     GOSUB 2000
*TO TAKE CARE OF HARD BETS AND PLACE BETS
     GOSUB 2500
     IF FIRST.ROLL#0 THEN GOTO 1030
     IF SUM=7 THEN GOTO 1010
     IF SUM=11 THEN GOTO 1010
     IF SUM=2 THEN GOTO 1020
     IF SUM=3 THEN GOTO 1020
     IF SUM=12 THEN GOTO 1020
     PRINT 'THE POINT IS ':SUM
     POINT=SUM
     FIRST.ROLL=1
     GOTO 100
1010 PRINT 'SHOOTER WINS'
     FOR I=1 TO NO.PLAYERS
       MONEY(I)=MONEY(I)+BETS(I,1)
       BETS(I,2)=0
     NEXT I
     GOTO 90
1020 PRINT 'SHOOTER LOSES'
     FOR I=1 TO NO.PLAYERS
       IF SUM # 12 THEN
         MONEY(I)=MONEY(I)+BETS(I,2)
       END
       BETS(I,1)=0
     NEXT I
     GOTO 90
1030 IF SUM=POINT THEN GOTO 1010
     IF SUM=7 THEN GOTO 1020
     GOTO 100
2000 FOR I=1 TO NO.PLAYERS
       IF SUM=2 THEN MONEY(I)=MONEY(I)+BETS(I,3)*9+BETS(I,4)*31+BETS(I,9)*3
       IF SUM=3 THEN MONEY(I)=MONEY(I)+BETS(I,3)*9+BETS(I,5)*16+BETS(I,9)*2
       IF SUM=4 THEN MONEY(I)=MONEY(I)+BETS(I,9)*2
       IF SUM=9 THEN MONEY(I)=MONEY(I)+BETS(I,9)*2
       IF SUM=10 THEN MONEY(I)=MONEY(I)+BETS(I,9)*2
       IF SUM=12 THEN MONEY(I)=MONEY(I)+BETS(I,3)*9+BETS(I,6)*31+BETS(I,9)*3
       IF SUM=7 THEN MONEY(I)=MONEY(I)+BETS(I,7)*6
       IF SUM=11 THEN MONEY(I)=MONEY(I)+BETS(I,8)*16+BETS(I,9)*2
       FOR J=3 TO 9
         BETS(I,J)=0
       NEXT J
     NEXT I
     RETURN
2500 IF SUM = 7 THEN
       FOR I=1 TO NO.PLAYERS
         K=19
         IF FIRST.ROLL = 0 THEN K=13
         FOR J=10 TO K
           BETS(I,J)=0
         NEXT J
       NEXT I
       RETURN
     END
     IF DICE1 = DICE2 THEN
       FOR I=1 TO NO.PLAYERS
         IF SUM=4 THEN MONEY(I)=MONEY(I)+BETS(I,10)*8+(BETS(I,14)/5)*9*FIRST.ROLL
         IF SUM=6 THEN MONEY(I)=MONEY(I)+BETS(I,11)*10+(BETS(I,16)/6)*7*FIRST.ROLL
         IF SUM=8 THEN MONEY(I)=MONEY(I)+BETS(I,12)*10+(BETS(I,17)/6)*7*FIRST.ROLL
         IF SUM=10 THEN MONEY(I)=MONEY(I)+BETS(I,13)*8+(BETS(I,19)/5)*9*FIRST.ROLL
       NEXT I
       RETURN
     END
     FOR I=1 TO NO.PLAYERS
       IF SUM=4 THEN BETS(I,10)=0;MONEY(I)=MONEY(I)+(BETS(I,14)/5)*9*FIRST.ROLL
       IF SUM=5 THEN MONEY(I)=MONEY(I)+(BETS(I,15)/5)*7*FIRST.ROLL
       IF SUM=6 THEN BETS(I,11)=0;MONEY(I)=MONEY(I)+(BETS(I,16)/6)*7*FIRST.ROLL
       IF SUM=8 THEN BETS(I,12)=0;MONEY(I)=MONEY(I)+(BETS(I,17)/6)*7*FIRST.ROLL
       IF SUM=9 THEN MONEY(I)=MONEY(I)+(BETS(I,18)/5)*7*FIRST.ROLL
       IF SUM=10 THEN BETS(I,13)=0;MONEY(I)=MONEY(I)+(BETS(I,19)/5)*9*FIRST.ROLL
     NEXT I
     RETURN
END
