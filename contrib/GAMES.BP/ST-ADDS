*ANOTHER STAR-TREK PROGRAM
     LCA=CHAR(0);BLINK=CHAR(7);UNBLINK=CHAR(0);CM=@(-1);EEOP='                                                                     ':CHAR(13)
     HOME=CHAR(30); EEOL='            '
     PROMPT '?'
     DIM R(20,9),C(20,9),T(20,3),E(20,8)
     DIM CH(20,3), CL(20,3)
     DIM F(15,34),FF(16,32)
     DIM S(5,8),D(5,18)
     D(1)='WARP ENGINES'
     D(2)='FIRE CONTROL'
     D(3)='SENSORS'
     D(4)='LAUNCH CONTROL'
     D(5)='REACTOR CONTROL'
     PRINT 'DO YOU WANT TO BE BRIEFED ':
     INPUT COM; IF COM[1,1] NE 'Y' THEN GOTO 5
     PRINT CM:'THE PURPOSE OF THIS GAME IS TO DESTROY THE KLINGON ENEMY'
     PRINT    'VESSELS, WHICH ATTACK BOTH THE ENTERPRISE AND THE STAR-BASE'
     PRINT    'AT EACH COMMAND REQUEST, YOU CAN:'
     PRINT    '"M" - MOVE AFT, FORWARD, PORT OR STARBOARD ("A", "F", "P" OR "S");'
     PRINT    '      ENERGY USED IS THE SQUARE OF THE DISTANCE MOVED.'
     PRINT    '      BY "DOCKING" (COINCIDENCE WITH BASE), YOU GET 1000 UNITS.'
     PRINT    '"R" - RECHARGE THE REACTORS'
     PRINT    '"L" - LAUNCH 1 TO 4 FIGHTERS; ENERGY USED IS 50 UNITS'
     PRINT    '"F" - FIRE AT A SPECIFIC KLINGON; ENERGY USED IS 10 TIMES'
     PRINT    '      THE NUMBER OF PHOTON TORPEDOS FIRED.'
     PRINT    '"D" - CHANGE THE EXPANSION FACTOR (SCALE) OF THE DISPLAY.'
     PRINT; PRINT 'NOW ENTER STARTING CONDITIONS REQUIRED.... GOOD LUCK!'
5    PRINT 'DISTANCE TO BASE (30-80)':
     INPUT DIFFICULTY
     IF DIFFICULTY < 0 OR DIFFICULTY > 80 THEN GOTO 5
10   PRINT 'NUMBER OF KLINGONS (MAX=10)':
     INPUT NUMENEMY
     IF NUMENEMY>10 THEN GOTO 10
100  PRINT CM: ;* CLEAR SCREEN
     EF= DIFFICULTY/11
     MAT R=''; MAT T=''; MAT S=0; MAT C=-99; MOVES=0; MAT FF=' '
     MAT CH= ''; MAT CL=''
     NUMFIGHTERS=0
***** SELECT POSITION OF ENTERPRISE
     T(1)='E'; E(1)=2000; R(1)=RND(50)+100; C(1)=RND(50)+100; ENTROW=R(1); ENTCOL=C(1)
     J=1; GOSUB12300
***** SELECT POSITION OF BASE
     T(2)='B'; E(2)=500
     TEMP=RND(2);IF TEMP=0 THEN TEMP=-1
     R(2)=ENTROW+INT(DIFFICULTY/(RND(3)+2))*TEMP
     TEMP=RND(2);IF TEMP=0 THEN TEMP=-1
     C(2)=ENTCOL+(DIFFICULTY-ABS(R(2)-ENTROW))*TEMP
     J=2; GOSUB 12300
     TEMP=3+RND(9); R(TEMP)='0'
***** SELECT POSITION OF KLINGONS
     FOR I=1 TO NUMENEMY
       T9='K'; S9=100+RND(145); GOSUB 12000
     NEXT I
     R(TEMP)=''
     KLINGONS=NUMENEMY; NUM=NUMENEMY+2
************************************************
500  GOSUB 2300;* DISPLAY MATRIX
1000 GOSUB 13000; PRINT 'COMMAND ':
     MAT CH=''; MAT CL=''
     INPUT COM; COM1=COM[1,1]; COM2=COM[2,15]
     IF COM1 EQ 'M' THEN GOTO 1100
     IF COM1 EQ 'F' THEN GOTO 1200
     IF COM1 EQ 'D' THEN GOTO 1300
     IF COM1 EQ 'L' THEN GOTO 2800
     IF COM1 EQ 'R' THEN GOTO 2900
     PRINT "COMMAND IS 'M' (MOVE), 'F' (FIRE), 'L' (LAUNCH FIGHTERS)"
     PRINT "           'D' (DISPLAY) OR 'R' (RECHARGE REACTOR)"
     GOTO 1000
1100 *  MOVE
     PRINT EEOP:; IF COM2 NE '' THEN COM=COM2[1,1]; COM2=COM2[2,15]; GOTO 1105
     PRINT 'HEADING ':; INPUT COM
1105 IF COM EQ 'F' OR COM EQ 'S' OR COM EQ 'A' OR COM EQ 'P' THEN GOTO 1110
     PRINT "HEADING IS 'F' (FORWARD), 'A' (AFT), 'P' (PORT) OR 'S' (STARBOARD)"
     GOTO 1000
1110 IF COM2 NE '' THEN GOTO 1115
     PRINT EEOP:'DISTANCE ':; INPUT COM2
1115 IF COM2 <= 0 THEN PRINT 'ILLEGAL DISTANCE' ; GOTO 1000
     IF COM2*COM2 > E(1) THEN PRINT 'INSUFFICIENT ENERGY      '; GOTO 1000
     IF S(1)<0 AND COM2>3 THEN PRINT D(1):' DAMAGED. MAX DISTANCE = 3      ';GOTO 1000
     E(1)=E(1)-COM2*COM2
     IF COM EQ 'F' THEN R(1)=R(1)-COM2; GOTO 1140
     IF COM EQ 'A' THEN R(1)=R(1)+COM2; GOTO 1140
     IF COM EQ 'S' THEN C(1)=C(1)+COM2; GOTO 1140
     C(1)=C(1)-COM2
1140 J=1; GOSUB 12390; GOSUB 12490;* PRINT ENTERPRISE'S NEW LOCATION & ENERGY
     GOSUB 12480
     ENTROW=R(1); ENTCOL=C(1)
     FOR J=2 TO 16
       IF R(J) NE '' THEN GOSUB 12450; GOSUB 12550
     NEXT J
************************************************
1150 *  CHECK FOR DOCKING
     IF ENTROW=R(2) AND ENTCOL=C(2) THEN
       E(1)=E(1)+1000; CL(1)='X'
       J=1; GOSUB 12390; PRINT BLINK:' DOCKED!':UNBLINK:CHAR(7)
     END
     IF E(1) > 2000 THEN
       E(1)=2000; CL(1)='X'
       J=1; GOSUB 12390; PRINT ' MAX. ENERGY!':
     END
     MOVES=MOVES+1 ; * COUNT NUMBER OF ENTERPRISE MOVES.
     GOSUB 3000 ; * BASE, KLINGONS, AND FIGHTERS MOVE.
*  CHANGE STATUS
     GOSUB 13000; PRINT EEOP
     FOR I=1 TO 5
       IF S(I)<0 THEN S(I)=S(I)+1; PRINT EEOP:D(I):' STATUS = ':S(I)
     NEXT I
     IF RND(10)<5 THEN GOTO 500
     TEMP = RND(5)+1
     S(TEMP) = S(TEMP)-(RND(3)+1)
     PRINT EEOP:CHAR(7):'SABOTAGE! ': D(TEMP):' STATUS = ':S(TEMP)
     IF E(1) < 200 THEN IF ENTCOL=C(2) OR ENTROW=R(2) THEN
       TEMP=ABS(ENTCOL-C(2))+ABS(ENTROW-R(2))
       IF TEMP > 3 AND S(1) < 0 THEN GOTO 500
       IF TEMP*TEMP >= E(1) THEN GOTO 500
       IF RND(10) < 2 THEN PRINT EEOP:'FROM BASE COMMANDER: WE SUGGEST THAT YOU DOCK, CAPTAIN KIRK.':
     END
     GOTO 500
************************************************:
1200 *  FIRE
     PRINT EEOP:
     IF S(2)<0 THEN PRINT D(2):' NOT OPERATIONAL'; GOTO 1000
     PRINT 'TARGET COORDINATES':
     INPUT COM
     R9=FIELD(COM,',',1)
     C9=FIELD(COM,',',2)
     IF R9>0 AND C9 > 0 THEN GOTO 1225
1205 PRINT 'ILLEGAL VALUE!   '; GOTO 1000
1225 PRINT EEOP:'PHOTON TORPEDOS':
     INPUT COM2; IF COM2 <= 0 THEN GOTO 1205
     IF COM2*10<E(1) THEN GOTO 1230
     PRINT 'INSUFFICIENT ENERGY!   '; GOTO 1000
1230 E(1)=E(1)-COM2*10
     GOSUB 11000; J=1
     IF FLAG=0 THEN PRINT 'MISS!!   ':CHAR(7); GOSUB 12390; GOTO 1150
     GOSUB 12390
     TEMP = ABS(ENTROW-R9)+ABS(ENTCOL-C9)
     S9=INT(COM2*10*RND(10)/(TEMP+1))
     E(ROW2)=E(ROW2)-S9
     J=ROW2; GOSUB 12390
     GOSUB 9777
     GOTO 1150
**********************************************::
1300 *  DISPLAY.
     PRINT EEOP:
     IF S(3)<0 THEN PRINT D(3):' NOT OPERATIONAL'; GOTO 1000
     PRINT 'EXPANSION FACTOR ':
     INPUT COM2
     IF COM2 <= 0 THEN
       OLDMOVES=MOVES; MOVES=0; PRINT CM
       MAT FF = ' '
       FOR J=1 TO 16
         IF R(J) NE '' THEN GOSUB 12300
       NEXT J
       GOSUB 2300
       MOVES=OLDMOVES; GOTO 1000
     END
     GOSUB 12480
     EF=COM2; XC=69; YC=1; GOSUB 15000; PRINT EF 'R1###'
     FOR J=2 TO 16
       IF R(J) NE '' THEN GOSUB 12450
     NEXT J
     GOTO 1000
*************************************************
2300 *
     XC=1; YC= 1; GOSUB 15000
     IF MOVES = 0 THEN
       PRINT '-!-!-! STARDATE           -!-!-!-':
       PRINT '        SECTOR RANGE ENERGY  EXP = ':EF 'R1###':
     END
     XC=17; YC=1; GOSUB 15000; PRINT MOVES 'L##':'; ':TIMEDATE()[1,5]:
     J=1
2480 I=J
     IF MOVES = 0 THEN
       XC=1;  YC=J+1; GOSUB 15000; PRINT ':':
       XC=33; YC=J+1; GOSUB 15000; PRINT ':':
     END
2495 *
     IF T(J) EQ 'Z' THEN
       T(J)=''; XC=35; YC=J+1; GOSUB 15000
       PRINT SPACE(38):
       CL(J)=''
     END
     IF CL(J) NE '' THEN
       XC=57; YC=J+1; GOSUB 15000; PRINT '             ':
     END
2600 J = J+1
     IF J = 16 THEN GOTO 2495 ELSE IF J < 16 THEN GOTO 2480
     IF MOVES = 0 THEN
       XC=1; YC=17; GOSUB 15000; PRINT '-!-!-!-!-!-!-!-!-!-!-!-!-!-!-!-!-':
     END
     RETURN
******************************************
2800 *  LAUNCH FIGHTER
     PRINT  EEOP:
     IF S(4)<0 THEN PRINT D(4):' NOT OPERATIONAL'; GOTO 1000
     IF COM2 NE '' THEN GOTO 2810
     PRINT 'NUMBER ':; INPUT COM2
2810 IF COM2 <= 0 THEN GOTO 1205
     IF NUMFIGHTERS+COM2>4 THEN PRINT'ONLY 4 FIGHTERS CAN BE SUPPORTED';GOTO 1000
     IF E(1)<50*COM2 THEN PRINT 'INSUFFICIENT ENERGY'; GOTO 1000
     NUMFIGHTERS=NUMFIGHTERS+COM2
     E(1)=E(1)-50*COM2; NUM=NUM+COM2
     J=1; GOSUB 12390
     T9='F'; S9=100+RND(50); R9=ENTROW; C9=ENTCOL
     FOR ROW=1 TO COM2
       GOSUB 12100
     NEXT ROW
     GOTO 1150
***********************************************
2900 *  RECHARGE
     PRINT EEOP:
     IF S(5)<0 THEN PRINT D(5):' NOT OPERATIONAL'; GOTO 1000
     TEMP=RND(250)+50
     PRINT 'REACTOR RECHARGED ':TEMP:' UNITS'
     E(1)=E(1)+TEMP
     J=1; GOSUB 12390
     GOTO 1150
3000 ******************************************
*  EXECUTE REST OF MATRIX
******************************************
     FOR ROW=2 TO 16
       IF R(ROW) EQ '' THEN GOTO 3030
       TYPE=T(ROW)
       IF TYPE LE 'F' THEN NUMTYPE=1 ELSE NUMTYPE=2
       GOSUB 3400
     3030  NEXT ROW
     RETURN
3400 *FIND CLOSEST TARGET
*R0,C0 = LAUNCH POINT
*R1,C1 = TARGET POINT
*RANGE = RANGE
     R0=R(ROW); C0=C(ROW); RANGE = 999999
     FOR I=1 TO 16
       IF I=ROW OR R(I) EQ '' THEN GOTO 3450
       IF T(I) LE 'F' THEN NUMTARG=1 ELSE NUMTARG=2
       IF NUMTARG#NUMTYPE THEN
         TEMP = ABS(R(I)-R0) + ABS(C(I)-C0)
         IF TEMP<RANGE THEN RANGE=TEMP; ROW2=I
       END
     3450  NEXT I
     R1=R(ROW2); C1=C(ROW2)
     IF TYPE EQ 'B' THEN
       IF RANGE>4 THEN R1=ENTROW; C1=ENTCOL; GOTO 3500 ELSE GOTO 3600
     END
     IF RANGE <= RND(12) THEN GOTO 3700;* FIRE!!
3500 *MOVE TOWARD TARGET
     IF R1>R0 THEN R2=RND(5)+1 ELSE R2=-RND(5)-1
     IF C1>C0 THEN C2=RND(5)+1 ELSE C2=-RND(5)-1
     J=ROW; GOSUB 12470
     R(ROW)=R(ROW)+R2; C(ROW)=C(ROW)+C2
     GOSUB 12450
     GOSUB 12490; GOSUB 12550
     RETURN
3600 *MOVE AWAY FROM TARGET
     IF R1<R0 THEN R2=RND(5)+1 ELSE R2=-RND(5)-1
     IF C1<C0 THEN C2=RND(5)+1 ELSE C2=-RND(5)-1
     J=ROW; GOSUB 12470
     R(ROW)=R(ROW)+R2; C(ROW)=C(ROW)+C2
     GOSUB 12450
     GOSUB 12490; GOSUB 12550
     RETURN
**********************************************
3700 *FIRE
     S9=INT(E(ROW)*RND(10)/(RANGE+1))
     E(ROW2)=E(ROW2)-S9
*****
9777 XC=58; YC=ROW2+1; GOSUB 15000
     PRINT BLINK:S9:' UNIT HIT':UNBLINK:
     J=ROW2; GOSUB 12390
     CL(J)='X'; * TO KILL BLINK LATER
     IF E(ROW2) > 0 THEN RETURN
     XC=38;YC=ROW2+1; GOSUB 15000; PRINT 'DESTROYED !  ':
     IF T(ROW2) EQ 'B' THEN GOTO 9800
     IF T(ROW2) EQ 'E' THEN GOTO 9850
     IF T(ROW2) EQ 'F' THEN NUMFIGHTERS=NUMFIGHTERS-1
     IF T(ROW2) EQ 'K' THEN NUMENEMY=NUMENEMY-1
     IF NUMENEMY=0 THEN GOTO 9900
     GOSUB 12470
     R(J) = ''; T(J) = 'Z'
     RETURN
**************************************************
*  END GAME
**************************************************
9800 GOSUB 13000
     PRINT 'YOUR STAR BASE HAS BEEN DESTROYED!!!              '
     GOTO 9870
9850 GOSUB 13000
     PRINT 'THE ENTERPRISE HAS BEEN DESTROYED!!!           '
9870 PRINT '      YOU LOSE!!!!                       '
     PRINT '                                                         '
     PRINT '                                                         '
     PRINT '                                                         '
     GOTO 10000
9900 GOSUB 13000
     PRINT 'THE ENTERPRISE IS VICTORIOUS!!!         '
     PRINT '      YOU WIN COMMANDER !!!      '
     PRINT '                                                         '
     PRINT '                                                         '
     PRINT '                                                         '
10000STOP
****************************************
*  SUBROUTINES
****************************************
11000*RETRIEVE(R9,C9)
*OUTPUT: FLAG=1 : FOUND
*        FLAG=0 : NOT FOUND
     FLAG=1
     FOR ROW2=2 TO 16
       IF R(ROW2) EQ R9 AND C(ROW2) EQ C9 THEN T9=T(ROW2);S9=E(ROW2);RETURN
     NEXT ROW2
     FLAG=0; T9=' '
     RETURN
12000*STORE-OBJECT(T9,S9)
     R9=ENTROW+RND(DIFFICULTY)-RND(DIFFICULTY)
     C9=ENTCOL+RND(DIFFICULTY)-RND(DIFFICULTY)
12100*STORE(R9,C9,T9,S9) (IF POSSIBLE)
     FOR J = 2 TO 16
       IF R(J) EQ '' THEN GOTO 12200
     NEXT J
     RETURN
12200R(J)=R9; C(J)=C9; T(J)=T9;  E(J)=S9
12300XC=36; YC=J+1; GOSUB 15000
     PRINT T(J) 'L# AT ':
     GOSUB 12500
     GOSUB 12550
     GOSUB 12400
     GOSUB 12450
     RETURN
***** PRINT ENERGY *****
12390XC=52; YC=J+1; GOSUB 15000
12400PRINT E(J) 'R ####':
     RETURN
***** CONCATENATE TYPE-CODE IN T(J) WITH APPROPRIATE FF-MATRIX ENTRY
12450R1=R(J)-ENTROW; C1=C(J)-ENTCOL
     R1=INT(R1/EF)+8; C1=2*INT(C1/EF)+16
     IF R1 <= 0 OR R1 > 15 OR C1 <= 0 OR C1 > 30 THEN RETURN
     FF(R1,C1) = T(J):FF(R1,C1)
12460YC=R1+1; XC=C1+1; GOSUB 15000; PRINT FF(R1,C1)[1,1]:
     RETURN
***** DELETE TYPE-CODE IN T(J) FROM APPROPRIATE FF-MATRIX LOCATION
12470R1=R(J)-ENTROW; C1=C(J)-ENTCOL
     R1=INT(R1/EF)+8; C1=2*INT(C1/EF)+16
     IF R1 <= 0 OR R1 > 15 OR C1 <= 0 OR C1 > 30 THEN RETURN
     TEMP = INDEX(FF(R1,C1),T(J),1)
     FF(R1,C1) = FF(R1,C1)[1,TEMP-1]:FF(R1,C1)[TEMP+1,99]
     GOTO 12460
***** CLEAR FF-MATRIX; CLEAR CORROSPONDING LOCATIONS ON SCREEN; RESET 'E'.
12480MAT FF = ' '; FF(8,16) = 'E '
     FOR J = 1 TO 16
       R1=R(J)-ENTROW; C1=C(J)-ENTCOL
       R1=INT(R1/EF)+8; C1=2*INT(C1/EF)+16
       IF R1 > 0 AND R1 < 16 AND C1 > 0 AND C1 < 32 THEN
         YC=R1+1; XC=C1+1; GOSUB 15000; PRINT ' ':
       END
     NEXT J
     YC=9; XC=17; GOSUB 15000; PRINT 'E':
     RETURN
***** PRINT LOCATION
12490XC=41; YC=J+1; GOSUB 15000
12500PRINT R(J) 'R###,':C(J) 'L###':' ':
     RETURN
***** PRINT RANGE
12550XC=49; YC=J+1; GOSUB 15000
     R1= R(J)-ENTROW; C1= C(J)-ENTCOL
     IF J=1 THEN  PRINT '   ':; RETURN
     PRINT ABS(C1)+ABS(R1) 'R###':
     RETURN
***** SET CURSOR AT "COMMAND" LOCATION
13000XC=1; YC=18; GOSUB 15000
     RETURN
**** POSITION CURSOR
15000PRINT @(XC,YC):
     RETURN
END
