REM ***** GOLF ***** DEMONSTATION ***** PROGRAM *****
REM COMPLIMENTS OF CENTURY COMPUTER COMPANY
     DIM HAZARDS(3)
     DIM TRAPS(6)
     TRAPS(1)="THE FAIRWAY."
     TRAPS(2)="THE ROUGH."
     TRAPS(3)="THE TREES."
     TRAPS(4)="AN ADJACENT FAIRWAY."
     TRAPS(5)="A TRAP."
     TRAPS(6)="THE LAKE."
     DIM COURSE(18,4)
     COURSE(1,1)=361
     COURSE(1,2)=4
     COURSE(1,3)=4
     COURSE(1,4)=2
     COURSE(2,1)=389
     COURSE(2,2)=4
     COURSE(2,3)=3
     COURSE(2,4)=3
     COURSE(3,1)=206
     COURSE(3,2)=3
     COURSE(3,3)=4
     COURSE(3,4)=2
     COURSE(4,1)=500
     COURSE(4,2)=5
     COURSE(4,3)=7
     COURSE(4,4)=2
     COURSE(5,1)=408
     COURSE(5,2)=4
     COURSE(5,3)=2
     COURSE(5,4)=4
     COURSE(6,1)=359
     COURSE(6,2)=4
     COURSE(6,3)=6
     COURSE(6,4)=4
     COURSE(7,1)=424
     COURSE(7,2)=4
     COURSE(7,3)=4
     COURSE(7,4)=2
     COURSE(8,1)=388
     COURSE(8,2)=4
     COURSE(8,3)=4
     COURSE(8,4)=4
     COURSE(9,1)=196
     COURSE(9,2)=3
     COURSE(9,3)=7
     COURSE(9,4)=2
     COURSE(10,1)=400
     COURSE(10,2)=4
     COURSE(10,3)=7
     COURSE(10,4)=2
     COURSE(11,1)=560
     COURSE(11,2)=5
     COURSE(11,3)=7
     COURSE(11,4)=2
     COURSE(12,1)=132
     COURSE(12,2)=3
     COURSE(12,3)=3
     COURSE(12,4)=2
     COURSE(13,1)=357
     COURSE(13,2)=4
     COURSE(13,3)=4
     COURSE(13,4)=4
     COURSE(14,1)=294
     COURSE(14,2)=4
     COURSE(14,3)=2
     COURSE(14,4)=4
     COURSE(15,1)=475
     COURSE(15,2)=5
     COURSE(15,3)=2
     COURSE(15,4)=3
     COURSE(16,1)=375
     COURSE(16,2)=4
     COURSE(16,3)=4
     COURSE(16,4)=2
     COURSE(17,1)=180
     COURSE(17,2)=3
     COURSE(17,3)=6
     COURSE(17,4)=2
     COURSE(18,1)=550
     COURSE(18,2)=5
     COURSE(18,3)=6
     COURSE(18,4)=6
     TOT.SCORE=0
     TOT.PAR=0
     SCORE=0
     DIST=0
     PRINT
     PRINT
     PRINT "WELCOME TO TCW'S 18-HOLE CHAMPIONSHIP COURSE"
     PRINT
50   SUB=3
     DUBB.CONTROL=.8
     HOLE=1
80   PRINT "WHAT IS YOUR HANDICAP":
     INPUT HANDICAP
     IF NOT(NUM(HANDICAP)) THEN GOTO 80
     IF HANDICAP > 30 OR HANDICAP < 0 THEN
       PRINT "PGA RULES HANDICAP = 0-30"
       GOTO 80
     END
110  IF HANDICAP > 9 THEN GOTO 140
120  PRINT "WAIT A MINUTE!  IS YOUR NAME STEVE WADDINGTON?"
     GOTO 160
140  IF HANDICAP < 20 THEN GOTO 160
     PRINT "ANYONE FOR TENNIS?"
160  PRINT "DIFFICULTIES AT GOLF INCLUDE:"
     PRINT "   0=HOOK, 1=SLICE, 2=POOR DISTANCE, 4=TRAP SHOTS, 5=PUTTING"
180  PRINT "WHICH IS YOUR WORST":
     INPUT WORST
     IF NOT(NUM(WORST)) THEN GOTO 180
     IF WORST > 5 OR WORST < 0 THEN GOTO 180
     GOTO 220
210  RAND=RND(998)/1000+.001
     RETURN
220  GOSUB 210
240  HAZ.CHANCE=0
     PUTTS=0
     ONE.TO.SIX=0
     HAZARDS(1)=0
     TOT.SCORE=TOT.SCORE+SCORE
     IF HOLE=1 THEN GOTO 400
270  PRINT "YOUR SCORE ON HOLE ": HOLE-1: " WAS ": SCORE
280  SCORE=PAR-SCORE+3
     IF SCORE > 0 THEN GOTO 285
     PRINT "KEEP YOUR HEAD DOWN"
     GOTO 390
285  SCORE=SCORE-3
     IF SCORE < 0 OR SCORE > 2 THEN GOTO 390
     IF SCORE=0 THEN PRINT "A PAR.  NICE GOING"; GOTO 390
     IF SCORE=1 THEN PRINT "A BIRDIE, AND TWEET-TWEET TO YOU"; GOTO 390
     IF PAR=3 THEN GOTO 380
     PRINT " A GREAT BIG EAGLE"
     GOTO 390
380  PRINT "A HOLE IN *O*N*E*"
390  IF HOLE=19 THEN GOTO 2080
400  SCORE=0
410  IF SCORE = 0 THEN GOTO 1930
420  IF HAZARDS(1) < 1 THEN GOTO 1460
430  SUB=1
     IF HAZARDS(1) > 5 THEN GOTO 1500
450  PRINT "SHOT WENT ": DISTANCE: " YARDS, AND IS ": FROM.HOLE: " YARDS FROM HOLE"
     PRINT "BALL IS ": OFFLINE: " YARDS OFF-LINE IN ":
     GOSUB 2170
480  PRINT "WHAT CLUB DO YOU WANT ": ; INPUT CLUB
     PCT.SWING = 1
     IF CLUB = 0 THEN GOTO 510
     IF CLUB MATCHES '1NW' THEN
       CLUB = CLUB[1,1]
     END ELSE
       IF CLUB MATCHES '1N,0N' THEN
         PCT.SWING = CLUB[3,2]/100
         CLUB = CLUB[1,1] + 20
         SW = 0
       END ELSE
         IF CLUB MATCHES '1N' THEN
           CLUB = CLUB + 10
         END ELSE GOTO 480
         GOTO 700
       END
     END
510  PRINT STR("*",50)
540  PRINT
550  IF CLUB = 9 THEN GOTO 480
560  PRINT "HERE'S YOUR BAG OF CLUBS:"
     PRINT "WOODS (FULL SWING ONLY)"
     PRINT "   1W"
     PRINT "   2W"
     PRINT "   3W"
580  PRINT "IRONS (FULL SWING ONLY)"
     PRINT "   2  TWO IRON"
     PRINT "   ."
     PRINT "   ."
     PRINT "   9  NINE IRON"
590  PRINT "IRONS (LESS THAN FULL SWING)"
     PRINT "   2,% TWO IRON - % = PER CENT OF SWING"
     PRINT "   ."
     PRINT "   ."
     PRINT "   9,50 NINE IRON, 50 PER CENT SWING"
620  PRINT "WHEN YOU REACH THE GREEN, IT WILL BE ASSUMED THAT YOU ARE"
     PRINT " USING A PUTTER.  THE PUTT POTENCY NO. REFERS TO THE STRENGTH"
     PRINT " WITH WHICH THE BALL IS PUTTED.  USE NUMBERS GREATER THAN"
     PRINT " ZERO, INCREASING THE NUMBER FOR GREATER DISTANCE."
680  CLUB=9
     GOTO 510
700  IF CLUB < 1 OR CLUB > 29 THEN GOTO 760
710  IF CLUB > 3 THEN GOTO 780
720  IF HAZARDS(1) < 5 OR CLUB=14 OR CLUB = 23 THEN GOTO 810
760  GOSUB 2340
     GOTO 480
780  IF CLUB < 12 THEN GOTO 760
790  CLUB=CLUB-6
     GOTO 720
810  SCORE=SCORE+1
     IF CLUB > 13 THEN GOTO 1180
840  IF HOLE/3 = INT(HOLE/3) THEN GOTO 1100
850  IF CLUB < 4 THEN GOTO 870
     GOTO 880
870  IF HAZARDS(1)=2 THEN GOTO 960
880  IF SCORE > 7 THEN GOTO 990
890  GOSUB 210
     DISTANCE=(((30-HANDICAP)*2.5+187-((30-HANDICAP)*.25+15)*CLUB/2)+25*RAND)
     DISTANCE=INT(DISTANCE*PCT.SWING)
     IF WORST = 2 THEN GOTO 1480
920  GOSUB 210
     OFFLINE=INT((RAND/.8)*(HANDICAP+16)*DIST*.0025)
     SQ=ABS(DIST-DISTANCE)
     SQ=OFFLINE*OFFLINE+SQ*SQ
     SQR=SQ
     IF SQ = 0 THEN GOTO 940
     SQR=(1+SQ)/2
930  TRIAL=SQR
     SQR=(TRIAL+SQ/TRIAL)/2
     IF SQR < TRIAL THEN GOTO 930
940  FROM.HOLE=INT(SQR)
     IF DIST-DISTANCE < 0 THEN GOTO 1010
950  GOTO 1030
960  PRINT "YOU DUBBED IT"
     DISTANCE = 35
     GOTO 920
990  IF DIST < 200 THEN GOTO 1610
1000 GOTO 890
1010 IF FROM.HOLE < 20 THEN GOTO 1030
1020 PRINT "TOO MUCH CLUB.  YOU ARE PAST THE HOLE."
1030 PREV.DIST = DIST
     DIST = FROM.HOLE
     IF FROM.HOLE > 27 THEN GOTO 1280
1060 IF FROM.HOLE > 20 THEN GOTO 1410
1070 IF FROM.HOLE > 0 THEN GOTO 1430
1080 HAZARDS(1) = 9
     GOTO 1820
1100 IF (72+((HANDICAP+1)/.85))/18 < TOT.SCORE+ONE.TO.SIX+(10*(HOLE-1)/18) THEN GOTO 850
1110 ONE.TO.SIX=ONE.TO.SIX+1
     IF SCORE/2 # INT(SCORE/2) THEN GOTO 1240
1130 GOTO 960
1180 IF PCT.SWING > 0 AND PCT.SWING <= 1 THEN GOTO 1200
1185 GOSUB 2340
     GOTO 480
1200 IF HAZARDS(1) = 5 THEN GOTO 1600
1210 IF CLUB = 14 THEN GOTO 880
1220 CLUB = CLUB - 10
     GOTO 880
1240 IF DISTANCE < 95 THEN GOTO 960
1250 PRINT "BALL HIT A TREE AND BOUNCED INTO ROUGH ":
     PRINT DISTANCE - 75: " YARDS FROM HOLE."
     DISTANCE=DISTANCE-75
     GOTO 480
1280 IF OFFLINE < 30 OR HAZ.CHANCE > OFFLINE THEN GOTO 1460
     IF WORST > 0 THEN GOTO 1350
     IF WORST > OFFLINE THEN GOTO 1350
     IF RND(14) = 7 THEN GOTO 1360
1310 PRINT "YOU HOOKED-":
     HAZARDS(1) = HAZARDS(3)
1330 IF OFFLINE > 45 THEN GOTO 1390
1340 GOTO 410
1350 R=RND(14)
     IF R > 3 AND R < 7 THEN GOTO 1310
1360 PRINT "YOU SLICED-":
     HAZARDS(1) = HAZARDS(2)
     GOTO 1330
1390 PRINT "BADLY."
     GOTO 410
1410 HAZARDS(1) = 5
     GOTO 410
1430 HAZARDS(1) = 8
     FROM.HOLE=FROM.HOLE*3
     GOTO 1690
1460 HAZARDS(1)=1
     GOTO 410
1480 DISTANCE = INT(.85 * DISTANCE)
     GOTO 920
1500 IF HAZARDS(1) > 6 THEN GOTO 1580
1510 PRINT "YOUR SHOT WENT INTO WATER."
1520 SCORE=SCORE+1
     PRINT "PENALTY STROKE ASSESSED.  HIT FROM PREVIOUS LOCATION."
     HAZ.CHANCE=HAZ.CHANCE+1
     HAZARDS(1)=1
     DIST=PREV.DIST
     GOTO 480
1580 PRINT "YOUR SHOT WENT OUT OF BOUNDS."
     GOTO 1520
1600 IF WORST=3 THEN GOTO 1630
1610 GOSUB 210
     FROM.HOLE=1+(3*INT((80/(40-HANDICAP))*RAND))
     GOTO 1690
1630 GOSUB 210
     IF RAND > DUBB.CONTROL THEN GOTO 1670
1640 DUBB.CONTROL=DUBB.CONTROL*.2
     PRINT "SHOT DUBBED, STILL IN TRAP."
     GOTO 480
1670 DUBB.CONTROL=.8
     GOTO 1610
1690 PRINT "ON GREEN, ":FROM.HOLE:" FEET FROM PIN."
1695 PRINT "PUTT POTENCY NUMBER":
     INPUT POTENCY
     IF NOT(NUM(POTENCY)) THEN GOTO 1695
     SW=0
     IF POTENCY > 0 THEN GOTO 1730
1720 GOSUB 2340
1730 IF SW # 0 THEN GOTO 1690
1740 SCORE=SCORE+1
     IF SCORE+1-PAR > (HANDICAP*.072)+2 THEN GOTO 1820
1760 IF PUTTS > 4 THEN GOTO 1820
1770 PUTTS=PUTTS+1
     IF WORST=4 THEN GOTO 1880
1790 GOSUB 210
     FROM.HOLE=INT(FROM.HOLE-POTENCY*(4+2*RAND)+1.5)
1800 IF FROM.HOLE < -1 THEN GOTO 1900
1810 IF FROM.HOLE > 1 THEN GOTO 1850
1820 PRINT "YOU H*O*L*E*D IT."
     HOLE=HOLE+1
     GOTO 240
1850 PRINT "PUTT SHORT."
     GOTO 1690
1880 GOSUB 210
     FROM.HOLE=INT(FROM.HOLE-POTENCY*(4+RAND)+1)
     GOTO 1800
1900 PRINT "PASSED BY CUP."
     FROM.HOLE=ABS(FROM.HOLE)
     GOTO 1690
1930 DIST=COURSE(HOLE,1)
     PAR=COURSE(HOLE,2)
     HAZARDS(2)=COURSE(HOLE,3)
     HAZARDS(3)=COURSE(HOLE,4)
     PRINT
1980 TOT.PAR=TOT.PAR+PAR
     PRINT "YOU ARE AT TEE OF HOLE ":HOLE:
     PRINT ", DISTANCE ":DIST:" YARDS, PAR ":PAR
     PRINT "ON YOUR RIGHT IS ":
     SUB=2
     GOSUB 2170
     PRINT "ON YOUR LEFT IS ":
     SUB=3
     GOSUB 2170
     GOTO 480
2080 PRINT
     PRINT "TOTAL SCORE FOR ":HOLE-1:" HOLES WAS ":TOT.SCORE
     IF TOT.SCORE = TOT.PAR THEN
       PRINT "EVEN PAR."
     END ELSE
       IF TOT.SCORE > TOT.PAR THEN
         PRINT TOT.SCORE - TOT.PAR: " OVER PAR."
       END ELSE
         PRINT TOT.PAR-TOT.SCORE: " UNDER PAR."
       END
     END
     STOP
2170 IF HAZARDS(SUB) < 1 OR HAZARDS(SUB) > 6 THEN
       PRINT "OUT OF BOUNDS."
       RETURN
     END
     PRINT TRAPS(HAZARDS(SUB))
     RETURN
2340 PRINT 'ENTER "0" FOR A DESCRIPTION OF CLUBS.'
     SW=9
     RETURN
END
