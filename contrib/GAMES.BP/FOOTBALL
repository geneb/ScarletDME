*THIS IS THE FOOTBALL PROGRAM
     DIM M(6)
     DIM N(6)
     NUL=CHAR(0)
     FF=@(-1)
     BELL=CHAR(0)
     RESET=@(0,9) CAT STR(' ',56) CAT @(4,9)
     CLR=NUL CAT NUL CAT FF CAT NUL CAT NUL
     M(1)="BALL CARRIER RACES PAST THE "
     M(2)="BALL CARRIER CROSSES OVER THE "
     M(3)="BALL CARRIER OUT RUNS OPPONENT AT THE "
     M(4)="BALL CARRIER HIT AT THE "
     M(5)="BALL CARRIER ADVANCES TO THE "
     M(6)="BALL CARRIER RAMBLES UP TO THE "
     N(1)="ILLEGAL PROCEDURE CALLED AGAINST "
     N(2)="BACKFIELD IN MOTION CALLED AGAINST "
     N(3)="OFF-SIDES CALLED AGAINST "
     N(4)="TO MANY MEN ON THE FIELD CALLED AGAINST "
     N(5)="UNSPORTSMAN-LIKE CONDUCT CALLED AGAINST "
     N(6)="HOLDING CALLED AGAINST "
100  *
     PRINT CLR:@(0,3):"THERE ARE 3 WAYS OF PLAYING    F O O T B A L L  ":@(3,6):"1   PLAYER  VS  PLAYER":@(3,8):"2   PLAYER  VS  COMPUTER":
     PRINT @(3,10):"3   COMPUTER  VS  COMPUTER":@(0,13):"ENTER BY NUMBER THE TYPE OF GAME TO BE PLAYED  ":
     INPUT TYPE
     IF TYPE#1 THEN IF TYPE#2 THEN IF TYPE#3 THEN GOTO 100
     PRINT CLR:@(0,3):"HOW MANY MINUTES PER QUARTER WOULD YOU LIKE TO PLAY?":
     INPUT QTR
     IF TYPE=3 THEN A=RND(10)+1; GOSUB 300; TEAM2=A; FORM1=RND(4)+1; FORM2=RND(4)+1; GOTO 130
120  *
     PRINT CLR:@(0,3):"SELECT YOUR TEAM":
     PRINT @(3,5):"1  FALCONS":@(3,6):"2  COWBOYS":@(3,7):"3  RAIDERS":@(3,8):"4  BEARS":@(3,9):"5  EAGLES":
     PRINT @(3,10):"6  PATROITS":@(3,11):"7  DOLPHINS":@(3,12):"8  STEELERS":@(3,13):"9  LIONS":@(2,14):"10  VIKINGS":
     B=1
     PRINT @(0,16):
125  *
     IF TYPE=1 THEN PRINT "ENTER TEAM - ":B:"  ": ELSE PRINT "ENTER BY NUMBER":
     INPUT A
     IF A=0 THEN GOTO 120
     IF A>10 THEN GOTO 120
     GOSUB 300
     IF TYPE=2 THEN TEAM2=A; GOTO 130
     IF B=2 THEN TEAM2=A; GOTO 135
     TEAM1=A
     B=2
     GOTO 125
130  *
     A=RND(10)+1
     GOSUB 300
     TEAM1=A
135  *
     IF TEAM1 EQ TEAM2 THEN GOTO 130
     PRINT CLR:@(0,2):"TODAY'S GAME IS THE ":TEAM1:" AGAINST THE ":TEAM2:" .":
     PRINT @(0,4):"A FLIP OF THE COIN!  DO THE ":TEAM2:" WANT  1 - HEADS  OR  2 - TAILS?":
     INPUT A
     B=RND(2)+1
     IF A=B THEN NAME=TEAM2; H.TMDR=1; TMDR=2 ELSE NAME=TEAM1; H.TMDR=2; TMDR=1
     IF B EQ '1' THEN B=" HEADS"
     IF B EQ '2' THEN B=" TAILS"
     PRINT @(0,6):"THE COIN LANDS ":B:" UP":
     PRINT @(0,8):NAME:" WIN THE TOSS AND WILL RECEIVE!":
     IF TYPE=3 THEN GOTO 200
140  *
     PRINT @(0,12):"WHAT TYPE OF OFFENSIVE FORMATION WILL YOUR TEAM RUN?":
     PRINT @(3,14):"1  WISHBONE":@(3,15):"2  I FORMATION":@(3,16):"3  SINGLE WING":@(3,17):"4  PRO":
     IF TYPE=1 THEN
       PRINT @(0,19):TEAM1:" FORMATION  ":; INPUT FORM1
       PRINT @(0,20):TEAM2:" FORMATION  ":; INPUT FORM2; GOTO 150
     END
     PRINT @(0,19):TEAM2:" FORMATION  ":
     INPUT FORM2
     FORM1=RND(4)+1
150  *
     IF FORM1=0 THEN FORM1=RND(4)+1
     IF FORM1>4 THEN FORM1=RND(4)+1
     IF FORM2=0 THEN FORM2=RND(4)+1
     IF FORM2>4 THEN FORM2=RND(4)+1
200  *
     DN=0; YDS=0; PYDS=0
     YTG=0
     PERIOD=0
     CLOCK=0
     SCORE1=0; SCORE2=0
     RUSH1=0;  RUSH2=0
     PASS1=0;  PASS2=0
     TOFF1=0;  TOFF2=0
     TPLAY1=0; TPLAY2=0
     TDNS1=0;  TDNS2=0
     COMPL1=0; COMPL2=0
     AYP1=0;   AYP2=0
     PENTL1=0; PENTL2=0
     NPASS1=0; NPASS2=0
     CPASS1=0; CPASS2=0
     NPUNT1=0; NPUNT2=0
     YPUNT1=0; YPUNT2=0
     TOS1=0;   TOS2=0
     GOSUB 2200
     HQTR=QTR*100
     PERIOD=1
     SW=1
210  *
     GOSUB 400
     DN=1
250  *
     GOSUB 997
     GOSUB 500
     GOSUB 800
260  *
     IF TYPE=2 THEN IF TMDR=1 THEN GOTO 2100
     IF TYPE=3 THEN GOTO 2100
     PRINT @(70,22):"     ":@(70,22):
     INPUT PL
     IF PL EQ 'RESET' THEN GOSUB 2200; GOSUB 997; GOTO 260
265  *
     IF TMDR=1 THEN TPLAY1=TPLAY1+1 ELSE TPLAY2=TPLAY2+1
     A=RND(24)+1
     IF A=3 THEN GOTO 270
     GOTO 280
270  *
     A=RND(6)+1
     PRINT RESET:N(A):OFF:
     GOSUB 998
     IF YDLN<20 THEN YDS=(INT(YDLN/2))*(-1) ELSE YDS = -10
     IF TMDR=1 THEN PENTL1=PENTL1-YDS ELSE PENTL2=PENTL2-YDS
     YDLN=YDLN+YDS
     PRINT RESET:ABS(YDS):" YARD PENALTY":; GOSUB 998
     PYDS=0
     DN=DN-1
     GOSUB 2000
     GOTO 250
280  *
     CHANGE=0
     PYDS=0; YDS=0
     IF PL=1 THEN GOSUB 1000; GOTO 250
     IF PL=2 THEN GOSUB 1000; GOTO 250
     IF PL=3 THEN GOSUB 1100; GOTO 250
     IF PL=4 THEN GOSUB 1100; GOTO 250
     IF PL=5 THEN GOSUB 1200; GOTO 250
     IF PL=6 THEN GOSUB 1200; GOTO 250
     IF PL=7 THEN GOSUB 1300; GOTO 250
     IF PL=8 THEN GOSUB 1400; GOTO 250
     IF PL=9 THEN GOSUB 1500; GOTO 250
     GOTO 270
300  *
     IF A=1 THEN A='FALCONS'; GOTO 399
     IF A=2 THEN A='COWBOYS'; GOTO 399
     IF A=3 THEN A='RAIDERS'; GOTO 399
     IF A=4 THEN A='BEARS'; GOTO 399
     IF A=5 THEN A='EAGLES'; GOTO 399
     IF A=6 THEN A='PATROITS'; GOTO 399
     IF A=7 THEN A='DOLPHINS'; GOTO 399
     IF A=8 THEN A='STEELERS'; GOTO 399
     IF A=9 THEN A='LIONS'; GOTO 399
     IF A=10 THEN A='VIKINGS'; GOTO 399
     A=''
399  *
     RETURN
400  *
     IF PERIOD=1 THEN T.DR=TMDR; NAMEA=TEAM1; NAMEB=TEAM2; GOTO 405
     IF PERIOD=3 THEN T.DR=TMDR; NAMEA=TEAM1; NAMEB=TEAM2; GOTO 405
     IF TMDR=1 THEN T.DR=2 ELSE T.DR=1
     NAMEB=TEAM1; NAMEA=TEAM2
405  *
     PRINT @(3,7):NAMEA:SPACE(5):@(44,7):SPACE(10-LEN(NAMEB)):NAMEB:
     FOR I=9 TO 22
       PRINT @(0,I):STR(' ',56):
     NEXT I
     IF T.DR=1 THEN GOTO 410
     A=NAMEA[1,1]
     FOR I=11 TO 15
       PRINT @(21,I):A:
     NEXT I
     PRINT @(18,16):A:
     FOR I=17 TO 21
       PRINT @(21,I):A:
     NEXT I
     A=NAMEB[1,1]
     PRINT @(36,11):A:@(43,12):A:@(36,13):A:@(51,14):A:@(43,15):A:@(36,16):A:
     PRINT @(43,17):A:@(51,18):A:@(36,19):A:@(43,20):A:@(36,21):A:
     GOTO 420
410  *
     A=NAMEB[1,1]
     FOR I=11 TO 15
       PRINT @(36,I):A:
     NEXT I
     PRINT @(39,16):A:
     FOR I=17 TO 21
       PRINT @(36,I):A:
     NEXT I
     A=NAMEA[1,1]
     PRINT @(22,11):A:@(15,12):A:@(22,13):A:@(6,14):A:@(15,15):A:@(22,16):A:
     PRINT @(15,17):A:@(6,18):A:@(22,19):A:@(15,20):A:@(22,21):A:
420  *
     FOR I=11 TO 23
       PRINT @(56,I):SPACE(23):
     NEXT I
     IF TYPE=3 THEN PRINT RESET:"THERE'S THE KICK-OFF":; GOSUB 999; GOTO 425
     IF TYPE=2 THEN IF TMDR=2 THEN PRINT RESET:"THERE'S THE KICK-OFF":; GOSUB 999; GOTO 425
     PRINT @(58,18):"HIT 'NEW LINE' TO":@(59,19):"   KICK OFF   ":
     INPUT A
     FOR I=17 TO 20
       PRINT @(56,I):SPACE(23):
     NEXT I
425  *
     PRINT @(58,12):"OFFENSIVE PLAYS":@(60,13):"1=DRAW PLAY":@(60,14):"2=END SWEEP":@(60,15):"3=REVERSE":
     PRINT @(60,16):"4=QTRBACK OPTION":@(60,17):"5=SHORT PASS":@(60,18):"6=SCREEN PASS":@(60,19):"7=LONG PASS":
     PRINT @(60,20):"8=FIELD GOAL":@(60,21):"9=PUNT":@(58,22):"ENTER A PLAY":
     YDLN=RND(50)+1
     IF TMDR=1 THEN
       OFF=TEAM1; DEF=TEAM2
     END ELSE
       OFF=TEAM2; DEF=TEAM1
     END
     PRINT @(0,9):SPACE(56):@(10,9):DEF:" KICK TO ":OFF:
     GOSUB 998
     IF YDLN>40 THEN PRINT RESET:"TOUCHBACK!  BALL COMES OUT TO THE 20 YARD LINE":; YDLN=20; DN=1; GOSUB 998; GOTO 499
     PRINT RESET:OFF:" RECEIVES BALL ON THE ":YDLN:" YARD LINE":
     GOSUB 998
     A=RND(10)+1
     IF A=5 THEN PRINT RESET:"BALL FUMBLED":; GOSUB 998; GOSUB 700; GOTO 499
     A=RND(6)+1
     IF A=3 THEN PRINT RESET:"BALL CARRIER SCRAMBLIES BUT IS TACKLED AT THE ":YDLN:; GOSUB 998; DN=1; GOTO 499
     IF A=1 THEN GOTO 430
     IF A=2 THEN YDS=RND(100)+1 ELSE YDS=RND(30)+1
     GOSUB 600
     DN=1
     GOTO 499
430  *
     YDS=RND(13)+1
     IF YDLN<13 THEN PRINT RESET:"BALL CARRIER RUNS INTO THE END ZONE":; GOSUB 998
     TEST=YDLN-YDS
     IF TEST<0 THEN GOTO 440
     PRINT RESET:"HE'S TACKLED AT THE ":(YDLN-YDS):" YARD LINE":
     YDLN=YDLN-YDS
     GOSUB 998
     DN=1
     GOTO 499
440  *
     PRINT RESET:"HE'S TACKLED IN THE END ZONE!   S A F T Y  !!!":
     IF TMDR=1 THEN TMDR=2; SCORE2=SCORE2+2 ELSE TMDR=1; SCORE1=SCORE1+2
     DN=1; YTG=10
     GOSUB 998
     GOSUB 997
     GOSUB 400
     DN=1
     CHANGE=1
499  *
     PYDS=0; YDS=0
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     RETURN
500  *
     ACC=INT(YDLN/2)
     IF ACC<1 THEN ACC=1
     IF PERIOD=1 THEN T.DR=TMDR; GOTO 505
     IF PERIOD=3 THEN T.DR=TMDR; GOTO 505
     IF TMDR=1 THEN T.DR=2 ELSE T.DR=1
505  *
     IF TMDR=1 THEN FORM=FORM1 ELSE FORM=FORM2
     IF T.DR=1 THEN ACC=ACC+3 ELSE ACC=53-ACC
     FOR I=11 TO 22
       PRINT @(0,I):STR(' ',57):
     NEXT I
     PRINT @(ACC,12):'E':@(ACC,17):'T':
     IF FORM=4 THEN GOTO 560
     IF FORM=3 THEN GOTO 540
     IF FORM=2 THEN GOTO 520
     PRINT @(ACC,13):'T':
     IF T.DR=1 THEN GOTO 510
     PRINT @(ACC,14):'G   B':@(ACC,15):'CQ B':@(ACC,16):'G   B':
     GOTO 515
510  *
     HACC=ACC-4
     PRINT @(HACC,14):'B   G':@(HACC,15):' B QC':@(HACC,16):'B   G':
515  *
     PRINT @(ACC,18):'E':
     GOTO 599
520  *
     HACC=ACC-4
     PRINT @(ACC,13):'T':@(ACC,14):'G':@(ACC,16):'G':@(ACC,20):'E':
     IF T.DR=1 THEN GOTO 530
     PRINT @(ACC,15):'CQ BB':@(ACC,18):' B'
     GOTO 599
530  *
     PRINT @(HACC,15):'BB QC':@(HACC,18):'   B ':
     GOTO 599
540  *
     HACC=ACC-3
     PRINT @(ACC,16):'T':@(ACC,18):'E':
     IF T.DR=1 THEN GOTO 550
     PRINT @(ACC,13):'G  B':@(ACC,14):'CQ':@(ACC,15):'G  B':@(ACC,21):' B':
     GOTO 599
550  *
     PRINT @(HACC,13):'B  G':@(HACC,14):'  QC':@(HACC,15):'B  G':@(HACC,21):'  B ':
     GOTO 599
560  *
     HACC=ACC-3
     PRINT @(ACC,13):'T':@(ACC,21):'E':
     IF T.DR=1 THEN GOTO 570
     PRINT @(ACC,14):'G  B':@(ACC,15):'CQ':@(ACC,16):'G  B':@(ACC,18):' B':
     GOTO 599
570  *
     PRINT @(HACC,14):'B  G':@(HACC,15):'  QC':@(HACC,16):'B  G':@(HACC,18):'  B ':
599  *
     RETURN
600  *
     TEST1=PYDS+YDS
     TEST=YDS
     MESS1=" YARDS"
     IF TEST1<0 THEN MESS="  LOSS OF "
     IF TEST1>0 THEN MESS="  GAIN OF "
     IF TEST1=0 THEN MESS="  NO GAIN "; MESS1=""
     IF TEST1=1 THEN MESS1=" YARD"
     IF TEST1=-1 THEN MESS1=" YARD"
610  *
     IF TEST<=10 THEN PYD=YDLN+TEST ELSE PYD=YDLN
     IF PYD<0 THEN GOTO 440
     IF PYD>50 THEN PYD=100-PYD
     IF YDLN<50 THEN IF TEST<10 THEN IF (YDLN+TEST)>50 THEN PRINT RESET:"HE'S ACCROSS MID-FIELD":; GOSUB 998
     IF TEST<=10 THEN
       IF (TEST+YDLN)<100 THEN
         PRINT RESET:"TACKLED AT THE ":PYD:MESS:ABS(YDS+PYDS):MESS1:
         YDLN=YDLN+TEST
         GOSUB 998
         GOTO 699
       END
     END
     IF TEST<=10 THEN IF (TEST+YDLN)>100 THEN GOTO 620
     IF TEST>10 THEN IF (YDLN+10)>100 THEN GOTO 620
     IF YDLN<50 THEN IF (YDLN+10)>50 THEN PRINT RESET:"HE'S ACCROSS MID-FIELD":; GOSUB 998
     YDLN=YDLN+10
     TEST=TEST-10
     A=RND(6)+1
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF PYD<0 THEN GOTO 620
     PRINT RESET:M(A):PYD:
     GOSUB 998
     GOTO 610
620  *
     PRINT BELL:RESET:OFF:" SCORE A   T O U C H D O W N  !!!":
     IF TMDR=1 THEN SCORE1=SCORE1+7; TMDR=2 ELSE SCORE2=SCORE2+7; TMDR=1
     GOSUB 998
     DN=1
     YTG=10
     PYDS=0; YDS=0
     GOSUB 997
     GOSUB 400
     CHANGE=1
699  *
     RETURN
700  *
     A=RND(10)+1
     IF A<6 THEN PRINT RESET:OFF:" RECOVERS THEIR OWN FUMBLE":; GOSUB 998; GOTO 799
     YDLN=100-YDLN
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     GOSUB 1950
     GOSUB 1800
     CHANGE=1
     PRINT RESET:OFF:" RECOVER ":DEF:" FUMBLE AT THE ":PYD:
     GOSUB 998
     A=RND(5)+1
     IF A>2 THEN PRINT RESET:OFF:" TAKE OVER AT THE ":PYD:; GOSUB 998; GOTO 798
     IF A=1 THEN YDS=RND(80)+1 ELSE YDS=RND(8)+1
     IF (YDLN+YDS)<100 THEN GOSUB 1930
     PRINT RESET:"HE'S RUNNING WITH THE BALL!!":; GOSUB 998
     GOSUB 600
798  *
     DN=1
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     YDS=0;PYDS=0
799  *
     RETURN
800  *
     IF TMDR=2 THEN TL=TEAM1[1,1] ELSE TL=TEAM2[1,1]
     IF T.DR=2 THEN ACC=ACC-2 ELSE ACC=ACC+2
     IF YDLN > 70 THEN GOTO 850
     R=RND(2)
     IF R=1 THEN GOTO 820
     PRINT @(ACC,14):TL:@(ACC,16):TL:
     IF T.DR=2 THEN HACC=ACC-12; GOTO 810
     PRINT @(ACC,11):'  ':TL:SPACE(9):TL:@(ACC,12):TL:SPACE(6):TL:
     PRINT @(ACC,15):'  ':TL:@(ACC,18):TL:SPACE(6):TL:@(ACC,19):'  ':TL:SPACE(9):TL:
     GOTO 899
810  *
     PRINT @(HACC,11):TL:SPACE(9):TL:@(HACC,19):TL:SPACE(9):TL:
     HACC=HACC+5
     PRINT @(HACC,12):TL:SPACE(6):TL:@(HACC,18):TL:SPACE(6):TL:
     PRINT @(HACC+5,15):TL
     GOTO 899
820  *
     PRINT @(ACC,13):TL:@(ACC,16):TL:@(ACC,19):TL:
     IF T.DR=2 THEN HACC=ACC-12; GOTO 830
     ACC=ACC+1
     PRINT @(ACC,12):TL:SPACE(10):TL:@(ACC,15):TL:@(ACC,17):TL:@(ACC,20):TL:SPACE(10):TL:
     ACC=ACC+5
     PRINT @(ACC,11):TL:@(ACC,21):TL:
     GOTO 899
830  *
     PRINT @(HACC,12):TL:SPACE(10):TL:@(HACC,20):TL:SPACE(10):TL:
     HACC=HACC+6
     PRINT @(HACC,11):TL:@(HACC,21):TL:
     HACC=HACC+5
     PRINT @(HACC,15):TL:@(HACC,17):TL:
     GOTO 899
850  *
     IF YDLN < 90 THEN GOTO 855
     IF T.DR=2 THEN ACC=ACC+1 ELSE ACC=ACC-1
855  *
     PRINT @(ACC,13):TL:@(ACC,14):TL:@(ACC,15):TL:@(ACC,17):TL:@(ACC,18):TL:@(ACC,19):TL:
     IF T.DR=2 THEN HACC=ACC-3; GOTO 860
     PRINT @(ACC,12):'  ':TL:@(ACC,16):TL:TL:'  ':TL:@(ACC,20):'  ':TL:
     GOTO 899
860  *
     PRINT @(HACC,12):'  ':TL:@(HACC,16):TL:' ':TL:TL:@(HACC,20):'  ':TL:
899  *
     RETURN
997  *
     TME1=TIME()
     IF SW=1 THEN SW=0; TME2=TME1
     TEST=TME1-TME2
     TME2=TME1
     HQTR=HQTR-TEST
     IF HQTR<1 THEN
       IF PERIOD=1 THEN
         NAMEA=TEAM2; NAMEB=TEAM1
         PRINT RESET:"END OF THE FIRST QUARTER":; GOSUB 998
         PRINT @(3,7):NAMEA:SPACE(5):@(44,7):SPACE(10-LEN(NAMEB)):NAMEB:
       END
     END
     IF HQTR<1 THEN
       IF PERIOD=2 THEN
         PRINT RESET:"END OF THE FIRST HALF"; GOSUB 998
         TMDR=H.TMDR; PERIOD=3; HQTR=QTR*100; DN=1; YTG=10; GOSUB 400; CHANGE=1; GOSUB 1930
       END
     END
     IF HQTR<1 THEN
       IF PERIOD=3 THEN
         NAMEA=TEAM2; NAMEB=TEAM1
         PRINT RESET:"END OF THE THIRD QUARTER":; GOSUB 998
         PRINT @(3,7):NAMEA:SPACE(5):@(44,7):SPACE(10-LEN(NAMEB)):NAMEB:
       END
     END
     IF HQTR<1 THEN PERIOD=PERIOD+1; HQTR=QTR*100
     IF LEN(HQTR)=4 THEN SEC=HQTR[3,2]; SEC=INT((SEC*6)/10); CLOCK=HQTR[1,2] CAT ':' CAT SEC
     IF LEN(HQTR)=3 THEN SEC=HQTR[2,2]; SEC=INT((SEC*6)/10); CLOCK=' ' CAT HQTR[1,1] CAT ':' CAT SEC
     IF LEN(HQTR)<3 THEN SEC=INT((HQTR*6)/10)
     IF LEN(HQTR)<3 THEN IF LEN(SEC)=2 THEN CLOCK='00:' CAT SEC
     IF LEN(HQTR)<3 THEN IF LEN(SEC)=1 THEN CLOCK='00:0' CAT SEC
     PRINT @(4,4):DN:@(11,4):SPACE(3-LEN(YTG)):YTG:@(20,4):PERIOD:@(23,4):CLOCK:
     PRINT @(33,5):SPACE(3-LEN(SCORE1)):SCORE1:SPACE(11-LEN(SCORE2)):SCORE2:
     PRINT @(70,2):SPACE(4-LEN(RUSH1)):RUSH1:SPACE(6-LEN(RUSH2)):RUSH2
     PRINT @(70,3):SPACE(4-LEN(PASS1)):PASS1:SPACE(6-LEN(PASS2)):PASS2
     PRINT @(70,4):SPACE(4-LEN(TOFF1)):TOFF1:SPACE(6-LEN(TOFF2)):TOFF2
     PRINT @(70,5):SPACE(4-LEN(TPLAY1)):TPLAY1:SPACE(6-LEN(TPLAY2)):TPLAY2
     PRINT @(70,6):SPACE(4-LEN(TDNS1)):TDNS1:SPACE(6-LEN(TDNS2)):TDNS2
     PRINT @(70,7):SPACE(4-LEN(COMPL1)):COMPL1:SPACE(6-LEN(COMPL2)):COMPL2
     PRINT @(70,8):SPACE(4-LEN(AYP1)):AYP1:SPACE(6-LEN(AYP2)):AYP2
     PRINT @(70,9):SPACE(4-LEN(PENTL1)):PENTL1:SPACE(6-LEN(PENTL2)):PENTL2
     PRINT @(70,10):SPACE(4-LEN(TOS1)):TOS1:SPACE(6-LEN(TOS2)):TOS2
     IF PERIOD=5 THEN GOTO 9998
     RETURN
998  *
     FOR I=1 TO 230
     NEXT I
     RETURN
999  *
     FOR I=1 TO 500
     NEXT I
     RETURN
1000 *
     R=RND(10)+1
     IF R<3 THEN GOTO 1010
     IF R>4 THEN GOTO 1020
1005 *
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF PYDS=0 THEN PRINT RESET:"NO GAIN - BALL AT THE ":PYD:
     IF PYDS<0 THEN PRINT RESET:"TACKLED FOR A LOSS OF ":ABS(PYDS):" BALL AT THE ":PYD:
     IF PYDS>0 THEN PRINT RESET:"TACKLED IMMEDIATELY - BALL AT THE ":PYD:" GAIN OF ":PYDS:
     YDS=0
     GOSUB 998
     GOSUB 1900
     GOTO 1090
1010 *
     YDS=-(RND(5)+1)
     GOSUB 1900
     GOSUB 600
     GOTO 1090
1020 *
     IF R>9 THEN GOTO 1030
1025 *
     YDS=RND(6)+1
     GOSUB 1900
     GOSUB 600
     GOTO 1090
1030 *
     YDS=RND(50)+1
     GOSUB 1900
     GOSUB 600
1090 *
     A=RND(18)+1
     IF A=9 THEN PRINT RESET:'BALL FUMBLED':; GOSUB 998; GOSUB 700
1095 *
     IF CHANGE=0 THEN GOSUB 2000
     RETURN
1100 *
     R=RND(10)+1
1110 *
     IF R<4 THEN GOTO 1150
     IF R>8 THEN GOSUB 1030; GOTO 1199
     IF R>5 THEN GOSUB 1025; GOTO 1199
     GOSUB 1005
     GOTO 1199
1150 *
     YDS= -(RND(12)+1)
     GOSUB 1900
     GOSUB 600
     GOSUB 1090
1199 *
     RETURN
1200 *
     LPASS=3
     R=RND(10)+1
     IF R>2 THEN X=10; GOTO 1210
     IF R>1 THEN GOTO 1205
1201 *
     YDS=-(RND(5)+1)
     YDLN=YDLN+YDS
     IF YDLN<0 THEN PL=1; GOSUB 440; GOTO 1299
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET:'SACKED AT THE ':PYD:' FOR A LOSS OF ':ABS(YDS):
     GOSUB 998
     PL=1; GOSUB 1900
     GOSUB 1090
     GOTO 1299
1205 *
     YDS=0
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET:'SACKED AT THE ':PYD:' FOR NO GAIN':
     GOSUB 998
     GOSUB 1090
     GOTO 1299
1210 *
     IF R>4 THEN GOTO 1250
     IF R=1 THEN GOTO 1220
1215 *
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET:'PASS INCOMPLETE - BALL AT THE ':PYD:
     GOSUB 998
     YDS=0; GOSUB 1900
     GOSUB 2000
     GOTO 1299
1220 *
     GOSUB 1290
     YDS=0; GOSUB 1900
     IF PYD<0 THEN PRINT RESET:"PASS INTERCEPTED IN THE END ZONE":; GOSUB 998; GOTO 1222
     PRINT RESET:'PASS INTERCEPTED AT THE ':PYD:; GOSUB 998
1222 *
     CHANGE=1
     PYDS=0
     GOSUB 1950
     GOSUB 1800
     YDLN=100-YDLN
     A=RND(5)+1
     IF PYD<0 THEN YDLN=20; PYD=20; GOTO 1223
     IF A<3 THEN GOTO 1225
1223 *
     PRINT RESET:OFF:' TAKE OVER AT THE ':PYD:; GOSUB 998; GOSUB 1090; DN=1; YTG=10; GOTO 1299
1225 *
     YDS=RND(20)+1
     PRINT RESET:"HE'S RUNNING WITH THE BALL":; GOSUB 998
     DN=1
     R=RND(2)+1
     PL=0
     IF R=1 THEN GOSUB 1000 ELSE GOSUB 1100
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     GOTO 1299
1250 *
     IF PL=6 THEN YDS=(RND(5)+1) * (-1)
     GOSUB 1290
     IF PYD<-10 THEN
       PRINT RESET:"PASS THROWN OUT OF THE END ZONE"
       GOSUB 998; YDLN=YDLN-YDS; PYDS=0; YDS=0; GOSUB 1900; GOSUB 2000; GOTO 1299
     END
     IF TMDR=1 THEN CPASS1=CPASS1+1 ELSE CPASS2=CPASS2+1
     GOSUB 1900; YDS=0
     IF PYD<0 THEN PRINT RESET:"HE CAUGHT THE PASS IN THE END ZONE!!":; GOSUB 998; GOSUB 620; GOTO 1299
     PRINT RESET:'PASS COMPLETED AT THE ':PYD:; GOSUB 998
     IF PL=6 THEN PRINT RESET:"HE HAS THE BLOCKERS"; GOSUB 998; R=RND(4)+6
     IF PL=6 THEN IF R=6 THEN GOSUB 1005; YDS=0; GOTO 1299
     IF PL=6 THEN IF R<9 THEN YDS=0
     IF PL=6 THEN GOSUB 1110; YDS=0; GOTO 1299
     GOSUB 1000
     GOTO 1299
1290 *
     A=RND(5)+1
     IF A>4 THEN YDS=-(RND(5)+1) ELSE YDS=RND(X)+1
     YDS=YDS+LPASS
     YDLN=YDLN+YDS
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF PYD=0 THEN PYD="GOAL LINE"
     PYDS=YDS
     RETURN
1299 *
     RETURN
1300 *
     LPASS=0
     R=RND(10)+1
     IF R>8 THEN GOSUB 1201; GOTO 1399
     IF R>5 THEN GOSUB 1215; GOTO 1399
     X=30; LPASS=25
     IF R<3 THEN GOSUB 1220 ELSE GOSUB 1250
1399 *
     RETURN
1400 *
     A=RND(10)+1
     IF YDLN<90 THEN GOTO 1410
     IF A<9 THEN GOTO 1450 ELSE GOTO 1470
1410 *
     IF YDLN<80 THEN GOTO 1420
     IF A<7 THEN GOTO 1450 ELSE GOTO 1470
1420 *
     IF YDLN<70 THEN GOTO 1430
     IF A<5 THEN GOTO 1450 ELSE GOTO 1470
1430 *
     IF YDLN<60 THEN GOTO 1440
     IF A<4 THEN GOTO 1450 ELSE GOTO 1470
1440 *
     IF YDLN<50 THEN GOTO 1470
     IF A>2 THEN GOTO 1470
1450 *
     IF YDLN>50 THEN PRINT RESET:"THE KICK IS UP AND":; GOSUB 998
     PRINT RESET:'THE KICK IS GOOD!!':
     IF TMDR=1 THEN TMDR=2; SCORE1=SCORE1+3 ELSE SCORE2=SCORE2+3; TMDR=1
     GOSUB 998
     DN=1; YTG=10
     GOSUB 997
     GOSUB 400
     GOSUB 997
     GOTO 1499
1470 *
     R=RND(10)+1
     IF R=5 THEN GOSUB 1700; GOTO 1499
     IF YDLN>50 THEN PRINT RESET:"THE KICK IS UP AND":; GOSUB 998
     PRINT RESET:'KICK IS NO GOOD';GOSUB 998
     YDLN=100-YDLN
     GOSUB 1800
     IF YDLN>30 THEN IF YDLN<45 THEN GOTO 1480
1475 *
     IF PYD<20 THEN YDLN=20; PYD=20
     PRINT RESET:'BALL COMES OUT TO THE ':PYD:
     DN=1; YTG=10
     GOSUB 998; GOTO 1499
1480 *
     R=RND(5)
     IF R#3 THEN GOTO 1475
     PRINT RESET:OFF:' CAUGHT THE BALL AND ARE RUNNING WITH IT':; GOSUB 998
     YDLN=RND(6)+1; DN=1
     PL=0
     GOSUB 1000
1499 *
     RETURN
1500 *
     R=RND(10)+1
     IF R=1 THEN GOSUB 1700; GOTO 1599
     CHANGE=1
     YDS=RND(20)+1
     DN=1; PYDS=0
     IF R<6 THEN GOTO 1520
     IF R<9 THEN GOTO 1540
     YDS=YDS+15
     GOTO 1550
1520 *
     YDS=YDS+40
     GOTO 1550
1540 *
     YDS=YDS+25
1550 *
     YDLN=100-(YDLN+YDS)
     GOSUB 1900
     GOSUB 1800
     IF YDLN<0 THEN PRINT RESET:"TOUCHBACK   BALL COMES OUT TO THE 20 YARD LINE":; YDLN=20; YTG=10; DN=1; GOTO 1599
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET:'KICK WAS GOOD - CAUGHT AT THE ':PYD:; GOSUB 998
     R=RND(3)+1
     YDS=0
     IF R=1 THEN GOSUB 1090; YTG=10; DN=1; GOTO 1599
     PRINT RESET:"HE'S RUNNING WITH THE BALL":; GOSUB 998
     PL=0
     IF R=2 THEN GOSUB 1100 ELSE GOSUB 1000
     YTG=10; DN=1
1599 *
     YDS=0
     RETURN
1700 *
     R=RND(3)+1
     PRINT RESET:'THE KICK WAS BLOCKED':; GOSUB 998
     A=RND(2)
     IF A=1 THEN YDS=RND(5) ELSE YDS= -RND(5)
     YDLN=YDLN+YDS
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     IF R>1 THEN GOTO 1710
     PRINT RESET:OFF:' RECOVER THEIR OWN BALL AT THE ':PYD:; GOSUB 998
     GOSUB 1900
     PYDS=YDS
     GOSUB 1000
     GOTO 1799
1710 *
     PRINT RESET:DEF:' RECOVER THE BALL AT THE ':PYD:; GOSUB 998
     GOSUB 1900
     GOSUB 1800
     YDLN=100-YDLN
     DN=1; YDS=0; PYDS=0
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     R=RND(2)
     IF R=1 THEN GOSUB 1000 ELSE GOSUB 1100
1799 *
     RETURN
1800 *
     IF TMDR=1 THEN TMDR=2 ELSE TMDR=1
     HOLD=DEF
     DEF=OFF
     OFF=HOLD
     RETURN
1900 *
     IF PL=1 THEN GOTO 1910
     IF PL=2 THEN GOTO 1910
     IF PL=3 THEN GOTO 1910
     IF PL=4 THEN GOTO 1910
     IF PL=5 THEN GOTO 1920
     IF PL=6 THEN GOTO 1920
     IF PL=7 THEN GOTO 1920
     IF PL=9 THEN GOTO 1940
     GOTO 1999
1910 *
     IF (YDLN+YDS)>100 THEN TEMP=100-YDLN ELSE TEMP=YDS
     IF (YDLN+YDS)<0 THEN TEMP=YDLN*(-1)
     IF TMDR=1 THEN RUSH1=RUSH1+TEMP ELSE RUSH2=RUSH2+TEMP
     GOTO 1925
1920 *
     IF (YDLN+YDS)>100 THEN TEMP=100-YDLN ELSE TEMP=YDS
     IF (YDLN+YDS)<0 THEN TEMP=YDLN*(-1)
     IF TMDR=1 THEN PASS1=PASS1+TEMP; NPASS1=NPASS1+1 ELSE PASS2=PASS2+TEMP; NPASS2=NPASS2+1
     IF NPASS1#0 THEN COMPL1=INT((CPASS1*100)/NPASS1)
     IF NPASS2#0 THEN COMPL2=INT((CPASS2*100)/NPASS2)
1925 *
     TOFF1=RUSH1+PASS1
     TOFF2=RUSH2+PASS2
     GOTO 1999
1930 *
     IF TMDR=1 THEN TDNS1=TDNS1+1 ELSE TDNS2=TDNS2+1
     GOTO 1999
1940 *
     IF (YDLN+YDS)>100 THEN TEMP=100-YDLN ELSE TEMP=YDS
     IF TMDR=1 THEN NPUNT1=NPUNT1+1; YPUNT1=YPUNT1+TEMP ELSE NPUNT2=NPUNT2+1; YPUNT2=YPUNT2+TEMP
     IF NPUNT1#0 THEN AYP1=INT(YPUNT1/NPUNT1)
     IF NPUNT2#0 THEN AYP2=INT(YPUNT2/NPUNT2)
     GOTO 1999
1950 *
     IF TMDR=1 THEN TOS1=TOS1+1 ELSE TOS2=TOS2+1
1999 *
     RETURN
2000 *
     DN=DN+1
     YDS=YDS+PYDS
     IF DN<5 THEN GOTO 2001
     IF YDS>=YTG THEN GOTO 2001
     GOSUB 1800
     YDLN=100-YDLN
     IF YDLN>50 THEN PYD=100-YDLN ELSE PYD=YDLN
     PRINT RESET:OFF:' TAKE OVER AT THE ':PYD:; GOSUB 998
     GOSUB 998
     DN=1
     GOSUB 1930
     IF YDLN>90 THEN YTG=100-YDLN ELSE YTG=10
     GOTO 2002
2001 *
     IF YDS>YTG THEN IF YDLN<90 THEN YTG=10; DN=1; GOSUB 1930; GOTO 2002
     IF YDS>YTG THEN IF YDLN>90 THEN YTG=100-YDLN; DN=1; GOSUB 1930; GOTO 2002
     YTG=YTG-YDS
     IF YTG<1 THEN IF YDLN<90 THEN YTG=10; DN=1; GOSUB 1930 ELSE YTG=100-YDLN
2002 *
     RETURN
2100 *
     IF DN=4 THEN IF YDLN>60 THEN PL=8; GOTO 2110
     IF DN=4 THEN PL=9; GOTO 2110
     IF DN=2 THEN IF YTG<4 THEN IF YDLN>30 THEN IF YDLN<70 THEN PL=7; GOTO 2110
     IF DN=3 THEN IF YTG<4 THEN PL=1; GOTO 2110
     IF YDLN<35 THEN PL=RND(6)+1; GOTO 2110
     IF YDLN>34 THEN IF YDLN<70 THEN PL=RND(7)+1; GOTO 2110
     IF YDLN>69 THEN IF YDLN<92 THEN PL=RND(5)+1; GOTO 2110
     PL=1
2110 *
     IF PL=1 THEN MESS=' TRY A SIMPLE DRAW PLAY'
     IF PL=2 THEN MESS=" QUARTERBACK CALLS THE END SWEEP"
     IF PL=3 THEN MESS=" RUN THE TRICKEY REVERSE"
     IF PL=4 THEN MESS=" TRY TO RUN THE QUARTERBACK OPTION"
     IF PL=5 THEN MESS=" THROWING A SHORT PASS"
     IF PL=6 THEN MESS=" QUARTERBACK DROPS BACK FOR A SCREEN PASS"
     IF PL=7 THEN MESS=" GO FOR THE LONG BOMB"
     IF PL=8 THEN MESS=" TRY FOR A FIELD GOAL"
     IF PL=9 THEN MESS=" ARE IN PUNT FORMATION"
     PRINT RESET:OFF:MESS:
     GOSUB 999
     GOTO 265
2200 *
     PRINT CLR:@(24,0):"*** FOOTBALL ***":@(70,0):"HOME  AWAY":@(70,1):"----  ----":
     PRINT @(2,2):"DOWN  YDS TO GO  QTR  TIME  HOME-TEAM  AWAY-TEAM       YDS RUSHING":
     PRINT @(2,3):"----  ---------  ---  ----  ":TEAM1:SPACE(11-LEN(TEAM1)):TEAM2:@(57,3):"YDS PASSING":
     PRINT @(4,4):"0       00      0  00:00":@(34,5):"00":@(45,5):"00":
     PRINT @(30,4):"---------  ---------       TOTAL OFFENSE":@(57,5):"TOTAL # PLAYS":
     PRINT @(57,6):"FIRST DOWNS":@(57,7):"% PASS COMPL":
     PRINT @(3,8):"G....1....2....3....4....5....4....3....2....1....G   AVE YDS/PUNT":
     PRINT @(57,9):"PENATLIES":@(57,10):"TURNOVERS":
     PRINT @(3,23):"G....1....2....3....4....5....4....3....2....1....G":
     PRINT @(58,12):"OFFENSIVE PLAYS":@(60,13):"1=DRAW PLAY":@(60,14):"2=END SWEEP":@(60,15):"3=REVERSE":
     PRINT @(60,16):"4=QTRBACK OPTION":@(60,17):"5=SHORT PASS":@(60,18):"6=SCREEN PASS":@(60,19):"7=LONG PASS":
     PRINT @(60,20):"8=FIELD GOAL":@(60,21):"9=PUNT":@(58,22):"ENTER A PLAY":
     RETURN
9998 *
     PRINT RESET:'TIME RAN OUT! - DO YOU WANT TO PLAY AGAIN(Y/N)?':
     INPUT A
     IF A EQ 'Y' THEN GOTO 100
     IF A NE 'N' THEN GOTO 9998
END
