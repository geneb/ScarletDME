*REVERSE GAME OF SKILL
100  PRINT
110  PRINT "REVERSE -- A GAME OF SKILL"
120  PRINT
130  DIM A(20)
140  REM *** N=NUMBER OF NUMBERS
150  N=9
160  PRINT "DO YOU WANT THE RULES":
170  INPUT A$
180  IF A$[1,1]="Y" THEN GOSUB 710
200  REM *** MAKE A RANDOM LIST A(1) TO A(N)
210  A(1)=RND(N)+1
220  FOR K=2 TO N
230    A(K)=RND(9)+1
240    FOR J=1 TO K-1
250      IF A(K)=A(J) THEN GOTO 230
260    NEXT J
270  NEXT K
280  REM *** PRINT ORIGINAL LIST AND START GAME
290  PRINT
300  PRINT "HERE WE GO ... THE LIST IS:"
310  T=0
320  GOSUB 610
330  PRINT "HOW MANY SHALL I REVERSE":
340  INPUT R
350  IF R=0 THEN GOTO 520
360  IF R<=N THEN GOTO 390
370  PRINT "OOPS!  TOO MANY - I CAN REVERSE AT MOST ":N
380  GOTO 330
390  T=T+1
400  REM *** REVERSE R NUMBERS AND PRINT NEW LIST
410  FOR K=1 TO INT(R/2)
420    Z=A(K)
430    A(K)=A(R-K+1)
440    A(R-K+1)=Z
450  NEXT K
460  GOSUB 610
470  REM *** CHECK FOR A WIN
480  FOR K=1 TO N
490    IF A(K)<>K THEN GOTO 330
500  NEXT K
510  PRINT "YOU WON IN ":T:" MOVES !!!"
520  PRINT
530  PRINT "TRY AGAIN":
540  INPUT A$
550  IF A$[1,1]="Y" THEN GOTO 210
555  PRINT
560  PRINT "O.K.  HOPE YOU HAD FUN!"
     STOP ;* NORMAL EXIT. ONLY WAY
600  REM *** SUBROUTINE TO PRINT LIST
610  PRINT
620  FOR K=1 TO N
630    PRINT A(K):" ":
640  NEXT K
650  PRINT
660  PRINT
670  RETURN
700  REM *** SUBROUTINE TO PRINT THE RULES
710  PRINT
720  PRINT "THIS IS THE GAME OF 'REVERSE'.  TO WIN, ALL YOU HAVE"
730  PRINT "TO DO IS ARRANGE A LIST OF NUMBERS (1 THROUGH ":N:")"
740  PRINT "IN NUMERICAL ORDER FROM LEFT TO RIGHT.  TO MOVE, YOU"
750  PRINT "TELL ME HOW MANY NUMBERS (COUNTING FROM THE LEFT) TO"
760  PRINT "REVERSE.  FOR EXAMPLE, IF THE CURRENT LIST IS:"
770  PRINT
780  PRINT "2 3 4 5 1 6 7 8 9"
790  PRINT
800  PRINT "AND YOU REVERSE 4, THE RESULT WILL BE:"
810  PRINT
820  PRINT "5 4 3 2 1 6 7 8 9"
830  PRINT
840  PRINT "NOW, IF YOU REVERSE 5, YOU WIN!"
850  PRINT
860  PRINT "1 2 3 4 5 6 7 8 9"
870  PRINT
880  PRINT "NO DOUBT YOU WILL LIKE THIS GAME OF SKILL, BUT"
890  PRINT "IF YOU WANT TO QUIT, REVERSE 0 (ZERO)."
920  RETURN
END
